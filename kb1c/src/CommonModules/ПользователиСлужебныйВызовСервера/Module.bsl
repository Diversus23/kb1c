////////////////////////////////////////////////////////////////////////////////
// Подсистема "Пользователи".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Только для внутреннего использования.
Функция ПроверитьВозможностьЗапускаСПустымСпискомПользователейИБ(ВернутьТекстОшибки = Ложь) Экспорт
	
	Возврат ПользователиСлужебный.ПроверитьВозможностьЗапускаСПустымСпискомПользователейИБ(
		ВернутьТекстОшибки);
	
КонецФункции

// Только для внутреннего использования.
Процедура СоздатьПервогоАдминистратораСистемы() Экспорт
	
	ТекстОшибки = ПользователиСлужебный.ПроверитьВозможностьЗапускаСПустымСпискомПользователейИБ(Истина);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		УстановитьПривилегированныйРежим(Истина);
		
		Администратор = ПользователиИнформационнойБазы.СоздатьПользователя();
		Администратор.Имя = НСтр("ru = 'Администратор'");
		Администратор.ПолноеИмя = Администратор.Имя;
		Администратор.АутентификацияСтандартная = Истина;
		Администратор.ПоказыватьВСпискеВыбора   = Истина;
		Администратор.Роли.Добавить(Метаданные.Роли.ПолныеПрава);
		Администратор.Роли.Добавить(Пользователи.РольАдминистратораСистемы());
		Администратор.Записать();
		
		ПользователиСлужебный.УстановитьНачальныеНастройки(Администратор.Имя);
		
	КонецЕсли;
	
КонецПроцедуры
