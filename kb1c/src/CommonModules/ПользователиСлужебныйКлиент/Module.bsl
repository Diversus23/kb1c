////////////////////////////////////////////////////////////////////////////////
// Подсистема "Пользователи".
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// Для работы интерфейса ролей в управляемой форме

// Только для внутреннего использования
//
Процедура РазвернутьПодсистемыРолей(Форма, Безусловно = Истина) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если НЕ Безусловно
	   И НЕ Элементы.РолиПоказатьТолькоВыбранныеРоли.Пометка Тогда
		
		Возврат;
	КонецЕсли;
	
	// Развернуть все.
	Для каждого Строка Из Форма.Роли.ПолучитьЭлементы() Цикл
		Элементы.Роли.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Выполняет попытку авторизации пользователя и проверку результата.
Процедура ПроверитьАвторизациюПользователя(Отказ = Истина) Экспорт
	
	ОшибкаАвторизации = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске().ОшибкаАвторизации;
	Если ОшибкаАвторизации = "" Тогда
		Возврат;
	ИначеЕсли ТипЗнч(ОшибкаАвторизации) = Тип("ФиксированнаяСтруктура") Тогда
		Если ЗначениеЗаполнено(ОшибкаАвторизации.ДополнительныеПараметрыКоманднойСтроки) Тогда
			ПрекратитьРаботуСистемы(Истина, ОшибкаАвторизации.ДополнительныеПараметрыКоманднойСтроки);
		Иначе
			ПрекратитьРаботуСистемы(Истина);
		КонецЕсли;
	КонецЕсли;
		
	Отказ = Истина;
	ТекстОшибки = ПользователиСлужебныйВызовСервера.ПроверитьВозможностьЗапускаСПустымСпискомПользователейИБ(Истина);
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Предупреждение(ОшибкаАвторизации);
		Возврат;
	КонецЕсли;
	
	СоздатьАдминистратораИлиЗавершитьРаботу(ТекстОшибки);
	
КонецПроцедуры

// Предлагает создать администратора или завершить работу.
Процедура СоздатьАдминистратораИлиЗавершитьРаботу(ТекстОшибки) Экспорт
	
	ТекстВопроса =
		НСтр("ru = 'Запуск с пустым списком пользователей информационной базы невозможен.
		           |Добавить пользователя Администратор и перезапустить программу?
		           |
		           |Пояснение: также можно завершить работу и вручную добавить в список
		           |пользователей информационной базы учетную запись администратора с двумя ролями
		           |""Администратор системы"", ""Полные права"" и повторить запуск программы от его имени.
		           |
		           |Для перехода к списку пользователей информационной базы необходимо открыть
		           |Конфигуратор и воспользоваться пунктом меню ""Администрирование - Пользователи"".'");
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("СоздатьИПерезапустить", НСтр("ru = 'Добавить и перезапустить'"));
	Кнопки.Добавить("ЗавершитьРаботу",       НСтр("ru = 'Завершить работу'"));
	
	Ответ = Вопрос(
		ТекстВопроса,
		Кнопки,
		,
		"СоздатьИПерезапустить",
		НСтр("ru = 'Авторизация пользователя'") );
		
	Если Ответ = "СоздатьИПерезапустить" Тогда
		ПользователиСлужебныйВызовСервера.СоздатьПервогоАдминистратораСистемы();
		ПрекратитьРаботуСистемы(Истина);
	КонецЕсли;
	
КонецПроцедуры

