////////////////////////////////////////////////////////////////////////////////////////////////////
// Подсистема "Контактная информация"
// 
////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Устарело. Необходимо использовать УправлениеКонтактнойИнформацией.ПредыдущийФорматКонтактнойИнформацииXML
//
Функция ПредыдущийФорматКонтактнойИнформацииXML(Знач Данные, Знач СтарыйСоставПолей=Ложь) Экспорт
	Возврат УправлениеКонтактнойИнформацией.ПредыдущийФорматКонтактнойИнформацииXML(Данные, СтарыйСоставПолей);
КонецФункции

// Устарело. Необходимо использовать УправлениеКонтактнойИнформацией.ПредыдущаяСтруктураКонтактнойИнформацииXML
//
Функция ПредыдущаяСтруктураКонтактнойИнформацииXML(Знач Данные, Знач ВидКонтактнойИнформации=Неопределено) Экспорт
	Возврат УправлениеКонтактнойИнформацией.ПредыдущаяСтруктураКонтактнойИнформацииXML(Данные, ВидКонтактнойИнформации);
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС

//  Возвращает структуру с полем "ДанныеВыбора", содержащую список для автоподбора населенного пункта 
//  по иерархическому представлению младший-старший
//
//  Параметры:
//      Текст                      - строка автоподбора
//      СкрыватьНеактуальныеАдреса - флаг того, что неактуальные адреса не должны попадать в автоподбор
//      ПредупреждатьОНеактуальных - флаг структуры результата. Истина - возвращаемый список значений 
//                                   состоит из структур с предупреждением о неактуальности. 
//                                   Ложь - обычный список значений.
//
Функция РезультатыАвтоПодбораНаселенногоПункта(Текст, СкрыватьНеактуальныеАдреса=Ложь, ПредупреждатьОНеактуальных=Истина) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Ложь, Новый СписокЗначений);
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат РезультатыАвтоПодбораНаселенногоПунктаКЛАДР(Текст, СкрыватьНеактуальныеАдреса, ПредупреждатьОНеактуальных, 50);
	КонецЕсли;
	
	Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Ложь, Новый СписокЗначений);
КонецФункции

//  Возвращает структуру с полем "ДанныеВыбора", содержащую список для автоподбора улиц
//  по иерархическому представлению младший-старший
//
//  Параметры:
//      КодНаселенногоПункта       - код классификатора для ограничения подбора
//      Текст                      - строка автоподбора
//      СкрыватьНеактуальныеАдреса - флаг того, что неактуальные адреса не должны попадать в автоподбор
//      ПредупреждатьОНеактуальных - флаг структуры результата. Истина - возвращаемый список значений 
//                                   состоит из структур с предупреждением о неактуальности. 
//                                   Ложь - обычный список значений.
//
Функция РезультатыАвтоПодбораУлицы(КодНаселенногоПункта, Текст, СкрыватьНеактуальныеАдреса=Ложь, ПредупреждатьОНеактуальных=Истина) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Ложь, Новый СписокЗначений);
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат РезультатыАвтоПодбораУлицыКЛАДР(КодНаселенногоПункта, Текст, СкрыватьНеактуальныеАдреса, ПредупреждатьОНеактуальных, 50);
	КонецЕсли;
	
	Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Ложь, Новый СписокЗначений);
КонецФункции

//  Возвращает структуру с полем "ДанныеВыбора", содержащую список вариантов населенных пунктов 
//  по иерархическому представлению младший-старший
//
//  Параметры:
//      Текст                      - строка автоподбора
//      СкрыватьНеактуальныеАдреса - флаг того, что неактуальные адреса не должны попадать в автоподбор
//      ВыбиратьСтрок              - ограничение на количество результатов
//
Функция НаселенныеПунктыПоПредставлению(Текст, СкрыватьНеактуальныеАдреса=Ложь, ВыбиратьСтрок=50) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Ложь, Новый СписокЗначений);
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат НаселенныеПунктыПоПредставлениюКЛАДР(Текст, СкрыватьНеактуальныеАдреса, ВыбиратьСтрок);
	КонецЕсли;
	
	Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Ложь, Новый СписокЗначений);
КонецФункции

//  Возвращает структуру с полем "ДанныеВыбора", содержащую список вариантов населенных пунктов 
//  по иерархическому представлению младший-старший
//
//  Параметры:
//      КодНаселенногоПункта       - код классификатора для ограничения подбора
//      Текст                      - строка автоподбора
//      СкрыватьНеактуальныеАдреса - флаг того, что неактуальные адреса не должны попадать в автоподбор
//      ВыбиратьСтрок              - ограничение на количество результатов
//
Функция УлицыПоПредставлению(КодНаселенногоПункта, Текст, СкрыватьНеактуальныеАдреса=Ложь, ВыбиратьСтрок=50) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Ложь, Новый СписокЗначений);
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат УлицыПоПредставлениюКЛАДР(КодНаселенногоПункта, Текст, СкрыватьНеактуальныеАдреса, ВыбиратьСтрок);
	КонецЕсли;
	
	Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Ложь, Новый СписокЗначений);    
КонецФункции

//  Возвращает варианты типов домов (по признаку владения)
Функция ВариантыДанныхДом() Экспорт
	Возврат Новый Структура("ВариантыТипа, МожноПодбиратьЗначения", 
		КонтактнаяИнформацияКлиентСерверПовтИсп.НаименованияОбъектовАдресацииПоТипу(1), Ложь);
КонецФункции

//  Возвращает варианты типов домов (по признаку строения)
Функция ВариантыДанныхСтроение() Экспорт
	Возврат Новый Структура("ВариантыТипа, МожноПодбиратьЗначения", 
		КонтактнаяИнформацияКлиентСерверПовтИсп.НаименованияОбъектовАдресацииПоТипу(2), Ложь);
КонецФункции

//  Возвращает варианты типов помещений
Функция ВариантыДанныхПомещение() Экспорт
	Возврат Новый Структура("ВариантыТипа, МожноПодбиратьЗначения", 
		КонтактнаяИнформацияКлиентСерверПовтИсп.НаименованияОбъектовАдресацииПоТипу(3, Ложь), Ложь);
КонецФункции

//  Возвращает флаг вхождения объекта в состав родителя
//
//  Параметры:
//      КодОбъекта  - код классификатора проверяемого объекта
//      КодРодителя - код классификатора родителя
//
Функция ЯвляетсяПотомком(КодОбъекта, КодРодителя) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Неопределено;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат ЯвляетсяПотомкомКЛАДР(КодОбъекта, КодРодителя);
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

//  Возвращает код региона по его полному наименованию
//
//  Параметры:
//      НаименованиеРегиона - Строка - полное наименование региона с сокращением
//
// Возвращаемое значение:
//      Строка - код региона из двух цифр. Пустая строка, если наименование определить не удалось
//      Неопределено - если нет ни одной подсистемы адресного классификатора
// 
Функция КодРегиона(Знач ПолноеНаименование) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Неопределено;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат КодРегионаКЛАДР(ПолноеНаименование);
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

//  Возвращает наименование региона по его коду
//
//  Параметры:
//      Код - Строка, Число - код региона
//
// Возвращаемое значение:
//      Строка - полное наименование региона с сокращением
//      Неопределено - если нет ни одной подсистемы адресного классификатора
// 
Функция РегионКода(Знач Код) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Неопределено;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат РегионКодаКЛАДР(Код);
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

//  Получает список всех регионов.
//
// Возвращаемое значение:
//      ТаблицаЗначений с колонками Код, Наименование, Сокращение, Представление
//      Неопределено - если нет ни одной подсистемы адресного классификатора
// 
Функция ВсеРегионы() Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Неопределено;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат ВсеРегионыКЛАДР();
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции


//  Возвращает структуру, описывающую населенный пункт в иерархии младший-старший
//  для текущего адресного классификатора. Имена ключей структуры зависят от 
//  классификатора
//
//  Параметры:
//      Код - необязательный код классификатора объекта. Если указан, то структура 
//            заполняется данными для этого объекта
//
Функция СписокРеквизитовНаселенныйПункт(Код=Неопределено) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Новый Структура;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат СписокРеквизитовНаселенныйПунктКЛАДР(Код);
	КонецЕсли;
	
	Возврат Новый Структура;
КонецФункции

//  Возвращает структуру, описывающую улицу в иерархии младший-старший
//  для текущего адресного классификатора. Имена ключей структуры зависят от 
//  классификатора
//
//  Параметры:
//      Код - необязательный код классификатора объекта. Если указан, то структура 
//            заполняется данными для этого объекта
//
Функция СписокРеквизитовУлица(Код=Неопределено) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Новый Структура;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат СписокРеквизитовУлицаКЛАДР(Код);
	КонецЕсли;
	
	Возврат Новый Структура;
КонецФункции

//  Возвращает код классификатора для объекта, представленного в полях.
//
//  Параметры:
//      ЧастиАдреса          - структура, зависимая от классификатора
//      СкрыватьНеактуальные - флаг скрытия актуальных
//
Функция КодНаселенногоПунктаПоЧастямАдреса(ЧастиАдреса, СкрыватьНеактуальные=Ложь) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Неопределено;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат КодОбъектаПоЧастямАдресаКЛАДР(ЧастиАдреса, СкрыватьНеактуальные);
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

//  Возвращает список для автоподбора элемента адреса, поиск по подобию. Список ограничен 50 записями
//
//  Параметры:
//      Текст (Строка)             - текст, введенный в поле
//      КодЧастиАдреса             - идентификатор обрабатываемой части адреса, зависит от классификатора
//      ЧастиАдреса                - значения для других частей адреса, зависит от классификатора
//      ПредупреждатьОНеактуальных - флаг структуры списка. 
//                                   Истина - в списке содержатся структуры с предупреждением о неактуальности
//                                   Ложь - обычный список значений
//
Функция СписокАвтоПодбораЭлементаАдреса(Текст, КодЧастиАдреса, ЧастиАдреса, ПредупреждатьОНеактуальных) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Новый СписокЗначений;    
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат СписокАвтоПодбораЭлементаАдресаКЛАДР(Текст, КодЧастиАдреса, ЧастиАдреса, ПредупреждатьОНеактуальных, 50);
	КонецЕсли;
	
	Возврат Новый СписокЗначений;
КонецФункции

//  Возвращает список вариантов для окончательно введенного текста элемента адреса
//
//  Параметры:
//      Текст          - текст, введенный в поле
//      КодЧастиАдреса - идентификатор обрабатываемой части адреса, зависит от классификатора
//      ЧастиАдреса    - значения для других частей адреса, зависит от классификатора
//      ВыбиратьСтрок  - ограничитель на возвращаемое число строк
//
Функция СписокВариантовЭлементаАдресаПоТексту(Текст, КодЧастиАдреса, ЧастиАдреса, ВыбиратьСтрок=50) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Новый СписокЗначений;    
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат СписокВариантовЭлементаАдресаПоТекстуКЛАДР(Текст, КодЧастиАдреса, ЧастиАдреса, ВыбиратьСтрок);
	КонецЕсли;
	
	Возврат Новый СписокЗначений;    
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////
// Общее служебное
//

// Преобразует контактную информацию XDTO в XML, возвращает строку
//
//  Параметры:
//      ОбъектXDTOИнформации - объект XDTO контактной информации
//
Функция СериализацияКонтактнойИнформации(ОбъектXDTOИнформации) Экспорт
	Запись = Новый ЗаписьXML;
	Запись.УстановитьСтроку(Новый ПараметрыЗаписиXML(, , Ложь, Ложь, ""));
	
	Если ОбъектXDTOИнформации<>Неопределено Тогда
		ФабрикаXDTO.ЗаписатьXML(Запись, ОбъектXDTOИнформации);
	КонецЕсли;
	
	Возврат СтрЗаменить(Запись.Закрыть(), Символы.ПС, "&#10;");
КонецФункции

// Преобразует XML в объект XDTO контактной информации
//
//  Параметры:
//      Текст        - строка XML
//      ОжидаемыйВид - необязательная ссылка на КИ, перечисление типов КИ, или объект с полем "Тип"
//
//      РезультатыЧтения - если параметр указан, то в нем ожидается структура, куда будут внесены дополнительные
//                         необязательные поля:
//                          - ТекстОшибки - описание ошибок процесса чтения. Возвращаемый функцией результат будет 
//                                          корректного типа, но не заполнен.
//
Функция ДесериализацияКонтактнойИнформации(Знач Текст, Знач ОжидаемыйВид=Неопределено, РезультатыЧтения=Неопределено) Экспорт
	
	ОжидаемыйТип = УправлениеКонтактнойИнформацией.ТипВидаКонтактнойИнформации(ОжидаемыйВид);
	
	ПеречислениеАдрес                 = Перечисления.ТипыКонтактнойИнформации.Адрес;
	ПеречислениеАдресЭлектроннойПочты = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
	ПеречислениеВебСтраница           = Перечисления.ТипыКонтактнойИнформации.ВебСтраница;
	ПеречислениеТелефон               = Перечисления.ТипыКонтактнойИнформации.Телефон;
	ПеречислениеФакс                  = Перечисления.ТипыКонтактнойИнформации.Факс;
	ПеречислениеДругое                = Перечисления.ТипыКонтактнойИнформации.Другое;
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	Если КонтактнаяИнформацияКлиентСервер.ЭтоКонтактнаяИнформацияВXML(Текст) Тогда
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(Текст);
		
		ТекстОшибки = Неопределено;
		Попытка
			Результат = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
		Исключение
			// Некорректный формат XML
			ЗаписьЖурналаРегистрации(КонтактнаяИнформацияСлужебныйПовтИсп.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка, , Текст, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())
			);
			
			Если ТипЗнч(ОжидаемыйВид)=Тип("СправочникСсылка.ВидыКонтактнойИнформации") Тогда
				ТекстОшибки = СтрЗаменить(НСтр("ru='Некорректный формат XML контактной информации для ""%1"", значения полей были очищены.'"),
					"%1", Строка(ОжидаемыйВид));
			Иначе
				ТекстОшибки = НСтр("ru='Некорректный формат XML контактной информации, значения полей были очищены.'");
			КонецЕсли;
		КонецПопытки;
		
		Если ТекстОшибки=Неопределено Тогда
			// Контролируем соответствие типов
			НайденТип = ?(Результат.Состав=Неопределено, Неопределено, Результат.Состав.Тип());
			Если ОжидаемыйТип=ПеречислениеАдрес И НайденТип<>ФабрикаXDTO.Тип(ПространствоИмен, "Адрес") Тогда
				ТекстОшибки = НСтр("ru='Ошибка десериализации контактной информации, ожидается адрес'");
			ИначеЕсли ОжидаемыйТип=ПеречислениеАдресЭлектроннойПочты И НайденТип<>ФабрикаXDTO.Тип(ПространствоИмен, "ЭлектроннаяПочта") Тогда
				ТекстОшибки = НСтр("ru='Ошибка десериализации контактной информации, ожидается адрес электронной почты'");
			ИначеЕсли ОжидаемыйТип=ПеречислениеВебСтраница И НайденТип<>ФабрикаXDTO.Тип(ПространствоИмен, "ВебСайт") Тогда
				ТекстОшибки = НСтр("ru='Ошибка десериализации контактной информации, ожидается веб-страница'");
			ИначеЕсли ОжидаемыйТип=ПеречислениеТелефон И НайденТип<>ФабрикаXDTO.Тип(ПространствоИмен, "НомерТелефона") Тогда
				ТекстОшибки = НСтр("ru='Ошибка десериализации контактной информации, ожидается телефон'");
			ИначеЕсли ОжидаемыйТип=ПеречислениеФакс И НайденТип<>ФабрикаXDTO.Тип(ПространствоИмен, "НомерФакса") Тогда
				ТекстОшибки = НСтр("ru='Ошибка десериализации контактной информации, ожидается телефон'");
			ИначеЕсли ОжидаемыйТип=ПеречислениеДругое И НайденТип<>ФабрикаXDTO.Тип(ПространствоИмен, "Прочее") Тогда
				ТекстОшибки = НСтр("ru='Ошибка десериализации контактной информации, ожидается ""другое""'");
			КонецЕсли;
		КонецЕсли;
		
		Если ТекстОшибки=Неопределено Тогда
			// Успешно прочитано
			Возврат Результат;
		КонецЕсли;
		
		// Проверим ошибку и вернем расширенную информацию
		Если РезультатыЧтения=Неопределено Тогда
			ВызватьИсключение ТекстОшибки;
		ИначеЕсли ТипЗнч(РезультатыЧтения)<>Тип("Структура") Тогда
			РезультатыЧтения = Новый Структура;
		КонецЕсли;
		РезультатыЧтения.Вставить("ТекстОшибки", ТекстОшибки);
		
		// Будет возвращен пустой объект
		Текст = "";
	КонецЕсли;
	
	Если ТипЗнч(Текст)=Тип("СписокЗначений") Тогда
		Представление = "";
		ЭтоНовый = Текст.Количество()=0;
	Иначе
		Представление = Строка(Текст);
		ЭтоНовый = ПустаяСтрока(Текст);
	КонецЕсли;
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
	
	// Разбор
	Если ОжидаемыйТип=ПеречислениеАдрес Тогда
		Если ЭтоНовый Тогда
			Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Адрес"));
		Иначе
			Результат = ДесериализацияАдреса(Текст, Представление, ОжидаемыйТип);
		КонецЕсли;
		
	ИначеЕсли ОжидаемыйТип=ПеречислениеТелефон Тогда
		Если ЭтоНовый Тогда
			Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "НомерТелефона"));
		Иначе
			Результат = ДесериализацияТелефона(Текст, Представление, ОжидаемыйТип)
		КонецЕсли;
		
	ИначеЕсли ОжидаемыйТип=ПеречислениеФакс Тогда
		Если ЭтоНовый Тогда
			Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "НомерФакса"));
		Иначе
			Результат = ДесериализацияФакса(Текст, Представление, ОжидаемыйТип)
		КонецЕсли;
		
	ИначеЕсли ОжидаемыйТип=ПеречислениеАдресЭлектроннойПочты Тогда
		Если ЭтоНовый Тогда
			Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "ЭлектроннаяПочта"));
		Иначе
			Результат = ДесериализацияПрочейКонтактнойИнформации(Текст, Представление, ОжидаемыйТип)
		КонецЕсли;
		
	ИначеЕсли ОжидаемыйТип=ПеречислениеВебСтраница Тогда
		Если ЭтоНовый Тогда
			Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "ВебСайт"));
		Иначе
			Результат = ДесериализацияПрочейКонтактнойИнформации(Текст, Представление, ОжидаемыйТип)
		КонецЕсли;
		
	ИначеЕсли ОжидаемыйТип=ПеречислениеДругое Тогда
		Если ЭтоНовый Тогда
			Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Прочее"));
		Иначе
			Результат = ДесериализацияПрочейКонтактнойИнформации(Текст, Представление, ОжидаемыйТип)    
		КонецЕсли;
		
	Иначе
		ВызватьИсключение НСтр("ru='Ошибка десериализации контактной информации, не указан ожидаемый тип'");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Разбирает представление КИ и возвращает XDTO
//
//  Параметры:
//      Текст        - строка XML
//      ОжидаемыйТип - ссылка на вид КИ или перечисление типа КИ для контроля типов,
//                     объект с полем "Тип"
//
Функция ПарсингКонтактнойИнформации(Текст, ОжидаемыйВид) Экспорт
	
	ОжидаемыйТип = УправлениеКонтактнойИнформацией.ТипВидаКонтактнойИнформации(ОжидаемыйВид);
	
	Если ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
		Возврат ДесериализацияАдреса("", Текст, ОжидаемыйТип);
		
	ИначеЕсли ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
		Возврат ДесериализацияПрочейКонтактнойИнформации("", Текст, ОжидаемыйТип);
		
	ИначеЕсли ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.ВебСтраница Тогда
		Возврат ДесериализацияПрочейКонтактнойИнформации("", Текст, ОжидаемыйТип);
		
	ИначеЕсли ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		Возврат ДесериализацияТелефона("", Текст, ОжидаемыйТип);
		
	ИначеЕсли ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.Факс Тогда
		Возврат ДесериализацияФакса("", Текст, ОжидаемыйТип);
		
	ИначеЕсли ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.Другое Тогда
		Возврат ДесериализацияПрочейКонтактнойИнформации("", Текст, ОжидаемыйТип);
		
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Разбирает представление КИ и возвращает строку XML
//
//  Параметры:
//      Текст        - строка XML
//      ОжидаемыйТип - ссылка на вид КИ или перечисление типа КИ для контроля типов
//
Функция ПарсингКонтактнойИнформацииXML(Текст, ОжидаемыйВид) Экспорт
	Возврат СериализацияКонтактнойИнформации(
		ПарсингКонтактнойИнформации(Текст, ОжидаемыйВид));
КонецФункции

// Преобразует строку в XDTO контактную информацию адреса
//
//  Параметры:
//      ЗначенияПолей - строка с сериализованной информацией, значения полей
//      Представление - представление старший-младший, используется для попытки разбора, 
//                      если ЗначенияПолей пусто
//      ОжидаемыйТип  - необязательный тип для контроля
//
//  Возвращаемое значение
//      ОбъектXDTO контактной информации.
//
Функция ДесериализацияАдреса(Знач ЗначенияПолей, Знач Представление="", Знач ОжидаемыйТип=Неопределено) Экспорт
	ТипЗначения = ТипЗнч(ЗначенияПолей);
	
	РазбиратьПоПолям = ТипЗначения=Тип("СписокЗначений") Или ТипЗначения=Тип("Структура") 
	                   Или ( ТипЗначения=Тип("Строка") И Не ПустаяСтрока(ЗначенияПолей) );
	
	Если РазбиратьПоПолям Тогда
		// Разбираем из значений полей
		Возврат ДесериализацияАдресаОбщая(ЗначенияПолей, Представление, ОжидаемыйТип);
	КонецЕсли;
	
	// Разбираем из представления по классификаторам
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат ДесериализацияАдресаПоПредставлениюКЛАДР(Представление);
	КонецЕсли;
	
	// Пустой объект с представлением
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
	Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Адрес"));
	Результат.Представление = Представление;
	
	Возврат Результат;
КонецФункции  

// Преобразует строку в XDTO контактную информацию телефона
//
//  Параметры:
//      ЗначенияПолей - строка с сериализованной информацией, значения полей
//      Представление - представление старший-младший, используется для попытки разбора, 
//                      если ЗначенияПолей пусто
//      ОжидаемыйТип  - необязательный тип для контроля
//
Функция ДесериализацияТелефона(ЗначенияПолей, Представление="", ОжидаемыйТип=Неопределено) Экспорт
	Возврат ДесериализацияТелефонаФакса(ЗначенияПолей, Представление, ОжидаемыйТип);
КонецФункции

// Преобразует строку в XDTO контактную информацию Факса
//
//  Параметры:
//      ЗначенияПолей - строка с сериализованной информацией, значения полей
//      Представление - представление старший-младший, используется для попытки разбора, 
//                      если ЗначенияПолей пусто
//      ОжидаемыйТип  - необязательный тип для контроля
//
Функция ДесериализацияФакса(ЗначенияПолей, Представление="", ОжидаемыйТип=Неопределено) Экспорт
	Возврат ДесериализацияТелефонаФакса(ЗначенияПолей, Представление, ОжидаемыйТип);
КонецФункции

// Преобразует строку в XDTO прочую контактную информацию 
//
//  Параметры:
//      ЗначенияПолей - строка с сериализованной информацией, значения полей
//      Представление - представление старший-младший, используется для попытки разбора, 
//                      если ЗначенияПолей пусто
//      ОжидаемыйТип  - необязательный тип для контроля
//
Функция ДесериализацияПрочейКонтактнойИнформации(ЗначенияПолей, Представление="", ОжидаемыйТип=Неопределено) Экспорт
	
	Если КонтактнаяИнформацияКлиентСервер.ЭтоКонтактнаяИнформацияВXML(ЗначенияПолей) Тогда
		// Общий формат контактной информации
		Возврат ДесериализацияКонтактнойИнформации(ЗначенияПолей, ОжидаемыйТип);
	КонецЕсли;
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
	Результат.Представление = Представление;
	
	Если ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
		Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "ЭлектроннаяПочта"));
		
	ИначеЕсли ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.ВебСтраница Тогда
		Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "ВебСайт"));
		
	ИначеЕсли ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.Другое Тогда
		Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Прочее"));
		
	ИначеЕсли ОжидаемыйТип<>Неопределено Тогда
		ВызватьИсключение НСтр("ru='Ошибка десериализации контактной информации, ожидается другой тип'");
		
	КонецЕсли;
	
	Результат.Состав.Значение = Представление;
	
	Возврат Результат;
	
КонецФункции

//  Читает и устанавливает представление для контактной информации. Объект может измениться.
//
//  Параметры:
//      XDTOИнформация - ОбъектXDTO или XML строка контактной информации
//      НовоеЗначение  - необязательное устанавливаемое новое представление в XDTOИнформация
//
Функция ПредставлениеКонтактнойИнформации(XDTOИнформация, НовоеЗначение=Неопределено) Экспорт
	НадоСериализовать = ТипЗнч(XDTOИнформация)=Тип("Строка");
	Если НадоСериализовать И Не КонтактнаяИнформацияКлиентСервер.ЭтоКонтактнаяИнформацияВXML(XDTOИнформация) Тогда
		// Старая версия значений полей, возвращаем саму строку
		Возврат XDTOИнформация;
	КонецЕсли;
	
	ОбъектXDTO = ?(НадоСериализовать, ДесериализацияКонтактнойИнформации(XDTOИнформация), XDTOИнформация);
	Если НовоеЗначение<>Неопределено Тогда
		ОбъектXDTO.Представление = НовоеЗначение;
		Если НадоСериализовать Тогда
			XDTOИнформация = СериализацияКонтактнойИнформации(ОбъектXDTO);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОбъектXDTO.Представление
КонецФункции

//  Читает и устанавливает комментарий для контактной информации
//
//  Параметры:
//      XDTOИнформация - ОбъектXDTO или XML строка контактной информации. Объект может измениться
//      НовоеЗначение  - необязательный устанавливаемый новый комментарий XDTOИнформация
//
Функция КомментарийКонтактнойИнформации(XDTOИнформация, НовоеЗначение=Неопределено) Экспорт
	НадоСериализовать = ТипЗнч(XDTOИнформация)=Тип("Строка");
	Если НадоСериализовать И Не КонтактнаяИнформацияКлиентСервер.ЭтоКонтактнаяИнформацияВXML(XDTOИнформация) Тогда
		// Старая версия значений полей, комментарий не поддерживается
		Возврат "";
	КонецЕсли;
	
	ОбъектXDTO = ?(НадоСериализовать, ДесериализацияКонтактнойИнформации(XDTOИнформация), XDTOИнформация);
	Если НовоеЗначение<>Неопределено Тогда
		ОбъектXDTO.Комментарий = НовоеЗначение;
		Если НадоСериализовать Тогда
			XDTOИнформация = СериализацияКонтактнойИнформации(ОбъектXDTO);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОбъектXDTO.Комментарий;
КонецФункции

//  Вычисляет и устанавливает флаг того, что адрес был введен в свободной форме
//  В качестве флага используется не пустота значения поля "Адрес_по_документу"
//
//  Параметры:
//      XDTOИнформация - ОбъектXDTO или XML строка контактной информации. Объект может измениться
//      НовоеЗначение  - булево - необязательное устанавливаемое новое значение
//
Функция АдресВведенВСвободнойФорме(XDTOИнформация, НовоеЗначение=Неопределено) Экспорт
	НадоСериализовать = ТипЗнч(XDTOИнформация)=Тип("Строка");
	Если НадоСериализовать И Не КонтактнаяИнформацияКлиентСервер.ЭтоКонтактнаяИнформацияВXML(XDTOИнформация) Тогда
		// Старая версия значений полей, не поддерживается
		Возврат Ложь;
	КонецЕсли;
	
	ОбъектXDTO = ?(НадоСериализовать, ДесериализацияКонтактнойИнформации(XDTOИнформация), XDTOИнформация);
	Если Не ЭтоРоссийскийАдрес(ОбъектXDTO) Тогда
		// Не поддерживаем
		Возврат Ложь;
	КонецЕсли;
	
	АдресРФ = ОбъектXDTO.Состав.Состав;
	Если ТипЗнч(НовоеЗначение)<>Тип("Булево") Тогда
		// Читаем
		Возврат Не ПустаяСтрока(АдресРФ.Адрес_по_документу);
	КонецЕсли;
		
	// Устанавливаем
	Если НовоеЗначение Тогда
		АдресРФ.Адрес_по_документу = ОбъектXDTO.Представление;
	Иначе
		АдресРФ.Сбросить("Адрес_по_документу");
	КонецЕсли;
	
	Если НадоСериализовать Тогда
		XDTOИнформация = СериализацияКонтактнойИнформации(ОбъектXDTO);
	КонецЕсли;
	Возврат НовоеЗначение;
КонецФункции

//  Формирует и возвращает представление контактной информации
//
//  Параметры:
//      Информация    - объект XDTO контактной информации, или строковое значение полей
//      ВидИнформации - ссылка на справочник вида КИ
//
Функция СформироватьПредставлениеКонтактнойИнформации(Информация, ВидИнформации) Экспорт
	
	Если ТипЗнч(Информация)=Тип("ОбъектXDTO") Тогда
		Если Информация.Состав=Неопределено Тогда
			// Что было, то и будет
			Возврат Информация.Представление;
		КонецЕсли;
		
		ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
		ТипИнформации    = Информация.Состав.Тип();
		Если ТипИнформации=ФабрикаXDTO.Тип(ПространствоИмен, "Адрес") Тогда
			Возврат ПредставлениеАдреса(Информация.Состав, ВидИнформации);
			
		ИначеЕсли ТипИнформации=ФабрикаXDTO.Тип(ПространствоИмен, "НомерТелефона") Тогда
			Возврат ПредставлениеТелефона(Информация.Состав, ВидИнформации);
			
		ИначеЕсли ТипИнформации=ФабрикаXDTO.Тип(ПространствоИмен, "НомерФакса") Тогда
			Возврат ПредставлениеТелефона(Информация.Состав, ВидИнформации);
			
		КонецЕсли;
		
		// Заглушка на другие типы
		Если ТипЗнч(ТипИнформации)=Тип("ОбъектXDTO") И ТипИнформации.Свойства.Получить("Значение")<>Неопределено Тогда
			Возврат Строка(Информация.Состав.Значение);
		КонецЕсли;
		
		Возврат Строка(Информация.Состав);
	КонецЕсли;
	
	// Старый формат или новый десериализованный
	Если ВидИнформации.Тип=Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
		НоваяИнфо = ДесериализацияАдреса(Информация,,Перечисления.ТипыКонтактнойИнформации.Адрес);
		Возврат СформироватьПредставлениеКонтактнойИнформации(НоваяИнфо, ВидИнформации);
	КонецЕсли;
	
	Возврат СокрЛП(Информация);
КонецФункции

//  Возвращает флаг того, что переданный адрес - российский
//
//  Параметры:
//      XDTOАдрес - объект XDTO КИ или адреса
//
Функция ЭтоРоссийскийАдрес(XDTOАдрес) Экспорт
	Возврат РоссийскийАдрес(XDTOАдрес)<>Неопределено;
КонецФункции

//  Возвращает извлеченный XDTO российского адреса или Неопределено для адреса иностранного
//
//  Параметры:
//      ОбъектИнформации - объект XDTO КИ или адреса
//
Функция РоссийскийАдрес(ОбъектИнформации) Экспорт
	Результат = Неопределено;
	ТипXDTO   = Тип("ОбъектXDTO");
	
	Если ТипЗнч(ОбъектИнформации)=ТипXDTO Тогда
		ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
		
		Если ОбъектИнформации.Тип()=ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация") Тогда
			Адрес = ОбъектИнформации.Состав;
		Иначе
			Адрес = ОбъектИнформации;
		КонецЕсли;
		
		Если ТипЗнч(Адрес)=ТипXDTO И Адрес.Тип()=ФабрикаXDTO.Тип(ПространствоИмен, "Адрес") Тогда
			Адрес = Адрес.Состав;
		КонецЕсли;
		
		Если ТипЗнч(Адрес)=ТипXDTO И Адрес.Тип()=ФабрикаXDTO.Тип(ПространствоИмен, "АдресРФ") Тогда
			Результат = Адрес;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

//  Читает и устанавливает почтовый индекс адреса
//
//  Параметры:
//      XDTOАдрес     - объект XDTO адреса или адреса РФ
//      НовоеЗначение - устанавливаемое значение
//
Функция ПочтовыйИндексАдреса(XDTOАдрес, НовоеЗначение=Неопределено) Экспорт
	
	Если НовоеЗначение=Неопределено Тогда
		// Чтение
		АдресРФ = РоссийскийАдрес(XDTOАдрес);
		Если АдресРФ=Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Результат = АдресРФ.Получить( КонтактнаяИнформацияКлиентСерверПовтИсп.XPathПочтовогоИндекса() );
		Если Результат<>Неопределено Тогда
			Результат = Результат.Значение;
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	// Запись
	КодИндекса = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииПочтовогоИндекса();
	
	ЗаписьИндекса = XDTOАдрес.Получить( КонтактнаяИнформацияКлиентСерверПовтИсп.XPathПочтовогоИндекса() );
	Если ЗаписьИндекса=Неопределено Тогда
		ЗаписьИндекса = XDTOАдрес.ДопАдрЭл.Добавить( ФабрикаXDTO.Создать(XDTOАдрес.ДопАдрЭл.ВладеющееСвойство.Тип) );
		ЗаписьИндекса.ТипАдрЭл = КодИндекса;
	КонецЕсли;
	
	ЗаписьИндекса.Значение = НовоеЗначение;
	Возврат НовоеЗначение;
КонецФункции

//  Возвращает почтовый индекс для адреса или Неопределено при неудаче поиска
//
//  Параметры:
//      XDTOАдрес - объект XDTO адреса или адреса РФ
//
Функция ОпределитьПочтовыйИндексАдреса(XDTOАдрес) Экспорт
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	
	Если XDTOАдрес.Тип()=ФабрикаXDTO.Тип(ПространствоИмен, "Адрес") Тогда
		XDTOАдресРФ = XDTOАдрес.Состав;
	Иначе 
		XDTOАдресРФ = XDTOАдрес;
	КонецЕсли;
	
	Если XDTOАдресРФ=Неопределено Или XDTOАдресРФ.Тип()<>ФабрикаXDTO.Тип(ПространствоИмен, "АдресРФ") Тогда
		Возврат Неопределено;   // Иностранный или пустой адрес
	КонецЕсли;
	
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат Неопределено;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		Возврат ОпределитьПочтовыйИндексАдресаКЛАДР(XDTOАдресРФ);
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

//  Читает и устанавливает район адреса
//
//  Параметры:
//      XDTOАдрес     - объект XDTO адреса или адреса РФ
//      НовоеЗначение - устанавливаемое значение
//
Функция РайонАдреса(XDTOАдрес, НовоеЗначение=Неопределено) Экспорт
	
	Если НовоеЗначение=Неопределено Тогда
		// Чтение
		
		Результат = Неопределено;
		ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
		
		XDTOТип = XDTOАдрес.Тип();
		Если XDTOТип=ФабрикаXDTO.Тип(ПространствоИмен, "АдресРФ") Тогда
			АдресРФ = XDTOАдрес;
		Иначе
			АдресРФ = XDTOАдрес.Состав;
		КонецЕсли;
		
		Если ТипЗнч(АдресРФ)=Тип("ОбъектXDTO") Тогда
			Возврат ЗначениеСвойстваПоXPath(АдресРФ, КонтактнаяИнформацияКлиентСерверПовтИсп.XPathРайона() );
		КонецЕсли;
		
		Возврат Неопределено;
	КонецЕсли;
	
	// Запись
	Запись = СвРайМО(XDTOАдрес);
	Запись.Район = НовоеЗначение;
	Возврат НовоеЗначение;
КонецФункции

//  Читает и устанавливает здания и помещения адреса. Возвращает структуру с полями "Здания" и "Помещения",
//  содержащими данные. Каждое поле - таблица значений с колонками "Тип",  "Сокращение", "Значение", где:
//      Тип        - строка тип внутреннего классифатора дополнительных адресных объектов. Например "Корпус"
//      Сокращение - сокращение названия для использования в представлении. При установке не используется
//      Значение   - значение номера дома, квартиры и т.п
//      ПутьXPath  - путь к значению объекта
//
//  Параметры:
//      XDTOАдрес     - объект XDTO адреса или адреса РФ
//      НовоеЗначение - необязательная структура с полями "Здания" и "Помещения", содержащими таблицы для
//                      для установки новых значения. Структура таблиц аналогична структуре результата функции.
//                      Можно смешивать здания и помещения в одной таблице.
//
Функция ЗданияИПомещенияАдреса(XDTOАдрес, НовоеЗначение=Неопределено) Экспорт
	
	Результат = Новый Структура("Здания, Помещения", 
		ТаблицаЗначений("Тип, Значение, Сокращение, ПутьXPath, Вид", "Тип, Вид"),
		ТаблицаЗначений("Тип, Значение, Сокращение, ПутьXPath, Вид", "Тип, Вид"));
	
	АдресРФ = РоссийскийАдрес(XDTOАдрес);
	Если АдресРФ=Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если НовоеЗначение<>Неопределено Тогда
		// Запись
		Если НовоеЗначение.Свойство("Здания") Тогда
			Для Каждого Строка Из НовоеЗначение.Здания Цикл
				ВставитьЗданиеПомещение(XDTOАдрес, Строка.Тип, Строка.Значение);
			КонецЦикла;
		КонецЕсли;
		Если НовоеЗначение.Свойство("Помещения") Тогда
			Для Каждого Строка Из НовоеЗначение.Помещения Цикл
				ВставитьЗданиеПомещение(XDTOАдрес, Строка.Тип, Строка.Значение);
			КонецЦикла;
		КонецЕсли;
		Возврат НовоеЗначение
	КонецЕсли;
	
	// Чтение
	Для Каждого ДопЭлемент Из АдресРФ.ДопАдрЭл Цикл
		Если ДопЭлемент.Номер<>Неопределено Тогда
			КодОбъекта = ДопЭлемент.Номер.Тип;
			ТипОбъекта = КонтактнаяИнформацияКлиентСерверПовтИсп.ТипОбъектаПоКодуСериализации(КодОбъекта);
			Если ТипОбъекта<>Неопределено Тогда
				Вид = ТипОбъекта.Тип;
				Если Вид=1 Или Вид=2 Тогда
					НоваяСтрока = Результат.Здания.Добавить();
				ИначеЕсли Вид=3 Тогда
					НоваяСтрока = Результат.Помещения.Добавить();
				Иначе
					НоваяСтрока = Неопределено;
				КонецЕсли;
				Если НоваяСтрока<>Неопределено Тогда
					НоваяСтрока.Тип        = ТипОбъекта.Наименование;
					НоваяСтрока.Значение   = ДопЭлемент.Номер.Значение;
					НоваяСтрока.Сокращение = ТипОбъекта.Сокращение;
					НоваяСтрока.ПутьXPath  = КонтактнаяИнформацияКлиентСерверПовтИсп.XPathНомераДополнительногоОбъектаАдресации(НоваяСтрока.Тип);
					НоваяСтрока.Вид        = Вид;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Результат.Здания.Сортировать("Вид");
	Результат.Помещения.Сортировать("Вид");
	
	Возврат Результат;
КонецФункции

//  Устанавливает значения полей населенного пункта в адресе
//  
//  Параметры:
//      XDTOАдрес         - объект XDTO адреса РФ
//      КодКлассификатора - полный код, зависит от классификатора
//
Процедура УстановитьНаселенныйПунктАдресаПоКоду(XDTOАдрес, КодКлассификатора) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		УстановитьНаселенныйПунктАдресаПоКодуКЛАДР(XDTOАдрес, КодКлассификатора);
	КонецЕсли;
	
КонецПроцедуры    

//  Устанавливает значения полей улицы
//  
//  Параметры:
//      XDTOАдрес         - объект XDTO адреса РФ
//      КодКлассификатора - полный код, зависит от классификатора
//
Процедура УстановитьУлицуАдресаПоКоду(XDTOАдрес, КодКлассификатора) Экспорт
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	
	Если Вариант=Неопределено Тогда
		// Нет подсистемы классификатора
		Возврат;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		УстановитьУлицуАдресаПоКодуКЛАДР(XDTOАдрес, КодКлассификатора);
	КонецЕсли;
	
КонецПроцедуры

//  Возвращает представление младший-старший для населенного пункта
//
//  Параметры:
//      ОбъектАдреса - XDTO адрес РФ
//
Функция ПредставлениеНаселенногоПункта(ОбъектАдреса) Экспорт
	
	АдресРФ = РоссийскийАдрес(ОбъектАдреса);
	Если АдресРФ=Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Если АдресРФ.СвРайМО=Неопределено Тогда
		Район = "";
	ИначеЕсли АдресРФ.СвРайМО.Район<>Неопределено Тогда
		Район = АдресРФ.СвРайМО.Район;
	ИначеЕсли АдресРФ.СвРайМО.СвМО<>Неопределено Тогда
		Район = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
			АдресРФ.СвРайМО.СвМО.МунОбр2, "",
			АдресРФ.СвРайМО.СвМО.МунОбр1, "");
	Иначе
		Район = "";;
	КонецЕсли;
	
	Возврат КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
		АдресРФ.НаселПункт, "",
		АдресРФ.Город,  "",
		Район, "",
		АдресРФ.Округ, "",
		АдресРФ.СубъектРФ, "");
	
КонецФункции    

//  Возвращает представление младший-старший для населенного пункта
//
//  Параметры:
//      ОбъектАдреса - XDTO адрес РФ
//
Функция ПредставлениеУлицы(ОбъектАдреса) Экспорт
	
	АдресРФ = РоссийскийАдрес(ОбъектАдреса);
	Если АдресРФ=Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
		АдресРФ.Улица, "");
	
КонецФункции

//  Возвращает представление адреса
//
//  Параметры:
//      XDTOАдрес     - объект XDTO адреса
//      ВидИнформации - ссылка на справочник вида КИ или структура
//
Функция ПредставлениеАдреса(XDTOАдрес, ВидИнформации) Экспорт
	
	// 1) Страна, если надо.
	// 2) Индекс, субъект рф, округ, район, город, внутригородской район, населенный пункт, улица
	// 3) Здания, помещения
	
	ПараметрыФормирования = Новый Структура("ВключатьСтрануВПредставление", Ложь);
	ЗаполнитьЗначенияСвойств(ПараметрыФормирования, ВидИнформации);
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	АдресРФ          = XDTOАдрес.Состав;
	Страна           = СокрЛП(XDTOАдрес.Страна);
	
	Если ЭтоРоссийскийАдрес(АдресРФ) Тогда
		// Это российский адрес, смотрим на настройки
		Если Не ПараметрыФормирования.ВключатьСтрануВПредставление Тогда
			Страна = "";
		КонецЕсли;
		// Значимые части
		Представление = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
			ПочтовыйИндексАдреса(АдресРФ), "",
			АдресРФ.СубъектРФ, "",
			АдресРФ.Округ, "",
			РайонАдреса(АдресРФ), "",
			АдресРФ.Город, "",
			АдресРФ.ВнутригРайон, "",
			АдресРФ.НаселПункт, "",
			АдресРФ.Улица, "");
		// Строения и помещения
		НомерНеВыведен = Истина;
		Данные = ЗданияИПомещенияАдреса(АдресРФ);
		Для Каждого Строка Из Данные.Здания Цикл
			Представление =  КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
				Представление, "",
				СокрЛП(Строка.Сокращение + ?(НомерНеВыведен, " № ", " ") + Строка.Значение), "");
			НомерНеВыведен = Ложь;
		КонецЦикла;
		Для Каждого Строка Из Данные.Помещения Цикл
			Представление =  КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
				Представление, "",
				СокрЛП(Строка.Сокращение + " " + Строка.Значение), "");
		КонецЦикла;
		
	Иначе
		// Это иностранный адрес, страна в представление будет включена всегда.
		Представление = СокрЛП(АдресРФ);
	КонецЕсли;
	
	// При пустом представлении страну нет смысла выводить
	Если ПустаяСтрока(Представление) Тогда
		Возврат "";
	КонецЕсли;
		
	Возврат КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Страна, "", Представление, "");
КонецФункции

//  Возвращает перечень ошибок для адреса.
//
//  Параметры:
//      XDTOАдрес         - объект XDTO адреса или список значений полей или строка с именами и значениями
//      ВидИнформации     - ссылка на соответствующий вид контактной информации 
//      РезультатГруппами - если Истина, то возвращает массив групп ошибок, иначе - список.
//
//  Возвращаемое значение зависит от значения флага РезультатГруппами:
//      а) Список значений, в котором Представление - описание ошибки, значение - XPath для поля
//      б) Массив структур с полями:
//            ТипОшибки - строка с наименованием ошибки. Возможные значения:
//              - "ПредставлениеНеСоответствуетНаборуПолей"
//              - "НеЗаполненыОбязательныеПоля"
//              - "НеУказаныСокращенияПолей"
//              - "НедопустимыеСимволыПолей"
//              - "НеСоответствуетДлинаПолей"
//              - "ОшибкиПоКлассификатору"
//            Сообщение - подробный текст ошибки
//            Поля      - массив структур с полями:
//                ИмяПоля   - внутренний идентификатор элемента адреса
//                Сообщение - подробный текст ошибки для поля
//
Функция ОшибкиЗаполненияАдреса(XDTOАдрес, ВидИнформации, РезультатГруппами=Ложь) Экспорт
	
	Если ТипЗнч(XDTOАдрес)=Тип("ОбъектXDTO") Тогда
		АдресРФ = XDTOАдрес.Состав;
	Иначе        
		XTDOКонтактная = ДесериализацияАдреса(XDTOАдрес);
		Адрес = XTDOКонтактная.Состав;
		АдресРФ = ?(Адрес=Неопределено, Неопределено, Адрес.Состав);
	КонецЕсли;
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	Если ТипЗнч(АдресРФ)<>Тип("ОбъектXDTO") Или АдресРФ.Тип()<>ФабрикаXDTO.Тип(ПространствоИмен, "АдресРФ") Тогда
		// Адрес за пределами РФ
		Возврат ?(РезультатГруппами, Новый Массив, Новый СписокЗначений);
	КонецЕсли;
	
	ВсеОшибки = ОбщиеГруппыОшибокЗаполненияАдреса(АдресРФ, ВидИнформации);
	ПроверятьКлассификатор = Истина;
	
	Для Каждого Группа Из ВсеОшибки Цикл
		Если Найти("НеУказаныСокращенияПолей, НедопустимыеСимволыПолей", Группа.ТипОшибки) Тогда
			ПроверятьКлассификатор = Ложь;
			Прервать;
		КонецЕсли            
	КонецЦикла;            
	
	ОшибкиКлассификатора = Новый СписокЗначений;
	Если ПроверятьКлассификатор Тогда
		Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
		Если Вариант=Неопределено Тогда 
			// Нет подсистемы классификатора
		ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
			ЗаполнитьОшибкиАдресаКЛАДР(АдресРФ, ОшибкиКлассификатора);
		КонецЕсли;
		
	КонецЕсли;
	
	Если РезультатГруппами Тогда
		НаименованиеГруппыОшибки = "ОшибкиПоКлассификатору";
		КоличествоОшибок = ОшибкиКлассификатора.Количество();
		
		Если КоличествоОшибок=1 И ОшибкиКлассификатора[0].Значение<>Неопределено
			И ОшибкиКлассификатора[0].Значение.ПутьXPath=Неопределено 
		Тогда
			ВсеОшибки.Добавить(ГруппаОшибокАдреса(НаименованиеГруппыОшибки,
				ОшибкиКлассификатора[0].Представление));
			
		ИначеЕсли КоличествоОшибок>0 Тогда
			// Подробное описание ошибок
			ВсеОшибки.Добавить(ГруппаОшибокАдреса(НаименованиеГруппыОшибки,
				НСтр("ru='Части адреса не соответствуют адресному классификатору:'")));
				
			ГруппаОшибокКлассификатора = ВсеОшибки[ВсеОшибки.ВГраница()];
			
			СписокСущностей = "";
			Для Каждого Элемент Из ОшибкиКлассификатора Цикл
				ЭлементОшибки = Элемент.Значение;
				Если ЭлементОшибки=Неопределено Тогда
					// Абстрактная ошибка
					ДобавитьОшибкуЗаполненияАдреса(ГруппаОшибокКлассификатора, 
						"", Элемент.Представление);
				Иначе
					ДобавитьОшибкуЗаполненияАдреса(ГруппаОшибокКлассификатора, 
						ЭлементОшибки.ПутьXPath, Элемент.Представление);
					СписокСущностей = СписокСущностей + ", " + ЭлементОшибки.СущностьПоля;
				КонецЕсли;
			КонецЦикла;
			
			ГруппаОшибокКлассификатора.Сообщение = ГруппаОшибокКлассификатора.Сообщение + Сред(СписокСущностей, 2);
		КонецЕсли;
		
		Возврат ВсеОшибки;
	КонецЕсли;
	
	// Объединяем все в список
	Результат = Новый СписокЗначений;
	Для Каждого Группа Из ВсеОшибки Цикл
		Для Каждого Поле Из Группа.Поля Цикл
			Результат.Добавить(Поле.ИмяПоля, Поле.Сообщение);
		КонецЦикла;
	КонецЦикла;
	Для Каждого ЭлементСписка Из ОшибкиКлассификатора Цикл
		Результат.Добавить(ЭлементСписка.Значение.ПутьXPath, ЭлементСписка.Представление);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Общие проверки на корректность адреса
//
//  Параметры:
//      ДанныеАдреса  - строка, список значений, XML, XDTO с данными адреса РФ
//      ВидИнформации - ссылка на справочник ВидыКонтакнойИнформации
//
//  Возвращает массив, содержащий структуры с полями:
//      - ТипОшибки - идентификатор группы ошибки. Может принимать значение:
//          "ПредставлениеНеСоответствуетНаборуПолей",
//          "НеЗаполненыОбязательныеПоля"
//          "НеУказаныСокращенияПолей"
//          "НедопустимыеСимволыПолей"
//          "НеСоответствуетДлинаПолей"
//      - Сообщение - Подробный текст ошибки
//      - Поля - массив структур с полями:
//          - ИмяПоля - внутренний идентификатор ошибочного поля
//          - Сообщение - подробный текст ошибки для поля
//
Функция ОбщиеГруппыОшибокЗаполненияАдреса(ДанныеАдреса, ВидИнформации) Экспорт
	Результат = Новый Массив;
	
	Если ТипЗнч(ДанныеАдреса)=Тип("ОбъектXDTO") Тогда
		АдресРФ = ДанныеАдреса;
	Иначе
		XTDOКонтактная = ДесериализацияАдреса(ДанныеАдреса);
		Адрес = XTDOКонтактная.Состав;
		Если Не ЭтоРоссийскийАдрес(Адрес) Тогда
			Возврат Результат;
		КонецЕсли;
		АдресРФ = Адрес.Состав;
		
		// 1) совпадение представления и набора данных
		Представление = ПредставлениеАдреса(АдресРФ, ВидИнформации);
		Если XTDOКонтактная.Представление<>Представление Тогда
			Результат.Добавить(ГруппаОшибокАдреса("ПредставлениеНеСоответствуетНаборуПолей",
				НСтр("ru='Адрес не соответствует значениям в наборе полей.'")));
			ДобавитьОшибкуЗаполненияАдреса(Результат[0], "",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Представление адреса для вида контактной информации ""%1"" отличается от данных в адресе.'"),
					Строка(ВидИнформации)));
		КонецЕсли;
	КонецЕсли;
	
	НеЗаполненыОбязательныеПоля = ГруппаОшибокАдреса("НеЗаполненыОбязательныеПоля",
		НСтр("ru='Не заполнены обязательные поля:'"));
	Результат.Добавить(НеЗаполненыОбязательныеПоля);
	
	НеУказаныСокращенияПолей = ГруппаОшибокАдреса("НеУказаныСокращенияПолей",
		НСтр("ru='Не указано сокращение для полей:'"));
	Результат.Добавить(НеУказаныСокращенияПолей);
	
	НедопустимыеСимволыПолей = ГруппаОшибокАдреса("НедопустимыеСимволыПолей",
		НСтр("ru='Найдены недопустимые символы в полях:'"));
	Результат.Добавить(НедопустимыеСимволыПолей);
	
	НеСоответствуетДлинаПолей = ГруппаОшибокАдреса("НеСоответствуетДлинаПолей",
		НСтр("ru='Не соответствует установленной длина полей:'"));
	Результат.Добавить(НеСоответствуетДлинаПолей);
	
	// 2) Индекс, Регион, Дом должны быть заполнены
	Индекс = ПочтовыйИндексАдреса(АдресРФ);
	Если ПустаяСтрока(Индекс) Тогда
		ДобавитьОшибкуЗаполненияАдреса(НеЗаполненыОбязательныеПоля, КонтактнаяИнформацияКлиентСерверПовтИсп.XPathПочтовогоИндекса(),
			НСтр("ru='Не указан почтовый индекс.'"), "Индекс");
	КонецЕсли;
	
	Регион = АдресРФ.СубъектРФ;
	Если ПустаяСтрока(Регион) Тогда
		ДобавитьОшибкуЗаполненияАдреса(НеЗаполненыОбязательныеПоля, "СубъектРФ",
			НСтр("ru='Не указан регион.'"), "Регион");
	КонецЕсли;
	
	ЗданияПомещения = ЗданияИПомещенияАдреса(АдресРФ);
	ДанныеДома = ЗданияПомещения.Здания.Найти(1, "Вид");    // 1 - вид по признаку владения
	Если ДанныеДома=Неопределено Тогда
		ДобавитьОшибкуЗаполненияАдреса(НеЗаполненыОбязательныеПоля, 
			КонтактнаяИнформацияКлиентСерверПовтИсп.XPathНомераДополнительногоОбъектаАдресации("Дом"),
			НСтр("ru='Не указан дом или владение.'"), "Дом");
	ИначеЕсли ПустаяСтрока(ДанныеДома.Значение) Тогда
		ДобавитьОшибкуЗаполненияАдреса(НеЗаполненыОбязательныеПоля, ДанныеДома.ПутьXPath,
			НСтр("ru='Не заполнено значение дома или владения.'"), "Дом");
	КонецЕсли;
	
	// 3) Регион, Район, Город, НаселенныйПункт, Улица должны:    
	//      - иметь сокращение
	//      - не длинней 50 символов
	//      - только кириллица
	
	ДопустимоКромеКириллицы = "/,-. 0123456789_";
	
	// Регион
	Если Не ПустаяСтрока(Регион) Тогда
		Поле = "СубъектРФ";
		Если ПустаяСтрока(КонтактнаяИнформацияКлиентСервер.Сокращение(Регион)) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеУказаныСокращенияПолей, "СубъектРФ",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Не указано сокращение в названии региона ""%1"".'"), Регион
				), "Регион");
		КонецЕсли;
		Если СтрДлина(Регион)>50 Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеСоответствуетДлинаПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Название региона ""%1"" должно быть короче 50 символов.'"), Регион
				), "Регион");
		КонецЕсли;
		Если Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Регион, Ложь, ДопустимоКромеКириллицы) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НедопустимыеСимволыПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='В названии региона ""%1"" есть не кириллические символы.'"), Регион
				), "Регион");
		КонецЕсли
	КонецЕсли;
	
	// Район
	Район = РайонАдреса(АдресРФ);
	Если Не ПустаяСтрока(Район) Тогда
		Поле = КонтактнаяИнформацияКлиентСерверПовтИсп.XPathРайона();
		Если ПустаяСтрока(КонтактнаяИнформацияКлиентСервер.Сокращение(Район)) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеУказаныСокращенияПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Не указано сокращение в названии района ""%1""'."), Район
				), "Район");
		КонецЕсли;
		Если СтрДлина(Район)>50 Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеСоответствуетДлинаПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Название района ""%1"" должно быть короче 50 символов.'"), Район
				), "Район");
		КонецЕсли;
		Если Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Район, Ложь, ДопустимоКромеКириллицы) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НедопустимыеСимволыПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='В названии района ""%1"" есть не кириллические символы.'"), Район
				), "Район");
		КонецЕсли;
	КонецЕсли;
	
	// Город
	Город = АдресРФ.Город;
	Если Не ПустаяСтрока(Город) Тогда
		Поле = "Город";
		Если ПустаяСтрока(КонтактнаяИнформацияКлиентСервер.Сокращение(Город)) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеУказаныСокращенияПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Не указано сокращение в названии города ""%1"".'"), Город
				), "Город");
		КонецЕсли;
		Если СтрДлина(Город)>50 Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеСоответствуетДлинаПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Название города ""%1"" должно быть короче 50 символов.'"), Город
				), "Город");
		КонецЕсли;
		Если Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Город, Ложь, ДопустимоКромеКириллицы) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НедопустимыеСимволыПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='В названии города ""%1"" есть не кириллические символы.'"), Город
				), "Город");
		КонецЕсли;
	КонецЕсли;
	
	// Населенный пункт
	НаселенныйПункт = АдресРФ.НаселПункт;
	Если Не ПустаяСтрока(НаселенныйПункт) Тогда
		Поле = "НаселПункт";
		Если ПустаяСтрока(КонтактнаяИнформацияКлиентСервер.Сокращение(НаселенныйПункт)) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеУказаныСокращенияПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Не указано сокращение в названии населенного пункта ""%1"".'"), НаселенныйПункт
				), "Населенный пункт");
		КонецЕсли;
		Если СтрДлина(НаселенныйПункт)>50 Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеСоответствуетДлинаПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Название населенного пункта ""%1"" должно быть короче 50 символов.'"), НаселенныйПункт
				), "Населенный пункт");
		КонецЕсли;
		Если Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(НаселенныйПункт, Ложь, ДопустимоКромеКириллицы) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НедопустимыеСимволыПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='В названии населенного пункта ""%1"" есть не кириллические символы.'"), НаселенныйПункт
				), "Населенный пункт");
		КонецЕсли;
	КонецЕсли;
	
	// Улица
	Улица = АдресРФ.Улица;
	Если Не ПустаяСтрока(Улица) Тогда
		Поле = "Улица";
		Если ПустаяСтрока(КонтактнаяИнформацияКлиентСервер.Сокращение(Улица)) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеУказаныСокращенияПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Не указано сокращение в названии улицы ""%1"".'"), Улица
				), "Улица");
		КонецЕсли;
		Если СтрДлина(Район)>50 Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеСоответствуетДлинаПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Название улицы ""%1"" должно быть короче 50 символов.'"), Улица
				), "Улица");
		КонецЕсли;
		Если Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Улица, Ложь, ДопустимоКромеКириллицы) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НедопустимыеСимволыПолей, Поле,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='В названии улицы ""%1"" есть не кириллические символы.'"), Улица
				), "Улица");
		КонецЕсли;
	КонецЕсли;
	
	// 4) Индекс - если есть, то 6 цифр
	Если Не ПустаяСтрока(Индекс) Тогда
		Поле = КонтактнаяИнформацияКлиентСерверПовтИсп.XPathПочтовогоИндекса();
		Если СтрДлина(Индекс)<>6 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Индекс) Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеСоответствуетДлинаПолей, Поле,
				НСтр("ru='Почтовый индекс должен состоять из 6 цифр.'"), "Индекс");
		КонецЕсли;
	КонецЕсли;
	
	// 5) Дом, Корпус, Квартира не длинней 10 символов
	Для Каждого ДанныеЗдания Из ЗданияПомещения.Здания Цикл
		Если СтрДлина(ДанныеЗдания.Значение)>10 Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеСоответствуетДлинаПолей, ДанныеЗдания.ПутьXPath,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Значение поля ""%1"" должно быть короче 10 символов.'"), ДанныеЗдания.Тип
				), ДанныеЗдания.Тип);
		КонецЕсли;            
	КонецЦикла;
	Для Каждого ДанныеПомещения Из ЗданияПомещения.Помещения Цикл
		Если СтрДлина(ДанныеПомещения.Значение)>10 Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеСоответствуетДлинаПолей, ДанныеПомещения.ПутьXPath,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( 
					НСтр("ru='Значение поля ""%1"" должно быть короче 10 символов.'"), ДанныеПомещения.Тип
				), ДанныеПомещения.Тип);
		КонецЕсли;
	КонецЦикла;
	
	// 6) Поля Город и НаселенныйПункт могут быть одновременно пустые только в регионе==городе ФЗ
	Если ПустаяСтрока(Город) И ПустаяСтрока(НаселенныйПункт) Тогда
		Если НазванияГородовФедеральногоЗначения().Найти(ВРег(Регион))=Неопределено Тогда
			ДобавитьОшибкуЗаполненияАдреса(НеЗаполненыОбязательныеПоля, "Город",
				НСтр("ru='Город может быть не указан только в регионе - городе федерального значения.'"),
				"Город");
			ДобавитьОшибкуЗаполненияАдреса(НеЗаполненыОбязательныеПоля, "НаселПункт",
				НСтр("ru='Населенный пункт может быть не указан только в регионе - городе федерального значения.'"),
				"Населенный пункт");
		КонецЕсли;
	КонецЕсли;
	
	// 7) Улица не может быть пустой если НаселенныйПункт пустой
	Если ПустаяСтрока(НаселенныйПункт) И ПустаяСтрока(Улица) Тогда
		ДобавитьОшибкуЗаполненияАдреса(НеЗаполненыОбязательныеПоля, "Улица",
			НСтр("ru='Если неопределен населенный пункт, то должна быть указана улица.'"), "Улица");
	КонецЕсли;
	
	// Все. Убираем пустые результаты, корректируем сообщение группы.
	Для Индекс = 1-Результат.Количество() По 0 Цикл
		Группа = Результат[-Индекс];
		Поля = Группа.Поля;
		СписокСущностей = "";
		Для ИндексПоля = 1-Поля.Количество() По 0 Цикл
			Поле = Поля[-ИндексПоля];
			Если ПустаяСтрока(Поле.Сообщение) Тогда
				Поля.Удалить(-ИндексПоля);
			Иначе
				СписокСущностей = ", " + Поле.СущностьПоля + СписокСущностей;
				Поле.Удалить("СущностьПоля");
			КонецЕсли;
		КонецЦикла;
		Если Поля.Количество()=0 Тогда
			Результат.Удалить(-Индекс);
		ИначеЕсли Не ПустаяСтрока(СписокСущностей) Тогда
			Группа.Сообщение = Группа.Сообщение + Сред(СписокСущностей, 2);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

//  Возвращает массив наименований регионов - городов федерального значения
Функция НазванияГородовФедеральногоЗначения() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("МОСКВА Г");
	Результат.Добавить("САНКТ-ПЕТЕРБУРГ Г");
	
	Возврат Результат;
КонецФункции    

//  Возвращает представление телефона
//
//  Параметры:
//      XDTOДанные    - объект XDTO номера телефона
//      ВидИнформации - необязательная ссылка на справочник вида КИ
//
Функция ПредставлениеТелефона(XDTOДанные, ВидИнформации=Неопределено) Экспорт
	Возврат УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеТелефона(
		СократитьНеЦифры(XDTOДанные.КодСтраны), 
		XDTOДанные.КодГорода,
		XDTOДанные.Номер,
		XDTOДанные.Добавочный,
		"");
КонецФункции    

//  Возвращает представление факса
//
//  Параметры:
//      XDTOДанные    - объект XDTO номера телефона
//      ВидИнформации - необязательная ссылка на справочник вида КИ
//
Функция ПредставлениеФакса(XDTOДанные, ВидИнформации=Неопределено) Экспорт
	Возврат УправлениеКонтактнойИнформациейКлиентСервер.СформироватьПредставлениеТелефона(
		СократитьНеЦифры(XDTOДанные.КодСтраны), 
		XDTOДанные.КодГорода,
		XDTOДанные.Номер,
		XDTOДанные.Добавочный,
		"");
КонецФункции    

// Возвращает флаг того, что текущий пользователь может загружать или очищать адресный классификатор
Функция ЕстьВозможностьИзмененияАдресногоКлассификатора() Экспорт
	
	Вариант = КонтактнаяИнформацияКлиентСервер.ИспользуемыйАдресныйКлассификатор();
	Если Вариант=Неопределено Тогда
		// Нет классификатора
		Возврат Ложь;
	ИначеЕсли Вариант=Перечисления["ВариантыАдресногоКлассификатора"].КЛАДР Тогда
		ОбъектКонтроля = Метаданные.РегистрыСведений.Найти("АдресныйКлассификатор");
		Возврат ОбъектКонтроля<>Неопределено И ПравоДоступа("Изменение", ОбъектКонтроля);
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////////////
// Совместимость
//

//  Возвращает структуру ("Представление", "ЗначенияПолей"), заполненную строкой и списком
//
//  Параметры:
//      XDTOКонтактнаяИнформация - XDTO контактной информации или строка XML
//      СтарыйСоставПолей        - булево - необязательный флаг того, что из состава полей будут исключены
//                                 поля, отсутствующие в версиях БСП младше 2.1.3
//
Функция КонтактнаяИнформацияВСтаруюСтруктуру(XDTOКонтактнаяИнформация, СтарыйСоставПолей=Ложь) Экспорт
	
	Если КонтактнаяИнформацияКлиентСервер.ЭтоКонтактнаяИнформацияВXML(XDTOКонтактнаяИнформация) Тогда
		XDTOКонтактная = ДесериализацияКонтактнойИнформации(XDTOКонтактнаяИнформация);
	Иначе
		XDTOКонтактная = XDTOКонтактнаяИнформация
	КонецЕсли;
	
	Результат = Новый Структура("Представление, ЗначенияПолей", XDTOКонтактная.Представление, Новый СписокЗначений);
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	Состав = XDTOКонтактная.Состав;
	
	Если Состав=Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Тип = Состав.Тип();
	Если Тип=ФабрикаXDTO.Тип(ПространствоИмен, "Адрес") Тогда
		Результат.ЗначенияПолей = АдресВСтарыйСписокПолей(Состав, Не СтарыйСоставПолей);
		Результат.ЗначенияПолей.Добавить(Результат.Представление, "Представление");
		
	ИначеЕсли Тип=ФабрикаXDTO.Тип(ПространствоИмен, "НомерТелефона") Тогда
		Результат.ЗначенияПолей = НомерТелефонаВСтарыйСписокПолей(Состав);
		Результат.ЗначенияПолей.Добавить(XDTOКонтактная.Комментарий, "Комментарий");
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Преобразует адрес формата XDTO в старый список полей типа СписокЗначений
//
// Параметры:
//     XDTOАдрес              - XDTOОбъект типа Адрес или АдресРФ, источник данных
//     РасширенныйСоставПолей - Необязательный флаг того, что состав полей будет сокращен для совместимости
//                              с обменом БСП 2.1.2
//
// Возвращаемый результат - список значений
//
Функция АдресВСтарыйСписокПолей(XDTOАдрес, РасширенныйСоставПолей=Истина) Экспорт
	Список = Новый СписокЗначений;
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	XDTOТип = XDTOАдрес.Тип();
	Если XDTOТип=ФабрикаXDTO.Тип(ПространствоИмен, "Адрес") Тогда
		
		// Страна с кодом
		ДобавитьЗначение(Список, "Страна", XDTOАдрес.Страна);
		Если ПустаяСтрока(XDTOАдрес.Страна) Тогда
			КодСтраны = "";
		Иначе
			Страна = Справочники.СтраныМира.НайтиПоНаименованию(XDTOАдрес.Страна, Истина);
			КодСтраны = СокрЛП(Страна.Код);
		КонецЕсли;
		ДобавитьЗначение(Список, "КодСтраны", КодСтраны);
		
		Если Не ЭтоРоссийскийАдрес(XDTOАдрес) Тогда
			Возврат Список;
		КонецЕсли;
		
		АдресРФ = XDTOАдрес.Состав;
		
	ИначеЕсли XDTOТип=ФабрикаXDTO.Тип(ПространствоИмен, "АдресРФ") Тогда
		АдресРФ = XDTOАдрес;
		
	Иначе
		Возврат Список;
		
	КонецЕсли;
	
	ДобавитьЗначение(Список, "Индекс", ПочтовыйИндексАдреса(АдресРФ) );
	
	ДобавитьЗначение(Список, "Регион", АдресРФ.СубъектРФ);
	ДобавитьЗначение(Список, "КодРегиона", КодРегиона(АдресРФ.СубъектРФ) );
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "РегионСокращение", КонтактнаяИнформацияКлиентСервер.Сокращение(АдресРФ.СубъектРФ));
	КонецЕсли;
	
	Район = РайонАдреса(АдресРФ);
	ДобавитьЗначение(Список, "Район", Район);
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "РайонСокращение", КонтактнаяИнформацияКлиентСервер.Сокращение(Район));
	КонецЕсли;
	
	ДобавитьЗначение(Список, "Город", АдресРФ.Город);
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "ГородСокращение", КонтактнаяИнформацияКлиентСервер.Сокращение(АдресРФ.Город));
	КонецЕсли;
	
	ДобавитьЗначение(Список, "НаселенныйПункт", АдресРФ.НаселПункт);
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "НаселенныйПунктСокращение", КонтактнаяИнформацияКлиентСервер.Сокращение(АдресРФ.НаселПункт));
	КонецЕсли;
	
	ДобавитьЗначение(Список, "Улица", АдресРФ.Улица);
	Если РасширенныйСоставПолей Тогда
		ДобавитьЗначение(Список, "УлицаСокращение", КонтактнаяИнформацияКлиентСервер.Сокращение(АдресРФ.Улица));
	КонецЕсли;
	
	// Дом и корпус
	ЗданияИПомещения = ЗданияИПомещенияАдреса(АдресРФ);
	
	ПараметрыОбъекта = ЗначениеЗданияИлиПомещения(ЗданияИПомещения.Здания, ВариантыДанныхДом(), РасширенныйСоставПолей);
	Если ПараметрыОбъекта.Количество()=0 Тогда
		ДобавитьЗначение(Список, "ТипДома", "");
		ДобавитьЗначение(Список, "Дом",     "");
	Иначе
		Для Каждого СтрокаОбъекта Из ПараметрыОбъекта Цикл
			ДобавитьЗначение(Список, "ТипДома", СтрокаОбъекта.Тип,      РасширенныйСоставПолей);
			ДобавитьЗначение(Список, "Дом",     СтрокаОбъекта.Значение, РасширенныйСоставПолей);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыОбъекта = ЗначениеЗданияИлиПомещения(ЗданияИПомещения.Здания, ВариантыДанныхСтроение(), РасширенныйСоставПолей);
	Если ПараметрыОбъекта.Количество()=0 Тогда
			ДобавитьЗначение(Список, "ТипКорпуса", "");
			ДобавитьЗначение(Список, "Корпус",     "");
	Иначе
		Для Каждого СтрокаОбъекта Из ПараметрыОбъекта Цикл
			ДобавитьЗначение(Список, "ТипКорпуса", СтрокаОбъекта.Тип,      РасширенныйСоставПолей);
			ДобавитьЗначение(Список, "Корпус",     СтрокаОбъекта.Значение, РасширенныйСоставПолей);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыОбъекта = ЗначениеЗданияИлиПомещения(ЗданияИПомещения.Помещения, ВариантыДанныхПомещение(), РасширенныйСоставПолей);
	Если ПараметрыОбъекта.Количество()=0 Тогда
		ДобавитьЗначение(Список, "ТипКвартиры", "");
		ДобавитьЗначение(Список, "Квартира",    "");
	Иначе
		Для Каждого СтрокаОбъекта Из ПараметрыОбъекта Цикл	
			ДобавитьЗначение(Список, "ТипКвартиры", СтрокаОбъекта.Тип,      РасширенныйСоставПолей);
			ДобавитьЗначение(Список, "Квартира",    СтрокаОбъекта.Значение, РасширенныйСоставПолей);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Список;
КонецФункции

Процедура ДобавитьЗначение(Список, ИмяПоля, Значение, РазрешитьДубли=Ложь)
	
	Если Не РазрешитьДубли Тогда
		Для Каждого Элемент Из Список Цикл
			Если Элемент.Представление=ИмяПоля Тогда
				Элемент.Значение = Строка(Значение);
				Возврат;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Список.Добавить(Строка(Значение), ИмяПоля);
КонецПроцедуры

Функция ЗначениеЗданияИлиПомещения(Данные, Варианты, ВсеЗначенияВарианта)
	Результат = ТаблицаЗначений("Тип, Значение");
	
	Для Каждого Вариант Из Варианты.ВариантыТипа Цикл
		Для Каждого Строка Из Данные.НайтиСтроки(Новый Структура("Тип", Вариант)) Цикл
			ЗаполнитьЗначенияСвойств(Результат.Добавить(), Строка);
			Если Не ВсеЗначенияВарианта Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция НомерТелефонаВСтарыйСписокПолей(XDTOТелефон)
	Результат = Новый СписокЗначений;
	
	Результат.Добавить(XDTOТелефон.КодСтраны,  "КодСтраны");
	Результат.Добавить(XDTOТелефон.КодГорода,  "КодГорода");
	Результат.Добавить(XDTOТелефон.Номер,      "НомерТелефона");
	Результат.Добавить(XDTOТелефон.Добавочный, "Добавочный");
	
	Возврат Результат;
КонецФункции

Функция ГруппаОшибокАдреса(ТипОшибки, Сообщение)
	Возврат Новый Структура("ТипОшибки, Сообщение, Поля", ТипОшибки, Сообщение, Новый Массив);
КонецФункции    

Процедура ДобавитьОшибкуЗаполненияАдреса(Группа, ИмяПоля="", Сообщение="", СущностьПоля="")
	Группа.Поля.Добавить(Новый Структура("ИмяПоля, Сообщение, СущностьПоля", ИмяПоля, Сообщение, СущностьПоля));
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////
// Реализация КЛАДР
//

//  Возвращает общий серверный модуль для вызовов подсистемы КЛАДР, обеспечивает работу без подсистемы
//
Функция СерверныйМодульКЛАДР()
	
	Если ОбщегоНазначенияКлиентСервер.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		Возврат	Вычислить("АдресныйКлассификатор");
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция ЧастиКодаКЛАДР(Знач ПолныйКод)
	// СС РРР ГГГ ППП УУУУ АА ДДДД
	
	Результат = Новый Структура("КодРегиона, КодРайона, КодГорода, КодНаселенногоПункта, КодУлицы, КодДома, ПризнакАктуальности");
	
	Результат.КодДома = ПолныйКод % 10000;
	ПолныйКод = Цел(ПолныйКод/10000);
	
	Результат.ПризнакАктуальности = ПолныйКод % 100;
	ПолныйКод = Цел(ПолныйКод/100);
	
	Результат.КодУлицы = ПолныйКод % 10000;
	ПолныйКод = Цел(ПолныйКод/10000);
	
	Результат.КодНаселенногоПункта = ПолныйКод % 1000;
	ПолныйКод = Цел(ПолныйКод/1000);
	
	Результат.КодГорода = ПолныйКод % 1000;
	ПолныйКод = Цел(ПолныйКод/1000);
	
	Результат.КодРайона = ПолныйКод % 1000;
	ПолныйКод = Цел(ПолныйКод/1000);
	
	Результат.КодРегиона = ПолныйКод;
	
	Возврат Результат;
КонецФункции        

Функция РезультатыАвтоПодбораНаселенногоПунктаКЛАДР(Текст, СкрыватьНеактуальныеАдреса, ПредупреждатьОНеактуальных, ВыбиратьСтрок)
	Результат = Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Истина, Новый СписокЗначений);
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ 
		|	КодАдресногоОбъектаВКоде КАК КодАдресногоОбъектаВКоде
		|ПОМЕСТИТЬ
		|	ТаблицаЗагруженныхРегионов
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор КАК ЗагруженныеРегионыВнутр
		|ГДЕ
		|	ТипАдресногоЭлемента = 1
		|	И 1 В (
		|		               ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=ЗагруженныеРегионыВнутр.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 2
		|		ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=ЗагруженныеРегионыВнутр.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 3
		|		ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=ЗагруженныеРегионыВнутр.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 4
		|		ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=ЗагруженныеРегионыВнутр.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 5
		|		ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=ЗагруженныеРегионыВнутр.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 6
		|	)
		|ИНДЕКСИРОВАТЬ ПО
		|	КодАдресногоОбъектаВКоде
		|;//////////////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ 
		|	ВЫБОР 
		|		КОГДА ЗагруженныеРегионы.КодАдресногоОбъектаВКоде ЕСТЬ NULL ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК МожноЗагружатьРегион,
		|	ОтобранныеАдреса.*
		|
		|ИЗ (
		|
		|ВЫБРАТЬ ПЕРВЫЕ " + Формат(ВыбиратьСтрок, "ЧН=; ЧГ=") + "
		|
		|	Адреса.КодАдресногоОбъектаВКоде КАК КодАдресногоОбъектаВКоде,
		|
		|	Адреса.Код КАК Код,
		|	Адреса.Наименование КАК АдресаНаименование,
		|
		|	ВЫБОР
		|		КОГДА Адреса.Наименование ПОДОБНО &НачалоСлова СПЕЦСИМВОЛ ""\"" ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПоложениеВСтроке,
		|
		|	ВЫБОР 
		|		КОГДА Адреса.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		КОГДА Регионы.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		КОГДА Районы.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		КОГДА Города.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		КОГДА НаселенныеПункты.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Неактуален,
		|
		|	Регионы.Сокращение              КАК РегионыСокращение,
		|	Регионы.Наименование            КАК РегионыНаименование,
		|	Районы.Сокращение               КАК РайоныСокращение,
		|	Районы.Наименование             КАК РайоныНаименование,
		|	Города.Сокращение               КАК ГородаСокращение,
		|	Города.Наименование             КАК ГородаНаименование,
		|	НаселенныеПункты.Сокращение     КАК НаселенныеПунктыСокращение,
		|	НаселенныеПункты.Наименование   КАК НаселенныеПунктыНаименование
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор КАК Адреса
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК Регионы
		|ПО
		|	Регионы.ТипАдресногоЭлемента = 1
		|	И Регионы.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Регионы.КодРайонаВКоде            = 0
		|	И Регионы.КодГородаВКоде            = 0
		|	И Регионы.КодНаселенногоПунктаВКоде = 0
		|	И Регионы.КодУлицыВКоде             = 0
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Регионы.ПризнакАктуальности=0","") + "
		|	 
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|   РегистрСведений.АдресныйКлассификатор КАК Районы
		|ПО
		|	Районы.ТипАдресногоЭлемента = 2
		|	И Районы.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Районы.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И Районы.КодГородаВКоде            = 0
		|	И Районы.КодНаселенногоПунктаВКоде = 0
		|	И Районы.КодУлицыВКоде             = 0
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Районы.ПризнакАктуальности=0","") + "
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК Города
		|ПО
		|	Города.ТипАдресногоЭлемента = 3
		|	И Города.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Города.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И Города.КодГородаВКоде            = Адреса.КодГородаВКоде
		|	И Города.КодНаселенногоПунктаВКоде = 0
		|	И Города.КодУлицыВКоде             = 0
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Города.ПризнакАктуальности=0","") + "
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК НаселенныеПункты
		|ПО	
		|	НаселенныеПункты.ТипАдресногоЭлемента = 4
		|	И НаселенныеПункты.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И НаселенныеПункты.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И НаселенныеПункты.КодГородаВКоде            = Адреса.КодГородаВКоде
		|	И НаселенныеПункты.КодНаселенногоПунктаВКоде = Адреса.КодНаселенногоПунктаВКоде
		|	И НаселенныеПункты.КодУлицыВКоде             = 0
		|	" + ?(СкрыватьНеактуальныеАдреса, "И НаселенныеПункты.ПризнакАктуальности=0","") + "
		|
		|ГДЕ
		|	Адреса.ТипАдресногоЭлемента <= 4
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Адреса.ПризнакАктуальности=0","") + "
		|
		|	И (
		|		// В начале строки
		|		Адреса.Наименование ПОДОБНО &НачалоТекста СПЕЦСИМВОЛ ""\""
		|		ИЛИ
		|		// В начале слова
		|		Адреса.Наименование ПОДОБНО &НачалоСлова СПЕЦСИМВОЛ ""\""
		|	)
		|
		|) ОтобранныеАдреса
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗагруженныхРегионов КАК ЗагруженныеРегионы
		|
		|ПО
		|	ЗагруженныеРегионы.КодАдресногоОбъектаВКоде = ОтобранныеАдреса.КодАдресногоОбъектаВКоде
		|
		|УПОРЯДОЧИТЬ ПО 
		|	ОтобранныеАдреса.ПоложениеВСтроке,
		|	ОтобранныеАдреса.РегионыНаименование,
		|	ОтобранныеАдреса.РайоныНаименование, 
		|	ОтобранныеАдреса.ГородаНаименование,
		|	ОтобранныеАдреса.НаселенныеПунктыНаименование,
		|	ОтобранныеАдреса.АдресаНаименование
		|
		|;//////////////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаЗагруженныхРегионов
		|");
	
	СтрокаПодобия = ЗамаскироватьСпецсимволы(Текст);
	Запрос.УстановитьПараметр("НачалоТекста", СтрокаПодобия + "%");
	Запрос.УстановитьПараметр("НачалоСлова",  "% " + СтрокаПодобия + "%");
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат.СлишкомМногоДанных = Выборка.Количество()>ВыбиратьСтрок;
	ДанныеВыбора = Результат.ДанныеВыбора;
	
	ПредупреждениеНеактуальности = НСтр("ru='Адрес ""%1"" неактуален.'");
	КартинкаНеактуальности = БиблиотекаКартинок.КонтактнаяИнформацияНеактуально;
	КартинкаАктуальности   = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		ПолноеНаименование = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
			Выборка.НаселенныеПунктыНаименование, Выборка.НаселенныеПунктыСокращение,
			Выборка.ГородаНаименование,  Выборка.ГородаСокращение,
			Выборка.РайоныНаименование,  Выборка.РайоныСокращение,
			Выборка.РегионыНаименование, Выборка.РегионыСокращение);
		
		Если Выборка.Неактуален Тогда
			ТекстПредупреждения = СтрЗаменить(ПредупреждениеНеактуальности, "%1", ПолноеНаименование);
			Пометка  = Истина;
			Картинка = КартинкаНеактуальности;
		Иначе
			ТекстПредупреждения = Неопределено;
			Пометка  = Ложь;
			Картинка = КартинкаАктуальности;
		КонецЕсли;
		
		Если ПредупреждатьОНеактуальных Тогда
			ДанныеВыбора.Добавить(Новый Структура("Предупреждение, Значение", ТекстПредупреждения,
				Новый Структура("Код, Представление, МожноЗагружатьРегион",
					Выборка.Код, ПолноеНаименование, Выборка.МожноЗагружатьРегион
				)), ПолноеНаименование, Пометка, Картинка);
		Иначе
			ДанныеВыбора.Добавить(ПолноеНаименование, ПолноеНаименование, Пометка, Картинка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция РезультатыАвтоПодбораУлицыКЛАДР(КодНаселенногоПункта, Текст, СкрыватьНеактуальныеАдреса, ПредупреждатьОНеактуальных, ВыбиратьСтрок)
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ " + Формат(ВыбиратьСтрок, "ЧН=; ЧГ=") + "
		|	Улицы.Код КАК Код,
		|
		|	ВЫБОР 
		|		КОГДА Улицы.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Неактуален,
		|
		|	Улицы.Наименование КАК Наименование,
		|	Улицы.Сокращение   КАК Сокращение
		|	
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор КАК НаселенныйПункт
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.АдресныйКлассификатор КАК Улицы
		|ПО
		|	Улицы.ТипАдресногоЭлемента = 5
		|	И Улицы.КодАдресногоОбъектаВКоде  = НаселенныйПункт.КодАдресногоОбъектаВКоде
		|	И Улицы.КодРайонаВКоде            = НаселенныйПункт.КодРайонаВКоде 
		|	И Улицы.КодГородаВКоде            = НаселенныйПункт.КодГородаВКоде 
		|	И Улицы.КодНаселенногоПунктаВКоде = НаселенныйПункт.КодНаселенногоПунктаВКоде
		|   " + ?(СкрыватьНеактуальныеАдреса, "И Улицы.ПризнакАктуальности=0","") + "
		|
		|ГДЕ
		|	НаселенныйПункт.Код = &Код
		|	И (
		|		Улицы.Наименование ПОДОБНО &НачалоСтроки СПЕЦСИМВОЛ ""\""
		|		ИЛИ 
		|		Улицы.Наименование ПОДОБНО &НачалоСлова СПЕЦСИМВОЛ ""\""
		|	)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Улицы.Наименование
		|");
	Запрос.УстановитьПараметр("Код", КодНаселенногоПункта);
	
	СтрокаПоиска = ЗамаскироватьСпецсимволы(Текст);
	Запрос.УстановитьПараметр("НачалоСтроки", СтрокаПоиска +  "%");         // Начало строки
	Запрос.УстановитьПараметр("НачалоСлова",  "% " + СтрокаПоиска + "%");   // Начало слова
	
	ДанныеВыбора = Новый СписокЗначений;
	
	ПредупреждениеНеактуальности = НСтр("ru='Адрес ""%1"" неактуален.'");
	КартинкаНеактуальности = БиблиотекаКартинок.КонтактнаяИнформацияНеактуально;
	КартинкаАктуальности   = Неопределено;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ПолноеНаименование = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
			Выборка.Наименование, Выборка.Сокращение);
		
		Если Выборка.Неактуален Тогда
			ТекстПредупреждения = СтрЗаменить(ПредупреждениеНеактуальности, "%1", ПолноеНаименование);
			Пометка  = Истина;
			Картинка = КартинкаНеактуальности;
		Иначе
			ТекстПредупреждения = Неопределено;
			Пометка  = Ложь;
			Картинка = КартинкаАктуальности;
		КонецЕсли;
		
		Если ПредупреждатьОНеактуальных Тогда
			ДанныеВыбора.Добавить(Новый Структура("Предупреждение, Значение", ТекстПредупреждения,
				Новый Структура("Код, Представление",
					Выборка.Код, ПолноеНаименование
				)), ПолноеНаименование, Пометка, Картинка);
		Иначе
			ДанныеВыбора.Добавить(ПолноеНаименование, ПолноеНаименование, Пометка, Картинка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Новый Структура("СлишкомМногоДанных, ДанныеВыбора",
		Выборка.Количество()>ВыбиратьСтрок, ДанныеВыбора);
	
КонецФункции

Функция НаселенныеПунктыПоПредставлениюКЛАДР(Текст, СкрыватьНеактуальныеАдреса, ВыбиратьСтрок)
	
	// В противоположность к "РезультатыАвтоПодбораНаселенногоПунктаКЛАДР"
	// 1) Разбиваем на части через ","
	// 2) У каждой части отсекаем и выбрасываем слева одно слово до пробела - это сокращение
	// 3) Пробуем найти все варианты по равенству
	
	ЧастиАдреса = КонтактнаяИнформацияКлиентСервер.ЧастиАдреса(Текст);
	Результат = Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Истина, Новый СписокЗначений);
	ДополнительныхСтрокПоиска = ЧастиАдреса.ВГраница();
	Если ДополнительныхСтрокПоиска<0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ОграничительСверху = "яяяяя";
	
	Наименование = ЧастиАдреса[0].Наименование;
	Сокращение   = ЧастиАдреса[0].Сокращение;
	Если ДополнительныхСтрокПоиска=0 И ПустаяСтрока(Сокращение) Тогда
		УсловиеНаименования = "(Адреса.Наименование>=&НаименованиеПоиска0 И Адреса.Наименование<&ОграничительПоиска0)";
	Иначе
		УсловиеНаименования = "(Адреса.Наименование=&НаименованиеПоиска0 И Адреса.Сокращение=&СокращениеПоиска0)";
	КонецЕсли;
	Запрос.УстановитьПараметр("НаименованиеПоиска0", Наименование);
	Запрос.УстановитьПараметр("ОграничительПоиска0", Наименование + ОграничительСверху);
	Запрос.УстановитьПараметр("СокращениеПоиска0",   Сокращение);
	
	Для Позиция=1 По ДополнительныхСтрокПоиска Цикл
		НомерПозиции = Формат(Позиция, "ЧН=; ЧГ=");
		ИмяПараметраНаименование = "НаименованиеПоиска" + НомерПозиции;
		ИмяПараметраСокращение   = "СокращениеПоиска"   + НомерПозиции;
		ИмяПараметраОграничитель = "ОграничительПоиска" + НомерПозиции;
		
		Наименование = ЧастиАдреса[Позиция].Наименование;
		Сокращение   = ЧастиАдреса[Позиция].Сокращение;
		Если ПустаяСтрока(Сокращение) Тогда
			УсловиеНаименования = УсловиеНаименования + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("
				|И (
				|	    (НаселенныеПункты.Наименование>=&%1 И НаселенныеПункты.Наименование<&%2)
				|	ИЛИ (Города.Наименование>=&%1 И Города.Наименование<&%2)
				|	ИЛИ (Районы.Наименование>=&%1 И Районы.Наименование<&%2)
				|	ИЛИ (Регионы.Наименование>=&%1 И Регионы.Наименование<&%2)
				|)",
				ИмяПараметраНаименование, ИмяПараметраОграничитель);
		Иначе
			УсловиеНаименования = УсловиеНаименования + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("
				|И (
				|	    (НаселенныеПункты.Наименование=&%1 И НаселенныеПункты.Сокращение=&%2)
				|	ИЛИ (Города.Наименование=&%1 И Города.Сокращение=&%2)
				|	ИЛИ (Районы.Наименование=&%1 И Районы.Сокращение=&%2)
				|	ИЛИ (Регионы.Наименование=&%1 И Регионы.Сокращение=&%2)
				|)",
				ИмяПараметраНаименование, ИмяПараметраСокращение);
		КонецЕсли;
		Запрос.УстановитьПараметр(ИмяПараметраНаименование, Наименование);
		Запрос.УстановитьПараметр(ИмяПараметраОграничитель, Наименование + ОграничительСверху);
		Запрос.УстановитьПараметр(ИмяПараметраСокращение,   Сокращение);
	КонецЦикла;
	
	Запрос.Текст = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ " + Формат(ВыбиратьСтрок, "ЧН=; ЧГ=") + "
		|	Адреса.Код КАК Код,
		|
		|	ВЫБОР 
		|		КОГДА Адреса.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		КОГДА Регионы.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		КОГДА Районы.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		КОГДА Города.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		КОГДА НаселенныеПункты.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Неактуален,
		|
		|	Регионы.Код          КАК РегионыКод,
		|	Регионы.Сокращение   КАК РегионыСокращение,
		|	Регионы.Наименование КАК РегионыНаименование,
		|
		|	Районы.Код          КАК РайоныКод,
		|	Районы.Сокращение   КАК РайоныСокращение,
		|	Районы.Наименование КАК РайоныНаименование,
		|
		|	Города.Код          КАК ГородаКод,
		|	Города.Сокращение   КАК ГородаСокращение,
		|	Города.Наименование КАК ГородаНаименование,
		|
		|	НаселенныеПункты.Код          КАК НаселенныеПунктыКод,
		|	НаселенныеПункты.Сокращение   КАК НаселенныеПунктыСокращение,
		|	НаселенныеПункты.Наименование КАК НаселенныеПунктыНаименование
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор КАК Адреса
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК Регионы
		|ПО
		|	Регионы.ТипАдресногоЭлемента = 1
		|	И Регионы.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Регионы.КодРайонаВКоде            = 0
		|	И Регионы.КодГородаВКоде            = 0
		|	И Регионы.КодНаселенногоПунктаВКоде = 0
		|	И Регионы.КодУлицыВКоде             = 0
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Регионы.ПризнакАктуальности=0","") + "
		|	 
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК Районы
		|ПО
		|	Районы.ТипАдресногоЭлемента = 2
		|	И Районы.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Районы.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И Районы.КодГородаВКоде            = 0
		|	И Районы.КодНаселенногоПунктаВКоде = 0
		|	И Районы.КодУлицыВКоде             = 0
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Районы.ПризнакАктуальности=0","") + "
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК Города
		|ПО
		|	Города.ТипАдресногоЭлемента = 3
		|	И Города.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Города.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И Города.КодГородаВКоде            = Адреса.КодГородаВКоде
		|	И Города.КодНаселенногоПунктаВКоде = 0
		|	И Города.КодУлицыВКоде             = 0
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Города.ПризнакАктуальности=0","") + "
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК НаселенныеПункты
		|ПО	
		|	НаселенныеПункты.ТипАдресногоЭлемента = 4
		|	И НаселенныеПункты.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И НаселенныеПункты.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И НаселенныеПункты.КодГородаВКоде            = Адреса.КодГородаВКоде
		|	И НаселенныеПункты.КодНаселенногоПунктаВКоде = Адреса.КодНаселенногоПунктаВКоде
		|	И НаселенныеПункты.КодУлицыВКоде             = 0
		|	" + ?(СкрыватьНеактуальныеАдреса, "И НаселенныеПункты.ПризнакАктуальности=0","") + "
		|
		|ГДЕ
		|	Адреса.ТипАдресногоЭлемента <= 4
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Адреса.ПризнакАктуальности=0","") + "
		|	И (" + УсловиеНаименования + ")
		|
		|УПОРЯДОЧИТЬ ПО 
		|	Адреса.ПризнакАктуальности,  // Актуальные - вперед
		|
		|	Адреса.Наименование,
		|	НаселенныеПункты.Наименование,
		|	Города.Наименование,
		|	Районы.Наименование,
		|	Регионы.Наименование
		|";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат.СлишкомМногоДанных = Выборка.Количество()>ВыбиратьСтрок;
	ДанныеВыбора = Результат.ДанныеВыбора;
	
	ПредупреждениеНеактуальности = НСтр("ru='Населенный пункт неактуален.'");
	КартинкаНеактуальности = БиблиотекаКартинок.КонтактнаяИнформацияНеактуально;
	КартинкаАктуальности   = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		ПолноеНаименование = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
			Выборка.НаселенныеПунктыНаименование, Выборка.НаселенныеПунктыСокращение,
			Выборка.ГородаНаименование,  Выборка.ГородаСокращение,
			Выборка.РайоныНаименование,  Выборка.РайоныСокращение,
			Выборка.РегионыНаименование, Выборка.РегионыСокращение);
		
		Если Выборка.Неактуален Тогда
			ТекстПредупреждения = ПредупреждениеНеактуальности;
			Пометка  = Истина;
			Картинка = КартинкаНеактуальности;
		Иначе            
			ТекстПредупреждения = Неопределено;
			Пометка  = Ложь;
			Картинка = КартинкаАктуальности;
		КонецЕсли;
		
		СписокРеквизитов = СписокРеквизитовНаселенныйПунктКЛАДР();
		СписокРеквизитов.НаселенныйПункт.Значение          = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.НаселенныеПунктыНаименование, Выборка.НаселенныеПунктыСокращение);
		СписокРеквизитов.НаселенныйПункт.КодКлассификатора = Выборка.НаселенныеПунктыКод;
		СписокРеквизитов.НаселенныйПункт.Наименование      = Выборка.НаселенныеПунктыНаименование;
		СписокРеквизитов.НаселенныйПункт.Сокращение        = Выборка.НаселенныеПунктыСокращение;
		
		СписокРеквизитов.Город.Значение          = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.ГородаНаименование, Выборка.ГородаСокращение);
		СписокРеквизитов.Город.КодКлассификатора = Выборка.ГородаКод;
		СписокРеквизитов.Город.Наименование      = Выборка.ГородаНаименование;
		СписокРеквизитов.Город.Сокращение        = Выборка.ГородаСокращение;
		
		СписокРеквизитов.Район.Значение          = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.РайоныНаименование, Выборка.РайоныСокращение);
		СписокРеквизитов.Район.КодКлассификатора = Выборка.РайоныКод;
		СписокРеквизитов.Район.Наименование      = Выборка.РайоныНаименование;
		СписокРеквизитов.Район.Сокращение        = Выборка.РайоныСокращение;
		
		СписокРеквизитов.Регион.Значение          = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.РегионыНаименование, Выборка.РегионыСокращение);
		СписокРеквизитов.Регион.КодКлассификатора = Выборка.РегионыКод;
		СписокРеквизитов.Регион.Наименование      = Выборка.РегионыНаименование;
		СписокРеквизитов.Регион.Сокращение   = Выборка.РегионыСокращение;
		
		ДанныеВыбора.Добавить(Новый Структура("Предупреждение, Значение", ТекстПредупреждения,
			Новый Структура("Код, Представление, СписокРеквизитов",
				Выборка.Код, ПолноеНаименование, СписокРеквизитов
			)), ПолноеНаименование, Пометка, Картинка);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция УлицыПоПредставлениюКЛАДР(КодНаселенногоПункта, Текст, СкрыватьНеактуальныеАдреса, ВыбиратьСтрок)
	
	ЧастиАдреса = КонтактнаяИнформацияКлиентСервер.ЧастиАдреса(Текст);
	Результат = Новый Структура("СлишкомМногоДанных, ДанныеВыбора", Истина, Новый СписокЗначений);
	КоличествоСтрокПоиска = ЧастиАдреса.ВГраница();
	Если КоличествоСтрокПоиска<0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	НаименованиеПоиска = ЧастиАдреса[0].Наименование;
	СокращениеПоиска   = ЧастиАдреса[0].Сокращение;
	Если ПустаяСтрока(СокращениеПоиска) Тогда
		УсловиеНаименования = "Улицы.Наименование ПОДОБНО &НачалоСтроки СПЕЦСИМВОЛ ""\"" ИЛИ  Улицы.Наименование ПОДОБНО &НачалоСлова СПЕЦСИМВОЛ ""\"" ";
	Иначе
		УсловиеНаименования = "Улицы.Наименование = &НаименованиеПоиска И Улицы.Сокращение = &СокращениеПоиска"
	КонецЕсли;
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ " + Формат(ВыбиратьСтрок, "ЧН=; ЧГ=") + "
		|	Улицы.Код КАК Код,
		|
		|	ВЫБОР 
		|		КОГДА Улицы.ПризнакАктуальности<>0 ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Неактуален,
		|
		|	Улицы.Наименование КАК Наименование,
		|	Улицы.Сокращение   КАК Сокращение
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор КАК НаселенныйПункт
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.АдресныйКлассификатор КАК Улицы
		|ПО
		|	Улицы.ТипАдресногоЭлемента = 5
		|	И Улицы.КодАдресногоОбъектаВКоде  = НаселенныйПункт.КодАдресногоОбъектаВКоде
		|	И Улицы.КодРайонаВКоде            = НаселенныйПункт.КодРайонаВКоде
		|	И Улицы.КодГородаВКоде            = НаселенныйПункт.КодГородаВКоде 
		|	И Улицы.КодНаселенногоПунктаВКоде = НаселенныйПункт.КодНаселенногоПунктаВКоде
		|
		|ГДЕ
		|	НаселенныйПункт.Код = &КодНаселенногоПункта
		|	" + ?(СкрыватьНеактуальныеАдреса, "И Улицы.ПризнакАктуальности=0","") + "
		|	И (" + УсловиеНаименования + ")
		|
		|УПОРЯДОЧИТЬ ПО 
		|	Улицы.ПризнакАктуальности,
		|	Улицы.Наименование
		|");
	Запрос.УстановитьПараметр("КодНаселенногоПункта", КодНаселенногоПункта);
	Запрос.УстановитьПараметр("НаименованиеПоиска",   НаименованиеПоиска);
	Запрос.УстановитьПараметр("СокращениеПоиска",     СокращениеПоиска);
	
	СтрокаПоиска = ЗамаскироватьСпецсимволы(Текст);
	Запрос.УстановитьПараметр("НачалоСтроки", СтрокаПоиска +  "%");         // Начало строки
	Запрос.УстановитьПараметр("НачалоСлова",  "% " + СтрокаПоиска + "%");   // Начало слова
	
	Выборка = Запрос.Выполнить().Выбрать();
	ДанныеВыбора = Результат.ДанныеВыбора;
	
	ПредупреждениеНеактуальности = НСтр("ru='Улица неактуальна.'");
	КартинкаНеактуальности = БиблиотекаКартинок.КонтактнаяИнформацияНеактуально;
	КартинкаАктуальности   = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		ПолноеНаименование = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
			Выборка.Наименование, Выборка.Сокращение);
		
		Если Выборка.Неактуален Тогда
			ТекстПредупреждения = ПредупреждениеНеактуальности;
			Пометка  = Истина;
			Картинка = КартинкаНеактуальности;
		Иначе            
			ТекстПредупреждения = Неопределено;
			Пометка  = Ложь;
			Картинка = КартинкаАктуальности;
		КонецЕсли;
		
		ДанныеВыбора.Добавить(Новый Структура("Предупреждение, Значение", ТекстПредупреждения,
			Новый Структура("Код, Представление",
				Выборка.Код, ПолноеНаименование
			)), ПолноеНаименование, Пометка, Картинка);
	КонецЦикла;
	
	Результат.СлишкомМногоДанных = Выборка.Количество()>ВыбиратьСтрок;
	Возврат Результат;
КонецФункции

Функция ЯвляетсяПотомкомКЛАДР(КодОбъекта, КодРодителя)
	СтруктураРодителя = ЧастиКодаКЛАДР(КодРодителя);
	СтруктураОбъекта  = ЧастиКодаКЛАДР(КодОбъекта);
	
	Если СтруктураОбъекта.КодУлицы<>0 Тогда
		Возврат СтруктураОбъекта.КодНаселенногоПункта=СтруктураРодителя.КодНаселенногоПункта
		И СтруктураОбъекта.КодГорода=СтруктураРодителя.КодГорода
		И СтруктураОбъекта.КодРайона=СтруктураРодителя.КодРайона
		И СтруктураОбъекта.КодРегиона=СтруктураРодителя.КодРегиона
		
	ИначеЕсли СтруктураОбъекта.КодНаселенногоПункта<>0 Тогда
		Возврат СтруктураОбъекта.КодГорода=СтруктураРодителя.КодГорода
		И СтруктураОбъекта.КодРайона=СтруктураРодителя.КодРайона
		И СтруктураОбъекта.КодРегиона=СтруктураРодителя.КодРегиона
		
	ИначеЕсли СтруктураОбъекта.КодГорода<>0 Тогда
		Возврат СтруктураОбъекта.КодРайона=СтруктураРодителя.КодРайона
		И СтруктураОбъекта.КодРегиона=СтруктураРодителя.КодРегиона
		
	ИначеЕсли СтруктураОбъекта.КодРайона<>0 Тогда
		Возврат СтруктураОбъекта.КодРегиона=СтруктураРодителя.КодРегиона
		
	ИначеЕсли СтруктураОбъекта.КодРегиона<>0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

// Возвращает или пустую структуру-заготовку, или заполненную структуру по коду
Функция СписокРеквизитовНаселенныйПунктКЛАДР(Код=Неопределено)
	
	// Первый уровень - внутренние идентификаторы
	Результат = Новый Структура;
	Результат.Вставить("Регион",          ЭлементАдреснойСтруктуры(НСтр("ru='Регион'"),    "СубъектРФ") );
	Результат.Вставить("Район",           ЭлементАдреснойСтруктуры(НСтр("ru='Район'"),     "СвРайМО/Район") );
	Результат.Вставить("Город",           ЭлементАдреснойСтруктуры(НСтр("ru='Город'"),     "Город") );
	Результат.Вставить("НаселенныйПункт", ЭлементАдреснойСтруктуры(НСтр("ru='Нас.пункт'"), "НаселПункт", ,Истина));
	
	// Подсказки для интерфейса
	Результат.Регион.Вставить("Подсказка",          НСтр("ru='Регион адреса'"));
	Результат.Район.Вставить("Подсказка",           НСтр("ru='Район адреса'"));
	Результат.Город.Вставить("Подсказка",           НСтр("ru='Город адреса'"));
	Результат.НаселенныйПункт.Вставить("Подсказка", НСтр("ru='Населенный пункт адреса'"));
	
	Если Код=Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Мы знаем что там КЛАДР, поэтому можем заполнить поля
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	НаселенныеПункты.Код          КАК НаселенныеПунктыКод,
		|	НаселенныеПункты.Наименование КАК НаселенныеПунктыНаименование,
		|	НаселенныеПункты.Сокращение   КАК НаселенныеПунктыСокращение,
		|	Города.Код                    КАК ГородаКод,
		|	Города.Наименование           КАК ГородаНаименование,
		|	Города.Сокращение             КАК ГородаСокращение,
		|	Районы.Код                    КАК РайоныКод,
		|	Районы.Наименование           КАК РайоныНаименование,
		|	Районы.Сокращение             КАК РайоныСокращение,
		|	Регионы.Код                   КАК РегионыКод,
		|	Регионы.Наименование          КАК РегионыНаименование,
		|	Регионы.Сокращение            КАК РегионыСокращение
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор КАК Адреса
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК Регионы
		|ПО
		|	Регионы.ТипАдресногоЭлемента = 1
		|	И Регионы.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Регионы.КодРайонаВКоде            = 0
		|	И Регионы.КодГородаВКоде            = 0
		|	И Регионы.КодНаселенногоПунктаВКоде = 0
		|	И Регионы.КодУлицыВКоде             = 0
		|	 
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК Районы
		|ПО
		|	Районы.ТипАдресногоЭлемента = 2
		|	И Районы.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Районы.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И Районы.КодГородаВКоде            = 0
		|	И Районы.КодНаселенногоПунктаВКоде = 0
		|	И Районы.КодУлицыВКоде             = 0
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК Города
		|ПО
		|	Города.ТипАдресногоЭлемента = 3
		|	И Города.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И Города.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И Города.КодГородаВКоде            = Адреса.КодГородаВКоде
		|	И Города.КодНаселенногоПунктаВКоде = 0
		|	И Города.КодУлицыВКоде             = 0
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ 
		|	РегистрСведений.АдресныйКлассификатор КАК НаселенныеПункты
		|ПО
		|	НаселенныеПункты.ТипАдресногоЭлемента = 4
		|	И НаселенныеПункты.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
		|	И НаселенныеПункты.КодРайонаВКоде            = Адреса.КодРайонаВКоде
		|	И НаселенныеПункты.КодГородаВКоде            = Адреса.КодГородаВКоде
		|	И НаселенныеПункты.КодНаселенногоПунктаВКоде = Адреса.КодНаселенногоПунктаВКоде
		|	И НаселенныеПункты.КодУлицыВКоде             = 0
		|
		|ГДЕ
		|	Адреса.Код = &Код
		|");
	Запрос.УстановитьПараметр("Код", Код);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.НаселенныйПункт.Значение          = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.НаселенныеПунктыНаименование, Выборка.НаселенныеПунктыСокращение);
		Результат.НаселенныйПункт.КодКлассификатора = Выборка.НаселенныеПунктыКод;
		Результат.НаселенныйПункт.Наименование      = Выборка.НаселенныеПунктыНаименование;
		Результат.НаселенныйПункт.Сокращение        = Выборка.НаселенныеПунктыСокращение;
		
		Результат.Город.Значение          = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.ГородаНаименование, Выборка.ГородаСокращение);
		Результат.Город.КодКлассификатора = Выборка.ГородаКод;
		Результат.Город.Наименование      = Выборка.ГородаНаименование;
		Результат.Город.Сокращение        = Выборка.ГородаСокращение;
		
		Результат.Район.Значение          = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.РайоныНаименование, Выборка.РайоныСокращение);
		Результат.Район.КодКлассификатора = Выборка.РайоныКод;
		Результат.Район.Наименование      = Выборка.РайоныНаименование;
		Результат.Район.Сокращение        = Выборка.РайоныСокращение;
		
		Результат.Регион.Значение          = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.РегионыНаименование, Выборка.РегионыСокращение);
		Результат.Регион.КодКлассификатора = Выборка.РегионыКод;
		Результат.Регион.Наименование      = Выборка.РегионыНаименование;
		Результат.Регион.Сокращение        = Выборка.РегионыСокращение;
	КонецЕсли;
	Возврат Результат;
КонецФункции

// Возвращает или пустую структуру-заготовку, или заполненную структуру по коду
Функция СписокРеквизитовУлицаКЛАДР(Код=Неопределено)
	Результат = Новый Структура("Улица",
		ЭлементАдреснойСтруктуры("Улица", "Улица"),);
	
	Если Код<>Неопределено Тогда
		// Мы знаем что там КЛАДР, поэтому можем заполнить поля
		Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	Улицы.Код          КАК УлицыКод,
			|	Улицы.Наименование КАК УлицыНаименование,
			|	Улицы.Сокращение   КАК УлицыСокращение
			|ИЗ
			|	РегистрСведений.АдресныйКлассификатор КАК Улицы
			|ГДЕ
			|	Улицы.Код = &Код
			|");
		Запрос.УстановитьПараметр("Код", Код);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат.Улица.Значение     = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(Выборка.УлицыНаименование, Выборка.УлицыСокращение);
			Результат.Улица.Код          = Выборка.УлицыКод;
			Результат.Улица.Наименование = Выборка.УлицыНаименование;
			Результат.Улица.Сокращение   = Выборка.УлицыСокращение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;        
КонецФункции

Функция СписокАвтоПодбораЭлементаАдресаКЛАДР(Текст, КодЧастиАдреса, ЧастиАдреса, ПредупреждатьОНеактуальных, ВыбиратьСтрок)
	Возврат СписокАнализаЭлементаАдресаКЛАДР(Текст, "", КодЧастиАдреса, ЧастиАдреса, Истина, ПредупреждатьОНеактуальных, ВыбиратьСтрок);
КонецФункции

Функция СписокВариантовЭлементаАдресаПоТекстуКЛАДР(Текст, КодЧастиАдреса, ЧастиАдреса, ВыбиратьСтрок=50)
	
	ЧастиАдресаПоиска = КонтактнаяИнформацияКлиентСервер.ЧастиАдреса(Текст);
	Если ЧастиАдресаПоиска.Количество()=0 Тогда
		Возврат Неопределено;
	КонецЕсли;        
	Наименование = СокрЛП(ЧастиАдресаПоиска[0].Наименование);
	Сокращение   = СокрЛП(ЧастиАдресаПоиска[0].Сокращение);
	
	ПоискПоПодобию = ПустаяСтрока(Сокращение);
	
	Возврат СписокАнализаЭлементаАдресаКЛАДР(Наименование, Сокращение, КодЧастиАдреса, ЧастиАдреса, ПоискПоПодобию, Истина, ВыбиратьСтрок);
КонецФункции

Функция СписокАнализаЭлементаАдресаКЛАДР(Наименование, Сокращение, КодЧастиАдреса, ЧастиАдреса, ПоискПоПодобию=Истина, ПредупреждатьОНеактуальных=Истина, ВыбиратьСтрок=50)
	
	КодРеквизита = ВРег(КодЧастиАдреса);
	
	Если КодРеквизита="РЕГИОН" Тогда
		Уровень = 1;    
	ИначеЕсли КодРеквизита="РАЙОН" Тогда
		Уровень = 2;    
	ИначеЕсли КодРеквизита="ГОРОД" Тогда
		Уровень = 3;
	ИначеЕсли КодРеквизита="НАСЕЛЕННЫЙПУНКТ" Тогда
		Уровень = 4;
	ИначеЕсли КодРеквизита="УЛИЦА" Тогда
		Уровень = 5;
	Иначе        
		Возврат Неопределено;
	КонецЕсли;        
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ ПЕРВЫЕ " + Формат(ВыбиратьСтрок, "ЧН=; ЧГ=") + "
		|
		|	Адреса.Код                 КАК Код,
		|	Адреса.Индекс              КАК Индекс,
		|	Адреса.ПризнакАктуальности КАК ПризнакАктуальности,
		|	Адреса.Наименование        КАК АдресаНаименование,
		|	Адреса.Сокращение          КАК АдресаСокращение,
		|
		|	ВЫБОР 
		|	КОГДА Адреса.ПризнакАктуальности>0 ТОГДА ИСТИНА 
		|	ИНАЧЕ ЛОЖЬ 
		|	КОНЕЦ КАК Неактуален,
		|
		|	" + ?(Уровень=1, "
		|	ВЫБОР КОГДА РегионЗагружен.Код ЕСТЬ NULL ТОГДА ИСТИНА ИНАЧЕ ЛОЖЬ КОНЕЦ
		|	", "
		|	ЛОЖЬ
		|	") + " КАК МожноЗагружатьРегион
		|
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор КАК Адреса
		|");
	
	// Добавляем информацию о загруженности региона
	Если Уровень=1 Тогда
		Запрос.Текст = Запрос.Текст + "
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	РегистрСведений.АдресныйКлассификатор КАК РегионЗагружен
			|ПО
			|	РегионЗагружен.КодАдресногоОбъектаВКоде = Адреса.КодАдресногоОбъектаВКоде
			|	И РегионЗагружен.ТипАдресногоЭлемента = 1
			|	И 1 В (
			|		               ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=РегионЗагружен.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 2
			|		ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=РегионЗагружен.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 3
			|		ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=РегионЗагружен.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 4
			|		ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=РегионЗагружен.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 5
			|		ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ПЕРВЫЕ 1 1 ИЗ РегистрСведений.АдресныйКлассификатор ГДЕ КодАдресногоОбъектаВКоде=РегионЗагружен.КодАдресногоОбъектаВКоде И ТипАдресногоЭлемента = 6
			|   )
			|";
	КонецЕсли;
	Если Уровень>1 Тогда
		// Район, опираемся на регион
		Запрос.Текст = Запрос.Текст + "
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
			|	РегистрСведений.АдресныйКлассификатор КАК Регионы
			|ПО
			|	Регионы.ТипАдресногоЭлемента = 1
			|	И Регионы.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
			|	И Регионы.КодРайонаВКоде            = 0
			|	И Регионы.КодГородаВКоде            = 0
			|	И Регионы.КодНаселенногоПунктаВКоде = 0
			|	И Регионы.КодУлицыВКоде             = 0
			|	И Регионы.Наименование = &Регион
			|	И Регионы.Сокращение   = &РегионСокращение
			|";
		Запрос.УстановитьПараметр("Регион",           ЧастиАдреса.Регион.Наименование);
		Запрос.УстановитьПараметр("РегионСокращение", ЧастиАдреса.Регион.Сокращение);
	КонецЕсли;
	Если Уровень>2 И (Не ПустаяСтрока(ЧастиАдреса.Район.Наименование)) Тогда
		// Город, опираемся на район + регион, район возможно пуст
		Запрос.Текст = Запрос.Текст + "
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
			|	РегистрСведений.АдресныйКлассификатор КАК Районы
			|ПО
			|	Районы.ТипАдресногоЭлемента = 2
			|	И Районы.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
			|	И Районы.КодРайонаВКоде            = Адреса.КодРайонаВКоде
			|	И Районы.КодГородаВКоде            = 0
			|	И Районы.КодНаселенногоПунктаВКоде = 0
			|	И Районы.КодУлицыВКоде             = 0
			|	И Районы.Наименование = &Район
			|	И Районы.Сокращение   = &РайонСокращение
			|";
		Запрос.УстановитьПараметр("Район",            ЧастиАдреса.Район.Наименование);
		Запрос.УстановитьПараметр("РайонСокращение",  ЧастиАдреса.Район.Сокращение);
	КонецЕсли;
	Если Уровень>3 И (Не ПустаяСтрока(ЧастиАдреса.Город.Наименование)) Тогда
		// Населенный пункт, опираемся на регион + район + город, район возможно пуст, город возможно пуст
		Запрос.Текст = Запрос.Текст + "
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
			|	РегистрСведений.АдресныйКлассификатор КАК Города
			|ПО
			|	Города.ТипАдресногоЭлемента = 3
			|	И Города.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
			|	И Города.КодРайонаВКоде            = Адреса.КодРайонаВКоде
			|	И Города.КодГородаВКоде            = Адреса.КодГородаВКоде
			|	И Города.КодНаселенногоПунктаВКоде = 0
			|	И Города.КодУлицыВКоде            = 0
			|	И Города.Наименование = &Город
			|	И Города.Сокращение   = &ГородСокращение
			|";
		Запрос.УстановитьПараметр("Город",           ЧастиАдреса.Город.Наименование);
		Запрос.УстановитьПараметр("ГородСокращение", ЧастиАдреса.Город.Сокращение);
	КонецЕсли;
	Если Уровень>4 И (Не ПустаяСтрока(ЧастиАдреса.НаселенныйПункт.Наименование)) Тогда
		// Улица, опираемся на регион + район + город + населенный пункт, возможно пустые
		Запрос.Текст = Запрос.Текст + "
			|ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
			|	РегистрСведений.АдресныйКлассификатор КАК НаселенныеПункты
			|ПО
			|	НаселенныеПункты.ТипАдресногоЭлемента = 4
			|	И НаселенныеПункты.КодАдресногоОбъектаВКоде  = Адреса.КодАдресногоОбъектаВКоде
			|	И НаселенныеПункты.КодРайонаВКоде            = Адреса.КодРайонаВКоде
			|	И НаселенныеПункты.КодГородаВКоде            = Адреса.КодГородаВКоде
			|	И НаселенныеПункты.КодНаселенногоПунктаВКоде = Адреса.КодНаселенногоПунктаВКоде
			|	И НаселенныеПункты.КодУлицыВКоде             = 0
			|
			|	И НаселенныеПункты.Наименование = &НаселенныйПункт
			|	И НаселенныеПункты.Сокращение   = &НаселенныйПунктСокращение
			|";
		Запрос.УстановитьПараметр("НаселенныйПункт",           ЧастиАдреса.НаселенныйПункт.Наименование);
		Запрос.УстановитьПараметр("НаселенныйПунктСокращение", ЧастиАдреса.НаселенныйПункт.Сокращение);
	КонецЕсли;
	
	Если ПустаяСтрока(Сокращение) Тогда
		УсловиеСокращения = "";
	Иначе
		Если ПоискПоПодобию Тогда
			УсловиеСокращения = "И Адреса.Сокращение ПОДОБНО &Сокращение СПЕЦСИМВОЛ ""\"" ";
			Запрос.УстановитьПараметр("Сокращение", ЗамаскироватьСпецсимволы(Сокращение) + "%");
		Иначе
			УсловиеСокращения = "И Адреса.Сокращение = &Сокращение";
			Запрос.УстановитьПараметр("Сокращение", Сокращение);
		КонецЕсли;
	КонецЕсли;        
	
	Если ПоискПоПодобию Тогда
		УсловиеНаименования = "И Адреса.Наименование ПОДОБНО &Наименование СПЕЦСИМВОЛ ""\"" ";
		Запрос.УстановитьПараметр("Наименование", ЗамаскироватьСпецсимволы(Наименование) + "%");
	Иначе
		УсловиеНаименования = "И Адреса.Наименование = &Наименование";
		Запрос.УстановитьПараметр("Наименование", Наименование);
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "    
		|ГДЕ
		|	Адреса.ТипАдресногоЭлемента = &Уровень
		|	" + УсловиеНаименования + "
		|	" + УсловиеСокращения + "
		|УПОРЯДОЧИТЬ ПО 
		|	Адреса.ПризнакАктуальности,
		|
		|	Адреса.Наименование
		|";
	
	Запрос.УстановитьПараметр("Уровень", Уровень);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПредупреждениеНеактуальности = НСтр("ru='Адрес ""%1"" неактуален.'");
	КартинкаНеактуальности = БиблиотекаКартинок.КонтактнаяИнформацияНеактуально;
	КартинкаАктуальности   = Неопределено;
	
	ДанныеВыбора = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		
		ПолноеНаименование = КонтактнаяИнформацияКлиентСервер.ПолноеНаименование(
			Выборка.АдресаНаименование, Выборка.АдресаСокращение);
		
		Если Выборка.Неактуален Тогда
			ТекстПредупреждения = СтрЗаменить(ПредупреждениеНеактуальности, "%1", ПолноеНаименование);
			Пометка  = Истина;
			Картинка = КартинкаНеактуальности;
		Иначе            
			ТекстПредупреждения = Неопределено;
			Пометка  = Ложь;
			Картинка = КартинкаАктуальности;
		КонецЕсли;
		
		Если ПредупреждатьОНеактуальных Тогда
			
			ДанныеСтроки = Новый Структура(
				"Код, Представление, ПолноеНаименование, Наименование, Сокращение, МожноЗагружатьРегион",
				Выборка.Код, ПолноеНаименование, ПолноеНаименование, 
				Выборка.АдресаНаименование, Выборка.АдресаСокращение, Выборка.МожноЗагружатьРегион);
			
			ДанныеВыбора.Добавить(Новый Структура("Предупреждение, Значение", ТекстПредупреждения, ДанныеСтроки),
				ПолноеНаименование, Пометка, Картинка);
		Иначе
			ДанныеВыбора.Добавить(ПолноеНаименование, ПолноеНаименование, Пометка, Картинка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеВыбора;
КонецФункции

Процедура УстановитьНаселенныйПунктАдресаПоКодуКЛАДР(XDTOАдресРФ, КодКлассификатора)
	
	МодульКЛАДР = СерверныйМодульКЛАДР();
	Если МодульКЛАДР=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураКЛАДР = Новый Структура;
	МодульКЛАДР.ПоКодуАдресногоЭлементаВСтруктуруПолучитьЕгоКомпоненты(КодКлассификатора, СтруктураКЛАДР);
	XDTOАдресРФ.СубъектРФ = СокрЛП(СтруктураКЛАДР.Регион);
	РайонАдреса(XDTOАдресРФ, СокрЛП(СтруктураКЛАДР.Район));
	XDTOАдресРФ.Город = СокрЛП(СтруктураКЛАДР.Город);
	XDTOАдресРФ.НаселПункт = СокрЛП(СтруктураКЛАДР.НаселенныйПункт);
	
КонецПроцедуры

Процедура УстановитьУлицуАдресаПоКодуКЛАДР(XDTOАдресРФ, КодКлассификатора)
	МодульКЛАДР = СерверныйМодульКЛАДР();
	Если МодульКЛАДР=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураКЛАДР = Новый Структура;
	МодульКЛАДР.ПоКодуАдресногоЭлементаВСтруктуруПолучитьЕгоКомпоненты(КодКлассификатора, СтруктураКЛАДР);
	Если Не ПустаяСтрока(СтруктураКЛАДР.Улица) Тогда
		XDTOАдресРФ.Улица = СтруктураКЛАДР.Улица;
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьОшибкиАдресаКЛАДР(XDTOАдресРФ, Результат)
	
	МодульКЛАДР = СерверныйМодульКЛАДР();
	Если МодульКЛАДР=Неопределено Тогда
		// Нет классификатора КЛАДР
		Возврат;
	КонецЕсли;
	
	Если МодульКЛАДР.ЧислоЗаполненныхАдресныхОбъектов()=0 Тогда
		// Классификатор КЛАДР не загружен ни по одному региону
		Результат.Добавить(Новый Структура("СущностьПоля, ПутьXPath"),
			НСтр("ru='Адресный классификатор не загружен.'"));
		Возврат;
	КонецЕсли;
	
	Данные = СтруктураКЛАДРПоАдресуРФ(XDTOАдресРФ);
	
	// Проверим регион
	КодРегиона = КодРегионаКЛАДР(Данные.Регион);
	Если ПустаяСтрока(КодРегиона) Тогда
		// Переданное значение - не регион с точки зрения КЛАДР
		Результат.Добавить(Новый Структура("СущностьПоля, ПутьXPath"), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Регион ""%1"" не является субъектом РФ по классификатору'"), 
				Данные.Регион));
		Возврат;
	КонецЕсли;
	
	// Проверим классификатор на загруженность по региону
	Если Не МодульКЛАДР.АдресныйЭлементЗагружен(Данные.Регион) Тогда
		Результат.Добавить(Новый Структура("СущностьПоля, ПутьXPath"), 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Адресный классификатор по региону ""%1"" не загружен'"), 
				Данные.Регион));
		Возврат;
	КонецЕсли;
		
	// Проверим на соответсвие частей классификатору
	СтруктураПроверки = МодульКЛАДР.ПроверитьСоответствиеАдресаКЛАДРу(
		Данные.Индекс, Данные.Регион, Данные.Район, Данные.Город, Данные.НаселенныйПункт, Данные.Улица, 
		Данные.Дом, Данные.Корпус);
	Если СтруктураПроверки=Неопределено Или Не СтруктураПроверки.ЕстьОшибки Тогда
		// Нет ошибок, адрес корректен
		Возврат;
	КонецЕсли;
	
	РеквизитыПроверки = СписокРеквизитовАдресаКЛАДР();
	
	Для Каждого Элемент Из СтруктураПроверки.СтруктураОшибок Цикл
		ИмяРеквизита = Элемент.Ключ;
		Если РеквизитыПроверки.Свойство(ИмяРеквизита) Тогда
			Результат.Добавить(
				Новый Структура("СущностьПоля, ПутьXPath", Элемент.Значение, РеквизитыПроверки[ИмяРеквизита].ПутьXPath),
				Элемент.Значение);
		Иначе
			Результат.Добавить(
				Новый Структура("СущностьПоля, ПутьXPath"),
				Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция КодОбъектаПоЧастямАдресаКЛАДР(ЧастиАдреса, СкрыватьНеактуальные=Ложь) 
	Перем ЗначениеЧастиАдреса;
	
	// Проверяем от младшего к старшему
	Улица = "";
	Если ЧастиАдреса.Свойство("Улица", ЗначениеЧастиАдреса) Тогда
		Если ЗначениеЧастиАдреса.Свойство("КодКлассификатора") И ЗначениеЗаполнено(ЗначениеЧастиАдреса.КодКлассификатора) Тогда
			Возврат ЗначениеЧастиАдреса.КодКлассификатора;
		КонецЕсли;
		Улица = СокрЛП(ЗначениеЧастиАдреса.Значение);
	КонецЕсли;
	
	НаселенныйПункт = "";
	Если ЧастиАдреса.Свойство("НаселенныйПункт", ЗначениеЧастиАдреса) Тогда
		Если ЗначениеЧастиАдреса.Свойство("КодКлассификатора") И ЗначениеЗаполнено(ЗначениеЧастиАдреса.КодКлассификатора) Тогда
			Возврат ЗначениеЧастиАдреса.КодКлассификатора;
		КонецЕсли;
		НаселенныйПункт = СокрЛП(ЗначениеЧастиАдреса.Значение);
	КонецЕсли;
	
	Город = "";
	Если ЧастиАдреса.Свойство("Город", ЗначениеЧастиАдреса) Тогда
		Если ЗначениеЧастиАдреса.Свойство("КодКлассификатора") И ЗначениеЗаполнено(ЗначениеЧастиАдреса.КодКлассификатора) Тогда
			Возврат ЗначениеЧастиАдреса.КодКлассификатора;
		КонецЕсли;
		Город = СокрЛП(ЗначениеЧастиАдреса.Значение);
	КонецЕсли;
	
	Район = "";
	Если ЧастиАдреса.Свойство("Район", ЗначениеЧастиАдреса) Тогда
		Если ЗначениеЧастиАдреса.Свойство("КодКлассификатора") И ЗначениеЗаполнено(ЗначениеЧастиАдреса.КодКлассификатора) Тогда
			Возврат ЗначениеЧастиАдреса.КодКлассификатора;
		КонецЕсли;
		Район = СокрЛП(ЗначениеЧастиАдреса.Значение);
	КонецЕсли;
	
	Регион = "";
	Если ЧастиАдреса.Свойство("Регион", ЗначениеЧастиАдреса) Тогда
		Если ЗначениеЧастиАдреса.Свойство("КодКлассификатора") И ЗначениеЗаполнено(ЗначениеЧастиАдреса.КодКлассификатора) Тогда
			Возврат ЗначениеЧастиАдреса.КодКлассификатора;
		КонецЕсли;
		Регион = СокрЛП(ЗначениеЧастиАдреса.Значение);
	КонецЕсли;
	
	// Проверяем по классификатору
	МодульКЛАДР = СерверныйМодульКЛАДР();
	Если МодульКЛАДР=Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = МодульКЛАДР.ПроверитьСоответствиеАдресаКЛАДРу("", Регион, Район, Город, НаселенныйПункт, Улица);
	
	Если Результат.Свойство("Улица", ЗначениеЧастиАдреса) Тогда
		Возврат ЗначениеЧастиАдреса.Код;
	ИначеЕсли Результат.Свойство("НаселенныйПункт", ЗначениеЧастиАдреса) Тогда
		Возврат ЗначениеЧастиАдреса.Код;
	ИначеЕсли Результат.Свойство("Город", ЗначениеЧастиАдреса) Тогда
		Возврат ЗначениеЧастиАдреса.Код;
	ИначеЕсли Результат.Свойство("Район", ЗначениеЧастиАдреса) Тогда
		Возврат ЗначениеЧастиАдреса.Код;
	ИначеЕсли Результат.Свойство("Регион", ЗначениеЧастиАдреса) Тогда
		Возврат ЗначениеЧастиАдреса.Код;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция КодРегионаКЛАДР(ПолноеНаименование)
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	КодАдресногоОбъектаВКоде КАК Код
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор
		|ГДЕ
		|	ТипАдресногоЭлемента = 1
		|	И КодРайонаВКоде            = 0
		|	И КодГородаВКоде            = 0
		|	И КодНаселенногоПунктаВКоде = 0
		|	И КодУлицыВКоде             = 0
		|
		|	И Наименование = &Наименование
		|	И Сокращение   = &Сокращение
		|");
	Параметры = КонтактнаяИнформацияКлиентСервер.НаименованиеСокращение(ПолноеНаименование);
	Запрос.УстановитьПараметр("Наименование", Параметры.Наименование);
	Запрос.УстановитьПараметр("Сокращение",   Параметры.Сокращение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Формат(Выборка.Код, "ЧЦ=2; ЧН=; ЧВН=");
	КонецЕсли;
	
	Возврат "";
КонецФункции

Функция РегионКодаКЛАДР(Знач Код)
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Наименование + "" "" + Сокращение КАК Регион
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор
		|ГДЕ
		|	ТипАдресногоЭлемента = 1
		|	И КодАдресногоОбъектаВКоде  = &Код
		|	И КодРайонаВКоде            = 0
		|	И КодГородаВКоде            = 0
		|	И КодНаселенногоПунктаВКоде = 0
		|	И КодУлицыВКоде             = 0
		|");
		
	Если ТипЗнч(Код)=Тип("Число") Тогда
		КодЧислом = Код;
	Иначе 
		ТипЧисло = Новый ОписаниеТипов("Число");
		КодЧислом = ТипЧисло.ПривестиЗначение(Код);
	КонецЕсли;
	Запрос.УстановитьПараметр("Код", КодЧислом);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Регион;
	КонецЕсли;
	
	Возврат "";
КонецФункции

Функция ВсеРегионыКЛАДР() 
	Запрос = Новый Запрос("
		|ВЫБРАТЬ 
		|	КодАдресногоОбъектаВКоде          КАК Код,
		|	Наименование                      КАК Наименование,
		|	Сокращение                        КАК Сокращение,
		|	Наименование + "" "" + Сокращение КАК Представление
		|ИЗ
		|	РегистрСведений.АдресныйКлассификатор
		|ГДЕ
		|	ТипАдресногоЭлемента = 1
		|	И КодРайонаВКоде            = 0
		|	И КодГородаВКоде            = 0
		|	И КодНаселенногоПунктаВКоде = 0
		|	И КодУлицыВКоде             = 0
		|");
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("Код");
	Результат.Индексы.Добавить("Представление");
	
	Возврат Результат;
КонецФункции

Функция ОпределитьПочтовыйИндексАдресаКЛАДР(XDTOАдресРФ)
	
	МодульКЛАДР = СерверныйМодульКЛАДР();
	Если МодульКЛАДР<>Неопределено Тогда
		Данные = СтруктураКЛАДРПоАдресуРФ(XDTOАдресРФ);
		Индекс = МодульКЛАДР.ПолучитьИндекс(
		Данные.Регион, Данные.Район, Данные.Город, Данные.НаселенныйПункт, Данные.Улица, 
		Данные.Дом, Данные.Корпус);
		Если Не ПустаяСтрока(Индекс) Тогда
			Возврат СокрЛП(Индекс);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

// Конструктор таблицы значений
Функция ТаблицаЗначений(СписокКолонок, СписокИндексов="")
	ТаблицаРезультата = Новый ТаблицаЗначений;
	
	Для Каждого КлючЗначение Из (Новый Структура(СписокКолонок)) Цикл
		ТаблицаРезультата.Колонки.Добавить(КлючЗначение.Ключ);
	КонецЦикла;
	
	СтрокиИндексов = СтрЗаменить(СписокИндексов, "|", Символы.ПС);
	Для НомерИндекса=1 По СтрЧислоСтрок(СтрокиИндексов) Цикл
		КолонкиИндекса = СокрЛП(СтрПолучитьСтроку(СтрокиИндексов, НомерИндекса));
		Для Каждого КлючЗначение Из (Новый Структура(КолонкиИндекса)) Цикл
			ТаблицаРезультата.Индексы.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаРезультата;
КонецФункции

// Конструктор внутренней структуры элемента адреса
Функция ЭлементАдреснойСтруктуры(Заголовок, ПутьXPath, Значение=Неопределено, Предопределенный=Ложь)
	Возврат Новый Структура("Заголовок, ПутьXPath, Значение, Предопределенный, Наименование, Сокращение, КодКлассификатора", 
		Заголовок, ПутьXPath, Значение, Предопределенный);
КонецФункции

// Возвращает строку для поиска в операторе ПОДОБНО
Функция ЗамаскироватьСпецсимволы(Текст)
	Результат = Текст;
	Спецсимвол = "\";
	Служебные  = "%_[]^" + Спецсимвол;
	Для Индекс=1 По СтрДлина(Служебные) Цикл
		Символ = Сред(Служебные, Индекс, 1);
		Результат = СтрЗаменить(Результат, Символ, Спецсимвол + Символ);
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция СписокРеквизитовАдресаКЛАДР() 
	
	Результат = СписокРеквизитовНаселенныйПунктКЛАДР();
	Для Каждого КлючЗначение Из СписокРеквизитовУлицаКЛАДР() Цикл
		Результат.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	// Индекс
	Результат.Вставить("Индекс", ЭлементАдреснойСтруктуры("Индекс", 
		КонтактнаяИнформацияКлиентСерверПовтИсп.XPathПочтовогоИндекса()));
	// Дом
	Результат.Вставить("Дом", ЭлементАдреснойСтруктуры("Дом",
		КонтактнаяИнформацияКлиентСерверПовтИсп.XPathНомераДополнительногоОбъектаАдресации("Дом")));
	// Корпус
	Результат.Вставить("Корпус", ЭлементАдреснойСтруктуры("Корпус", 
		КонтактнаяИнформацияКлиентСерверПовтИсп.XPathНомераДополнительногоОбъектаАдресации("Корпус")));
	
	Возврат Результат;
КонецФункции

Функция СтруктураКЛАДРПоАдресуРФ(XDTOАдресРФ)
	Результат = Новый Структура;
	
	// Поля, известные КЛАДР
	Результат.Вставить("Индекс", ПочтовыйИндексАдреса(XDTOАдресРФ));
	Результат.Вставить("Регион", XDTOАдресРФ.СубъектРФ);
	Результат.Вставить("Район", РайонАдреса(XDTOАдресРФ));
	Результат.Вставить("Город", XDTOАдресРФ.Город);
	Результат.Вставить("НаселенныйПункт", XDTOАдресРФ.НаселПункт);
	Результат.Вставить("Улица", XDTOАдресРФ.Улица);
	
	ТаблицаЗданий = ЗданияИПомещенияАдреса(XDTOАдресРФ).Здания;
	ВсегоЗданий   = ТаблицаЗданий.Количество();
	
	// По КЛАДРу можно проверить только дом и корпус
	Дом = Неопределено;
	Для Каждого Вариант Из ВариантыДанныхДом().ВариантыТипа Цикл
		Строка = ТаблицаЗданий.Найти(Вариант, "Тип");
		Дом = ?(Строка=Неопределено, Неопределено, Строка.Значение);
		Если Не ПустаяСтрока(Дом) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Результат.Вставить("Дом", Дом);
	
	Корпус = Неопределено;
	Для Каждого Вариант Из ВариантыДанныхСтроение().ВариантыТипа Цикл
		Строка = ТаблицаЗданий.Найти(Вариант, "Тип");
		Корпус = ?(Строка=Неопределено, Неопределено, Строка.Значение);
		Если Не ПустаяСтрока(Корпус) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Результат.Вставить("Корпус", Корпус);
	
	Возврат Результат;
КонецФункции

// Внутреннее для сериализации
Функция ДесериализацияАдресаОбщая(ЗначенияПолей, Представление, ОжидаемыйТип=Неопределено)
	
	Если КонтактнаяИнформацияКлиентСервер.ЭтоКонтактнаяИнформацияВXML(ЗначенияПолей) Тогда
		// Общий формат контактной информации
		Возврат ДесериализацияКонтактнойИнформации(ЗначенияПолей, ОжидаемыйТип);
	КонецЕсли;
	
	Если ОжидаемыйТип<>Неопределено Тогда
		Если ОжидаемыйТип<>Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
			ВызватьИсключение НСтр("ru='Ошибка десериализации контактной информации, ожидается адрес'");
		КонецЕсли;
	КонецЕсли;
	
	// Старый формат через разделитель строк и равенство
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
	
	Результат.Представление = Представление;
	Результат.Комментарий   = "";
	Результат.Состав        = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Адрес"));
	
	АдресРоссийский = Истина;
	НазваниеРоссии  = ВРег(Справочники.СтраныМира.Россия.Наименование);
	
	ЭлементКвартира = Неопределено;
	ЭлементКорпус   = Неопределено;
	ЭлементДом      = Неопределено;
	
	// Российский
	АдресРФ = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "АдресРФ"));
	// Общий
	Адрес = Результат.Состав;
	
	ТипЗначенийПолей = ТипЗнч(ЗначенияПолей);
	Если ТипЗначенийПолей=Тип("СписокЗначений") Тогда
		СписокПолей = ЗначенияПолей;
	ИначеЕсли ТипЗначенийПолей=Тип("Структура") Тогда
		СписокПолей = УправлениеКонтактнойИнформациейКлиентСервер.ПреобразоватьСтрокуВСписокПолей(
			УправлениеКонтактнойИнформациейКлиентСервер.СтрокаПолей(ЗначенияПолей, Ложь));
	Иначе        
		// Уже преобразовано в строку
		СписокПолей = УправлениеКонтактнойИнформациейКлиентСервер.ПреобразоватьСтрокуВСписокПолей(ЗначенияПолей);
	КонецЕсли;
	
	ТипКвартирыНеопределен = Истина;
	ТипКорпусаНеопределен  = Истина;
	ТипДомаНеопределен     = Истина;
	
	Для Каждого ЭлементСписка Из СписокПолей Цикл
		ИмяПоля = ВРег(ЭлементСписка.Представление);
		
		Если ИмяПоля="ИНДЕКС" Тогда
			ЭлементИндекс = СоздатьДопАдрЭл(АдресРФ);
			ЭлементИндекс.ТипАдрЭл = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииПочтовогоИндекса();
			ЭлементИндекс.Значение = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля="СТРАНА" Тогда
			Адрес.Страна = ЭлементСписка.Значение;
			Если ВРег(ЭлементСписка.Значение)<>НазваниеРоссии Тогда
				АдресРоссийский = Ложь;
			КонецЕсли;
			
		ИначеЕсли ИмяПоля="КОДСТРАНЫ" Тогда
			;
			
		ИначеЕсли ИмяПоля="КОДРЕГИОНА" Тогда
			АдресРФ.СубъектРФ = РегионКода(ЭлементСписка.Значение);
			
		ИначеЕсли ИмяПоля="РЕГИОН" Тогда
			АдресРФ.СубъектРФ = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля="РАЙОН" Тогда
			Если АдресРФ.СвРайМО=Неопределено Тогда
				АдресРФ.СвРайМО = ФабрикаXDTO.Создать( АдресРФ.Тип().Свойства.Получить("СвРайМО").Тип )
			КонецЕсли;
			АдресРФ.СвРайМО.Район = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля="ГОРОД" Тогда
			АдресРФ.Город = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля="НАСЕЛЕННЫЙПУНКТ" Тогда
			АдресРФ.НаселПункт = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля="УЛИЦА" Тогда
			АдресРФ.Улица = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля="ТИПДОМА" Тогда
			Если ЭлементДом=Неопределено Тогда
				ЭлементДом = СоздатьНомерДопАдрЭл(АдресРФ);
			КонецЕсли;
			ЭлементДом.Тип = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииОбъектаАдресации(ЭлементСписка.Значение);
			ТипДомаНеопределен = Ложь;
			
		ИначеЕсли ИмяПоля="ДОМ" Тогда
			Если ЭлементДом=Неопределено Тогда
				ЭлементДом = СоздатьНомерДопАдрЭл(АдресРФ);
			КонецЕсли;
			ЭлементДом.Значение = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля="ТИПКОРПУСА" Тогда
			Если ЭлементКорпус=Неопределено Тогда
				ЭлементКорпус = СоздатьНомерДопАдрЭл(АдресРФ);
			КонецЕсли;
			ЭлементКорпус.Тип = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииОбъектаАдресации(ЭлементСписка.Значение);
			ТипКорпусаНеопределен = Ложь;
			
		ИначеЕсли ИмяПоля="КОРПУС" Тогда
			Если ЭлементКорпус=Неопределено Тогда
				ЭлементКорпус = СоздатьНомерДопАдрЭл(АдресРФ);
			КонецЕсли;
			ЭлементКорпус.Значение = ЭлементСписка.Значение;
			
		ИначеЕсли ИмяПоля="ТИПКВАРТИРЫ" Тогда
			Если ЭлементКвартира=Неопределено Тогда
				ЭлементКвартира = СоздатьНомерДопАдрЭл(АдресРФ);
			КонецЕсли;
			ЭлементКвартира.Тип = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииОбъектаАдресации(ЭлементСписка.Значение);
			ТипКвартирыНеопределен = Ложь;
			
		ИначеЕсли ИмяПоля="КВАРТИРА" Тогда
			Если ЭлементКвартира=Неопределено Тогда
				ЭлементКвартира = СоздатьНомерДопАдрЭл(АдресРФ);
			КонецЕсли;
			ЭлементКвартира.Значение = ЭлементСписка.Значение;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Умолчания
	Если ТипДомаНеопределен И ЭлементДом<>Неопределено Тогда
		ЭлементДом.Тип = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииОбъектаАдресации("Дом");
	КонецЕсли;
	
	Если ТипКорпусаНеопределен И ЭлементКорпус<>Неопределено Тогда
		ЭлементКорпус.Тип = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииОбъектаАдресации("Корпус");
	КонецЕсли;
	
	Если ТипКвартирыНеопределен И ЭлементКвартира<>Неопределено Тогда
		ЭлементКвартира.Тип = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииОбъектаАдресации("Квартира");
	КонецЕсли;
	
	Адрес.Состав = ?(АдресРоссийский, АдресРФ, Представление);
	
	Возврат Результат;
КонецФункции

Процедура ВставитьЗданиеПомещение(XDTOАдрес, Тип, Значение)
	Если ПустаяСтрока(Значение) Тогда
		Возврат;
	КонецЕсли;
	
	Запись = XDTOАдрес.Получить( КонтактнаяИнформацияКлиентСерверПовтИсп.XPathНомераДополнительногоОбъектаАдресации(Тип) );
	Если Запись=Неопределено Тогда
		Запись = XDTOАдрес.ДопАдрЭл.Добавить( ФабрикаXDTO.Создать(XDTOАдрес.ДопАдрЭл.ВладеющееСвойство.Тип) );
		Запись.Номер = ФабрикаXDTO.Создать(Запись.Свойства().Получить("Номер").Тип);
		Запись.Номер.Значение = Значение;
		
		КодТипа = КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииОбъектаАдресации(Тип);
		Если КодТипа=Неопределено Тогда
			КодТипа = Тип;
		КонецЕсли;
		Запись.Номер.Тип = КодТипа
	Иначе        
		Запись.Значение = Значение;
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьНомерДопАдрЭл(АдресРФ)
	ДопАдрЭл = СоздатьДопАдрЭл(АдресРФ);
	ДопАдрЭл.Номер = ФабрикаXDTO.Создать(ДопАдрЭл.Тип().Свойства.Получить("Номер").Тип);
	Возврат ДопАдрЭл.Номер;
КонецФункции

Функция СоздатьДопАдрЭл(АдресРФ)
	СвойствоДопАдрЭл = АдресРФ.ДопАдрЭл.ВладеющееСвойство;
	ДопАдрЭл = ФабрикаXDTO.Создать(СвойствоДопАдрЭл.Тип);
	АдресРФ.ДопАдрЭл.Добавить(ДопАдрЭл);
	Возврат ДопАдрЭл;
КонецФункции

Функция СвРайМО(АдресРФ)
	Если АдресРФ.СвРайМО<>Неопределено Тогда
		Возврат АдресРФ.СвРайМО;
	КонецЕсли;
	
	АдресРФ.СвРайМО = ФабрикаXDTO.Создать( АдресРФ.Свойства().Получить("СвРайМО").Тип );
	Возврат АдресРФ.СвРайМО;
КонецФункции

Функция ДесериализацияАдресаПоПредставлениюКЛАДР(Знач Представление)
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
	Результат.Состав = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "Адрес"));
	Результат.Представление = Представление;
	
	ДанныеАнализа = ЧастиАдресаТаблицей(Представление);
	Если ДанныеАнализа.Количество()=0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Адрес = Результат.Состав;
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ 
		|	ДанныеАдреса.Позиция0     КАК Позиция0,
		|	ДанныеАдреса.Позиция      КАК Позиция,
		|	ДанныеАдреса.Значение     КАК Значение,
		|	ДанныеАдреса.Наименование КАК Наименование,
		|	ДанныеАдреса.Сокращение   КАК Сокращение,
		|	ДанныеАдреса.Начало       КАК Начало,
		|	ДанныеАдреса.Длина        КАК Длина
		|ПОМЕСТИТЬ 
		|	ДанныеАдреса
		|ИЗ
		|	&ДанныеАдреса КАК ДанныеАдреса
		|ИНДЕКСИРОВАТЬ ПО
		|	Позиция0, Позиция, Наименование, Сокращение
		|;//////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ 
		|	ДанныеАдреса.Позиция      КАК Позиция,
		|	ДанныеАдреса.Значение     КАК Значение,
		|	ДанныеАдреса.Наименование КАК Наименование,
		|	ДанныеАдреса.Сокращение   КАК Сокращение,
		|
		|	ДанныеАдреса.Начало       КАК Начало,
		|	ДанныеАдреса.Длина        КАК Длина,
		|	ЛОЖЬ                      КАК Обработано,
		|
		|	МИНИМУМ(АдресныйКлассификатор.ТипАдресногоЭлемента) КАК УровеньПоКлассификатору,
		|	ВЫБОР 
		|		КОГДА МИНИМУМ(АдресныйКлассификатор.ТипАдресногоЭлемента) ЕСТЬ NULL ТОГДА ЛОЖЬ 
		|		ИНАЧЕ ИСТИНА 
		|	КОНЕЦ КАК НайденоПоКлассификатору,
		|
		|	МИНИМУМ(АдресныеСокращения.Уровень) КАК УровеньПоСокращениям,
		|	ВЫБОР 
		|		КОГДА МИНИМУМ(АдресныеСокращения.Уровень) ЕСТЬ NULL ТОГДА ЛОЖЬ 
		|		ИНАЧЕ ИСТИНА 
		|	КОНЕЦ КАК НайденоПоСокращениям,
		|	
		|	ВЫБОР
		|		КОГДА НЕ МИНИМУМ(СтраныМира.Наименование) ЕСТЬ NULL ТОГДА ИСТИНА
		|		КОГДА ДанныеАдреса.Значение В (&СтраныКлассификатора) ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НайденоПоСтранамМира,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеАдреса.Значение=СтранаРоссия.Наименование ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоСтранаРоссия,
		|	
		|	ВЫБОР
		|		КОГДА ДанныеАдреса.Наименование ПОДОБНО ""[0-9][0-9][0-9][0-9][0-9][0-9]"" ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НайденоПоИндексу
		|
		|ИЗ 
		|	ДанныеАдреса КАК ДанныеАдреса
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.АдресныйКлассификатор КАК АдресныйКлассификатор
		|ПО	
		|	АдресныйКлассификатор.Наименование=ДанныеАдреса.Наименование
		|	И АдресныйКлассификатор.Сокращение=ДанныеАдреса.Сокращение
		|	И АдресныйКлассификатор.ТипАдресногоЭлемента<=5
		|	И АдресныйКлассификатор.ТипАдресногоЭлемента>=ДанныеАдреса.Позиция0
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	РегистрСведений.АдресныеСокращения КАК АдресныеСокращения
		|ПО	
		|	АдресныеСокращения.Сокращение=ДанныеАдреса.Сокращение
		|	И АдресныеСокращения.Уровень<=5
		|	
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.СтраныМира КАК СтраныМира
		|ПО 	
		|	СтраныМира.Наименование=ДанныеАдреса.Значение
		|
		|ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.СтраныМира КАК СтранаРоссия
		|ПО
		|	СтранаРоссия.Ссылка = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|
		|СГРУППИРОВАТЬ ПО 
		|	СтранаРоссия.Наименование,
		|	ДанныеАдреса.Позиция,
		|	ДанныеАдреса.Значение,
		|	ДанныеАдреса.Наименование,
		|	ДанныеАдреса.Сокращение,
		|	ДанныеАдреса.Начало,
		|	ДанныеАдреса.Длина
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеАдреса.Позиция УБЫВ
		|");
		
	Классификатор = Справочники.СтраныМира.ТаблицаКлассификатора();
	Запрос.УстановитьПараметр("СтраныКлассификатора", Классификатор.ВыгрузитьКолонку("Наименование") );
	Запрос.УстановитьПараметр("ДанныеАдреса", ДанныеАнализа);
	
	РезультатАнализа = Запрос.Выполнить().Выгрузить();
	
	АдресРФ = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "АдресРФ"));
	
	// Просмотр будет с конца, чтобы актуальным было первое
	ЭтоРоссийскийАдрес = Истина;
	СтрокаСтраны = Неопределено;
	
	ОбработанныеУровни = Новый Соответствие;
	ВведеноВСвободнойФорме = Ложь;
	
	ТаблицаЗданийПомещений = Новый ТаблицаЗначений;
	ТаблицаЗданийПомещений.Колонки.Добавить("Тип");
	ТаблицаЗданийПомещений.Колонки.Добавить("Значение");
	
	Для Каждого Строка Из РезультатАнализа Цикл
		// Проход по классификатору, сокращения, страны, индекс
		Если Строка.НайденоПоИндексу Тогда
			ПочтовыйИндексАдреса(АдресРФ, Строка.Значение);
			Строка.Обработано = Истина;
			
		ИначеЕсли Строка.ЭтоСтранаРоссия Тогда
			ЭтоРоссийскийАдрес = Истина;
			Строка.Обработано  = Истина;
			СтрокаСтраны       = Строка;
			
		ИначеЕсли Строка.НайденоПоСтранамМира Тогда
			ЭтоРоссийскийАдрес = Ложь;
			Строка.Обработано  = Истина;
			СтрокаСтраны       = Строка;
			
		ИначеЕсли Строка.НайденоПоКлассификатору Тогда
			Уровень = Строка.УровеньПоКлассификатору;
			УстановитьЧастьВАдресеПоУровнюКЛАДР(АдресРФ, Уровень, Строка.Значение);
			Строка.Обработано = Истина;
			// Контроль повторной обработки
			Если ОбработанныеУровни[Уровень]<>Неопределено Тогда
				ВведеноВСвободнойФорме = Истина;
			КонецЕсли;
			
		ИначеЕсли Строка.НайденоПоСокращениям Тогда
			Уровень = Строка.УровеньПоСокращениям;
			УстановитьЧастьВАдресеПоУровнюКЛАДР(АдресРФ, Уровень, Строка.Значение);
			Строка.Обработано = Истина;
			// Контроль повторной обработки
			Если ОбработанныеУровни[Уровень]<>Неопределено Тогда
				ВведеноВСвободнойФорме = Истина;
			КонецЕсли;
			
		Иначе
			// Проверка на квартиру или здание
			Тип = СокрЛП(СтрЗаменить(Строка.Наименование, "№", ""));
			Если КонтактнаяИнформацияКлиентСерверПовтИсп.КодСериализацииОбъектаАдресации(Тип)<>Неопределено Тогда
				// Вставляем в начало, так как порядок результата запроса у нас обратный
				НоваяСтрока = ТаблицаЗданийПомещений.Вставить(0);
				НоваяСтрока.Значение = СокрЛП(СтрЗаменить(Строка.Сокращение, "№", ""));
				НоваяСтрока.Тип      = Тип;
				Строка.Обработано = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Смотрим на страну разбора
	Если ЭтоРоссийскийАдрес Тогда
		Адрес.Страна = СокрЛП(Справочники.СтраныМира.Россия.Наименование);	
		Адрес.Состав = АдресРФ;
		
		ЗданияИПомещения = Новый Структура("Здания, Помещения", ТаблицаЗданийПомещений, ТаблицаЗданийПомещений);
		ЗданияИПомещенияАдреса(АдресРФ, ЗданияИПомещения);
		
		Если ВведеноВСвободнойФорме Или РезультатАнализа.Найти(Ложь, "Обработано")<>Неопределено Тогда
			// Что-то осталось, считаем адресом в свободной форме
			АдресРФ.Адрес_По_Документу = Представление;
		КонецЕсли;
		
		Возврат Результат;
	КонецЕсли;
	
	// Адрес за пределами РФ
	Если СтрокаСтраны=Неопределено Тогда
		Адрес.Состав = Представление;
	Иначе
		Адрес.Страна = СтрокаСтраны.Значение;
		
		// Состав без страны, она остается в представлении
		Позиция = СтрокаСтраны.Начало + СтрокаСтраны.Длина;
		Длина   = СтрДлина(Представление);
		Разделители = "," + Символы.ПС;
		Пока Позиция<=Длина И Найти(Разделители, Сред(Представление, Позиция, 1))<=0 Цикл
			Позиция = Позиция + 1;
		КонецЦикла;
		Пока Позиция<=Длина И Найти(Разделители, Сред(Представление, Позиция, 1))>0 Цикл
			Позиция = Позиция + 1;
		КонецЦикла;
		Адрес.Состав = СокрЛП( Лев(Представление, СтрокаСтраны.Начало -1 ) + " " + Сред(Представление, Позиция) );
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ЧастиАдресаТаблицей(Знач Текст)
	
	ТипСтрока = Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(128));
	ТипЧисло  = Новый ОписаниеТипов("Число");
	
	Результат = Новый ТаблицаЗначений;
	Колонки = Результат.Колонки;
	Колонки.Добавить("Позиция0",     ТипЧисло);
	Колонки.Добавить("Позиция",      ТипЧисло);
	Колонки.Добавить("Значение",     ТипСтрока);
	Колонки.Добавить("Наименование", ТипСтрока);
	Колонки.Добавить("Сокращение",   ТипСтрока);
	
	Колонки.Добавить("Начало", ТипЧисло);
	Колонки.Добавить("Длина",  ТипЧисло);
	
	Номер = 0;
	Для Каждого Часть Из СловаТекстаТаблицей(Текст, "," + Символы.ПС) Цикл
		Значение = СокрЛП(Часть.Значение);
		Если ПустаяСтрока(Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Строка = Результат.Добавить();
		Строка.Значение = Значение;
		
		Строка.Позиция0 = Номер;
		Номер = Номер + 1;
		Строка.Позиция  = Номер;
		
		Строка.Начало = Часть.Начало;
		Строка.Длина  = Часть.Длина;
		
		Позиция = СтрДлина(Значение);
		Пока Позиция>0 Цикл
			Символ = Сред(Значение, Позиция, 1);
			Если ПустаяСтрока(Символ) Тогда
				Строка.Наименование = Лев(Значение, Позиция-1);
				Прервать;
			КонецЕсли;
			Строка.Сокращение = Символ + Строка.Сокращение;
			Позиция = Позиция - 1;
		КонецЦикла;
		
		Если ПустаяСтрока(Строка.Наименование) Тогда
			Строка.Наименование = Строка.Сокращение;
			Строка.Сокращение   = "";
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция СловаТекстаТаблицей(Знач Текст, Знач Разделители=Неопределено)
	
	НачалоСлова = 0;
	Состояние   = 0;
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ТипЧисло  = Новый ОписаниеТипов("Число");
	
	Результат = Новый ТаблицаЗначений;
	Колонки = Результат.Колонки;
	Колонки.Добавить("Значение", ТипСтрока);
	Колонки.Добавить("Начало",   ТипЧисло);
	Колонки.Добавить("Длина",    ТипЧисло);
	
	Для Позиция=1 По СтрДлина(Текст) Цикл
		ТекущийСимвол = Сред(Текст, Позиция, 1);
		ЭтоРазделитель = ?(Разделители=Неопределено, ПустаяСтрока(ТекущийСимвол), Найти(Разделители, ТекущийСимвол)>0);
		
		Если Состояние=0 И (Не ЭтоРазделитель) Тогда
			НачалоСлова = Позиция;
			Состояние   = 1;
		ИначеЕсли Состояние=1 И ЭтоРазделитель Тогда
			Строка = Результат.Добавить();
			Строка.Начало = НачалоСлова;
			Строка.Длина  = Позиция-НачалоСлова;
			Строка.Значение = Сред(Текст, Строка.Начало, Строка.Длина);
			Состояние = 0;
		КонецЕсли;
	КонецЦикла;
	
	Если Состояние=1 Тогда
		Строка = Результат.Добавить();
		Строка.Начало = НачалоСлова;
		Строка.Длина  = Позиция-НачалоСлова;
		Строка.Значение = Сред(Текст, Строка.Начало, Строка.Длина)
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура УстановитьЧастьВАдресеПоУровнюКЛАДР(XDTOАдресРФ, Знач Уровень, Знач Значение)
	
	// XPath
	Если Уровень=1 Тогда
		Путь = "СубъектРФ";
	ИначеЕсли Уровень=2 Тогда
		Путь = "СвРайМО/Район";
	ИначеЕсли Уровень=3 Тогда
		Путь = "Город";
	ИначеЕсли Уровень=4 Тогда
		Путь = "НаселПункт";
	ИначеЕсли Уровень=5 Тогда
		Путь = "Улица";
	Иначе
		Возврат;
	КонецЕсли;
	
	УстановитьСвойствоПоXPath(XDTOАдресРФ, Путь, Значение);
КонецПроцедуры

Функция ДесериализацияТелефонаФакса(ЗначенияПолей, Представление="", ОжидаемыйТип=Неопределено)
	
	Если КонтактнаяИнформацияКлиентСервер.ЭтоКонтактнаяИнформацияВXML(ЗначенияПолей) Тогда
		// Общий формат контактной информации
		Возврат ДесериализацияКонтактнойИнформации(ЗначенияПолей, ОжидаемыйТип);
	КонецЕсли;
	
	ПространствоИмен = КонтактнаяИнформацияКлиентСерверПовтИсп.ПространствоИмен();
	
	Если ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		Данные = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "НомерТелефона"));
	ИначеЕсли ОжидаемыйТип=Перечисления.ТипыКонтактнойИнформации.Факс Тогда
		Данные = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "НомерФакса"));
	ИначеЕсли ОжидаемыйТип=Неопределено Тогда
		// Считаем телефоном
		Данные = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "НомерТелефона"));
	Иначе
		ВызватьИсключение НСтр("ru='Ошибка десериализации контактной информации, ожидается телефон или факс'");
	КонецЕсли;
	
	Результат = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(ПространствоИмен, "КонтактнаяИнформация"));
	Результат.Представление = Представление;
	Результат.Состав        = Данные;
	
	// Из пар ключ-значение
	СписокЗначенийПолей = Неопределено;
	Если ТипЗнч(ЗначенияПолей)=Тип("СписокЗначений") Тогда
		СписокЗначенийПолей = ЗначенияПолей;
	ИначеЕсли Не ПустаяСтрока(ЗначенияПолей) Тогда
		СписокЗначенийПолей = УправлениеКонтактнойИнформациейКлиентСервер.ПреобразоватьСтрокуВСписокПолей(ЗначенияПолей);
	КонецЕсли;
	
	Если СписокЗначенийПолей<>Неопределено Тогда
		Для Каждого ЗначениеПоля Из СписокЗначенийПолей Цикл
			Поле = ВРег(ЗначениеПоля.Представление);
			Если Поле="КОДСТРАНЫ" Тогда
				Данные.КодСтраны = ЗначениеПоля.Значение;
			ИначеЕсли Поле="КОДГОРОДА" Тогда
				Данные.КодГорода = ЗначениеПоля.Значение;
			ИначеЕсли Поле="НОМЕРТЕЛЕФОНА" Тогда
				Данные.Номер = ЗначениеПоля.Значение;
			ИначеЕсли Поле="ДОБАВОЧНЫЙ" Тогда
				Данные.Добавочный = ЗначениеПоля.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Возврат Результат;
	КонецЕсли;
	
	// Разбираем из представления.
	
	// Группы цифр, разделенные символами - не цифрами: страна, город, номер, добавочный. 
	// Добавочный включает в себя непробельные символы слева и справа
	Позиция = 1;
	Данные.КодСтраны  = НайтиПодстрокуЦифр(Представление, Позиция);
	НачалоГорода = Позиция;
	
	Данные.КодГорода  = НайтиПодстрокуЦифр(Представление, Позиция);
	Данные.Номер      = НайтиПодстрокуЦифр(Представление, Позиция, " -");
	
	Добавочный = СокрЛП(Сред(Представление, Позиция));
	Если Лев(Добавочный, 1) = "," Тогда
		Добавочный = СокрЛ(Сред(Добавочный, 2));
	КонецЕсли;
	Если ВРег(Лев(Добавочный, 3 ))= "ДОБ" Тогда
		Добавочный = СокрЛ(Сред(Добавочный, 4));
	КонецЕсли;
	Если ВРег(Лев(Добавочный, 1 ))= "." Тогда
		Добавочный = СокрЛ(Сред(Добавочный, 2));
	КонецЕсли;
	Данные.Добавочный = СокрЛП(Добавочный);
	
	// Корректируем возможные ошибки
	Если ПустаяСтрока(Данные.Номер) Тогда
		Если Лев(СокрЛ(Представление),1)="+" Тогда
			// Была попытка явно указать код страны, оставляем страну в покое
			Данные.КодГорода  = "";
			Данные.Номер      = СократитьНеЦифры(Сред(Представление, НачалоГорода));
			Данные.Добавочный = "";
		Иначе
			Данные.КодСтраны  = "";
			Данные.КодГорода  = "";
			Данные.Номер      = Представление;
			Данные.Добавочный = "";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции  

// Возвращает первую подстроку из цифр в строке. Параметр ПозицияНачала изменяется на первую не цифру
Функция НайтиПодстрокуЦифр(Текст, ПозицияНачала=Неопределено, ДопустимоКромеЦифр="")
	
	Если ПозицияНачала=Неопределено Тогда
		ПозицияНачала = 1;
	КонецЕсли;
	
	Результат = "";
	ПозицияКонца = СтрДлина(Текст);
	ПоискНачала  = Истина;
	
	Пока ПозицияНачала<=ПозицияКонца Цикл
		Символ = Сред(Текст, ПозицияНачала, 1);
		ЭтоЦифра = Символ>="0" И Символ<="9";
		
		Если ПоискНачала Тогда
			Если ЭтоЦифра Тогда
				Результат = Результат + Символ;
				ПоискНачала = Ложь;
			КонецЕсли;
		Иначе
			Если ЭтоЦифра Или Найти(ДопустимоКромеЦифр, Символ)>0 Тогда
				Результат = Результат + Символ;    
			Иначе
				Прервать;
			КонецЕсли;
		КонецЕсли;
		
		ПозицияНачала = ПозицияНачала + 1;
	КонецЦикла;
	
	// Убираем возможные висящие разделители справа
	Возврат СократитьНеЦифры(Результат, ДопустимоКромеЦифр, Ложь);
	
КонецФункции

Функция СократитьНеЦифры(Текст, ДопустимоКромеЦифр="", Направление=Истина)
	
	Длина = СтрДлина(Текст);
	Если Направление Тогда
		// Сокращение слева
		Индекс = 1;
		Конец  = 1 + Длина;
		Шаг    = 1;
	Иначе
		// Сокращение справа    
		Индекс = Длина;
		Конец  = 0;
		Шаг    = -1;
	КонецЕсли;
	
	Пока Индекс<>Конец Цикл
		Символ = Сред(Текст, Индекс, 1);
		ЭтоЦифра = (Символ>="0" И Символ<="9") Или Найти(ДопустимоКромеЦифр, Символ)=0;
		Если ЭтоЦифра Тогда
			Прервать;
		КонецЕсли;
		Индекс = Индекс + Шаг;
	КонецЦикла;
	
	Если Направление Тогда
		// Сокращение слева
		Возврат Прав(Текст, Длина - Индекс + 1);
	КонецЕсли;
	
	// Сокращение справа
	Возврат Лев(Текст, Индекс);
	
КонецФункции

// Получение глубокого свойства объекта
Функция ЗначениеСвойстваПоXPath(ОбъектXTDO, XPath) Экспорт
	
	// Переносов строки в XPath не ожидаем
	СтрокаСвойств = СтрЗаменить(СтрЗаменить(XPath, "/", Символы.ПС), Символы.ПС + Символы.ПС, "/");
	
	ЧислоСвойств = СтрЧислоСтрок(СтрокаСвойств);
	Если ЧислоСвойств=1 Тогда
		Возврат ОбъектXTDO.Получить(СтрокаСвойств);
	КонецЕсли;
	
	Результат = ?(ЧислоСвойств=0, Неопределено, ОбъектXTDO);
	Для Индекс=1 По ЧислоСвойств Цикл
		Результат = Результат.Получить(СтрПолучитьСтроку(СтрокаСвойств, Индекс));     
		Если Результат=Неопределено Тогда 
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Установка глубокого свойства объекта
Процедура УстановитьСвойствоПоXPath(ОбъектXTDO, XPath, Значение) Экспорт
	
	// Переносов строки в XPath не ожидаем
	СтрокаСвойств = СтрЗаменить(СтрЗаменить(XPath, "/", Символы.ПС), Символы.ПС + Символы.ПС, "/");
	
	ЧислоСвойств = СтрЧислоСтрок(СтрокаСвойств);
	Если ЧислоСвойств=1 Тогда
		ОбъектXTDO.Установить(СтрокаСвойств, Значение);
		Возврат;
	ИначеЕсли ЧислоСвойств<1 Тогда
		Возврат;
	КонецЕсли;
		
	РодительскийОбъект = Неопределено;
	ТекущийОбъект      = ОбъектXTDO;
	Для Индекс=1 По ЧислоСвойств Цикл
		
		ТекущееИмя = СтрПолучитьСтроку(СтрокаСвойств, Индекс);
		Если ТекущийОбъект.Установлено(ТекущееИмя) Тогда
			РодительскийОбъект = ТекущийОбъект;
			ТекущийОбъект = ТекущийОбъект.ПолучитьXDTO(ТекущееИмя);
		Иначе
			НовыйТип = ТекущийОбъект.Свойства().Получить(ТекущееИмя).Тип;
			ТипТипа = ТипЗнч(НовыйТип);
			Если ТипТипа=Тип("ТипОбъектаXDTO") Тогда
				НовыйОбъект = ФабрикаXDTO.Создать(НовыйТип);
				ТекущийОбъект.Установить(ТекущееИмя, НовыйОбъект);
				РодительскийОбъект = ТекущийОбъект;
				ТекущийОбъект = НовыйОбъект; 
			ИначеЕсли ТипТипа=Тип("ТипЗначенияXDTO") Тогда
				// Непосредственное значение
				ТекущийОбъект.Установить(ТекущееИмя, Значение);
				РодительскийОбъект = Неопределено;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если РодительскийОбъект<>Неопределено Тогда
		РодительскийОбъект.Установить(ТекущееИмя, Значение);
	КонецЕсли;
	
КонецПроцедуры
