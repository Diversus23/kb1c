#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// При записи контролируются курсы подчиненных валют
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ОтключитьКонтрольПодчиненныхВалют") Тогда
		
		ДополнительныеСвойства.Вставить("ЗависимыеВалюты", Новый Соответствие);
		
		Если Количество() > 0 Тогда
			ЗагрузитьКурсыПодчиненныхВалют();
		Иначе
			УдалитьКурсыПодчиненныхВалют();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Находит все зависимые валюты и изменяет их курс
//
Процедура ЗагрузитьКурсыПодчиненныхВалют()
	
	Для Каждого ЗаписьВалютыВладельца Из ЭтотОбъект Цикл
		
		ЗависимыеВалюты = РаботаСКурсамиВалют.ПолучитьСписокЗависимыхВалют(ЗаписьВалютыВладельца.Валюта, ДополнительныеСвойства);
		Для Каждого СтрокаТаблицы Из ЗависимыеВалюты Цикл
			
			НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Валюта.Установить(СтрокаТаблицы.Ссылка, Истина);
			НаборЗаписей.Отбор.Период.Установить(ЗаписьВалютыВладельца.Период, Истина);
			
			ЗаписьКурсовВалют = НаборЗаписей.Добавить();
			ЗаписьКурсовВалют.Валюта    = СтрокаТаблицы.Ссылка;
			ЗаписьКурсовВалют.Период    = ЗаписьВалютыВладельца.Период;
			ЗаписьКурсовВалют.Курс      = ЗаписьВалютыВладельца.Курс + ЗаписьВалютыВладельца.Курс * СтрокаТаблицы.Наценка / 100;
			ЗаписьКурсовВалют.Кратность = ЗаписьВалютыВладельца.Кратность;
			
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПодчиненныхВалют", Истина);
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Очищает курсы зависимых валют
//
Процедура УдалитьКурсыПодчиненныхВалют()
	
	ВалютаВладелец = Отбор.Валюта.Значение;
	Период = Отбор.Период;
	
	ЗависимыеВалюты = РаботаСКурсамиВалют.ПолучитьСписокЗависимыхВалют(ВалютаВладелец, ДополнительныеСвойства);
	Для Каждого СтрокаТаблицы Из ЗависимыеВалюты Цикл
		НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Валюта.Установить(СтрокаТаблицы.Ссылка, Истина);
		НаборЗаписей.Отбор.Период.Установить(Период, Истина);
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПодчиненныхВалют", Истина);
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли