
Функция ПолучитьЗначениеНастройки(знач ИмяНастройки, знач ПользовательСсылка) Экспорт
	
	Если НЕ ТипЗнч(ПользовательСсылка) = Тип("СправочникСсылка.Пользователи") Тогда
		ПользовательСсылка = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяНастройки"	, ИмяНастройки);
	Запрос.УстановитьПараметр("Пользователь"	, ПользовательСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиБазыЗнаний.ИмяНастройки,
	|	НастройкиБазыЗнаний.Пользователь,
	|	НастройкиБазыЗнаний.ЗначениеСсылка,
	|	НастройкиБазыЗнаний.ЗначениеЧисло,
	|	НастройкиБазыЗнаний.ЗначениеСтрока,
	|	НастройкиБазыЗнаний.ЗначениеБулево,
	|	НастройкиБазыЗнаний.ИмяРесурса
	|ИЗ
	|	РегистрСведений.НастройкиБазыЗнаний КАК НастройкиБазыЗнаний
	|ГДЕ
	|	НастройкиБазыЗнаний.ИмяНастройки = &ИмяНастройки
	|	И НастройкиБазыЗнаний.Пользователь = &Пользователь";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка[Выборка.ИмяРесурса];
	
КонецФункции

