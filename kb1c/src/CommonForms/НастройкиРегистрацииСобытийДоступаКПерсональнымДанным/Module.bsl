////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если НЕ Пользователи.ЭтоПолноправныйПользователь(, Истина) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ЗащитаПерсональныхДанных.ПриСозданииФормыНастройкиРегистрацииСобытий(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если СохранитьДанныеПроизвольнойФормы(Отказ) Тогда
		ЗаписатьНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	Если СохранитьДанныеПроизвольнойФормы() Тогда
		ЗаписатьНаСервере();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	ЗаписатьНаСервере();
	Закрыть();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаписатьНаСервере()
	ЗащитаПерсональныхДанных.ПриЗаписиФормыНастройкиРегистрацииСобытий(ЭтаФорма);
	Модифицированность = Ложь;
КонецПроцедуры

&НаКлиенте
Функция СохранитьДанныеПроизвольнойФормы(Отказ = Неопределено)
	// Вызывает диалог сохранения редактируемых данных произвольной формы
	//   Вызывается из обработчиков "ПередЗакрытием" и "ПриЗакрытии".
	// 
	// Параметры:
	//   Отказ - (...) Флаг отказа от закрытия формы.
	//       |- (Булево)    для обработчика "ПередЗакрытием", используется для толстого и тонкого клиентов
	//       |- (Неопределено) для обработчика "ПриЗакрытии", используется для веб клиента
	// 
	// Возвращаемое значение: 
	//   (Булево) Необходимость сохранения данных формы.
	
	// Изменение сохраняемых данных должно устанавливать модифицированность формы
	Если Модифицированность <> Истина Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Определение клиента
	#Если ВебКлиент Тогда
		ЭтоВебКлиент = Истина;
	#Иначе
		ЭтоВебКлиент = Ложь;
	#КонецЕсли
	
	// Предупреждение необходимо вызвать только один раз для одного клиента
	Если ЭтоВебКлиент <> (Отказ = Неопределено) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
	Кнопки = ?(ЭтоВебКлиент, РежимДиалогаВопрос.ДаНет, РежимДиалогаВопрос.ДаНетОтмена);
	Ответ = Вопрос(ТекстВопроса, Кнопки, 60, КодВозвратаДиалога.Нет); 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ЭтоВебКлиент И Ответ = КодВозвратаДиалога.Отмена Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции
