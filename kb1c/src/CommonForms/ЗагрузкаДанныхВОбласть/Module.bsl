
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПродолжитьЗагрузкуДанных(Команда)
	
	Если РазделительСуществует(ЗначениеРазделителя) Тогда
		
		Текст = НСтр("ru = 'Указанная область данных существует. Загрузка может привести к повреждению текущих данных.
			|Продолжить загрузку?'");
		Если Вопрос(Текст, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет) = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	АдресВХранилище = Неопределено;
	Если Не ПоместитьФайл(АдресВХранилище) Тогда
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Выполняется загрузка данных в область'"));
	ЗагрузитьДанныеНаСервере(АдресВХранилище, ЗначениеРазделителя);
	
	Отказ = Ложь;
	СтандартныеПодсистемыКлиент.ДействияПередНачаломРаботыСистемы(Отказ);
	
	Если Отказ Тогда
		Предупреждение(НСтр("ru = 'Загрузка данных завершена с ошибками'"));
		Возврат;
	КонецЕсли;
	
	СтандартныеПодсистемыКлиент.ДействияПриНачалеРаботыСистемы();
	Состояние(НСтр("ru = 'Загрузка данных завершена успешно'"));

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗагрузитьДанныеНаСервере(Знач АдресВХранилище, ЗначениеРазделителя)
	
	ДанныеАрхива = ПолучитьИзВременногоХранилища(АдресВХранилище);
	ИмяАрхива = ПолучитьИмяВременногоФайла("zip");
	ДанныеАрхива.Записать(ИмяАрхива);
	ДанныеАрхива = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.УстановитьРазделениеСеанса(Истина, ЗначениеРазделителя);
	
	Если НЕ РазделительСуществует(ЗначениеРазделителя) Тогда
		
		МенеджерЗаписи = РегистрыСведений.ОбластиДанных.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ОбластьДанныхВспомогательныеДанные = ЗначениеРазделителя;
		МенеджерЗаписи.Статус        = Перечисления.СтатусыОбластейДанных.Используется;
		МенеджерЗаписи.Записать();
		
		МенеджерЗаписи = Константы.ПредставлениеОбластиДанных.СоздатьМенеджерЗначения();
		МенеджерЗаписи.ОбластьДанныхВспомогательныеДанные = ЗначениеРазделителя;
		МенеджерЗаписи.Значение = ЗначениеРазделителя;
		МенеджерЗаписи.Записать();
		
	КонецЕсли;
	
	ВыгрузкаЗагрузкаДанных.ЗагрузитьТекущуюОбластьИзАрхива(ИмяАрхива);
	
	Попытка
		УдалитьФайлы(ИмяАрхива);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Удаление временных файлов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

	ПользователиСлужебный.АвторизоватьТекущегоПользователя();
	
	ОбновлениеИнформационнойБазы.ВыполнитьОбновлениеИнформационнойБазы();
	
КонецПроцедуры

&НаСервере
Функция РазделительСуществует(ЗначениеРазделителя)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбластиДанных.ОбластьДанныхВспомогательныеДанные КАК ОбластьДанных
	|ИЗ
	|	РегистрСведений.ОбластиДанных КАК ОбластиДанных
	|ГДЕ
	|	ОбластиДанных.ОбластьДанныхВспомогательныеДанные = &ОбластьДанных";
	Запрос.УстановитьПараметр("ОбластьДанных", ЗначениеРазделителя);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции
