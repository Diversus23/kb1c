////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Пользователь <> Неопределено Тогда
		МассивПользователей = Новый Массив;
		МассивПользователей.Добавить(Параметры.Пользователь);
		
		ВнешниеПользователи = ?(
			ТипЗнч(Параметры.Пользователь) = Тип("СправочникСсылка.ВнешниеПользователи"), Истина, Ложь);
		
		Элементы.ФормаЗаписатьИЗакрыть.Заголовок = НСтр("ru = 'Записать'");
		
		РежимОткрытияИзКарточкиПользователя = Истина;
	Иначе
		МассивПользователей = Параметры.Пользователи;
		ВнешниеПользователи = Параметры.ВнешниеПользователи;
		РежимОткрытияИзКарточкиПользователя = Ложь;
	КонецЕсли;
	
	КоличествоПользователей = МассивПользователей.Количество();
	Если КоличествоПользователей > 1
		И Параметры.Пользователь = Неопределено Тогда
		Заголовок = НСтр("ru = 'Группы пользователей'");
		Элементы.ДеревоГруппПометка.ТриСостояния = Истина;
	КонецЕсли;
	
	СписокПользователей = Новый Структура;
	СписокПользователей.Вставить("МассивПользователей", МассивПользователей);
	СписокПользователей.Вставить("КоличествоПользователей", КоличествоПользователей);
	ЗаполнитьДеревоГрупп();
	
	Если ДеревоГрупп.ПолучитьЭлементы().Количество() = 0 Тогда
		Элементы.ГруппыИлиПредупреждение.ТекущаяСтраница = Элементы.Предупреждение;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ДеревоГрупп

&НаКлиенте
Процедура ДеревоГруппВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьЗначение(Элемент.ТекущиеДанные.Группа);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	УведомлениеПользователя = Новый Структура;
	УведомлениеПользователя.Вставить("Сообщение");
	УведомлениеПользователя.Вставить("ЕстьОшибки");
	УведомлениеПользователя.Вставить("ПолныйТекстСообщения");
	
	ЗаписатьИзменения(УведомлениеПользователя);
	
	Если УведомлениеПользователя.ЕстьОшибки = Ложь Тогда
		Если УведомлениеПользователя.Сообщение <> Неопределено Тогда
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Перемещение пользователей'"), , УведомлениеПользователя.Сообщение, БиблиотекаКартинок.Информация32);
		КонецЕсли;
	Иначе
		
		Если УведомлениеПользователя.ПолныйТекстСообщения <> Неопределено Тогда
			Отчет = Новый ТекстовыйДокумент;
			Отчет.ДобавитьСтроку(УведомлениеПользователя.ПолныйТекстСообщения);
			
			ТекстВопроса = УведомлениеПользователя.Сообщение;
			КнопкиВопроса = Новый СписокЗначений;
			КнопкиВопроса.Добавить(НСтр("ru='ОК'"));
			КнопкиВопроса.Добавить(НСтр("ru='Показать отчет'"));
			Ответ = Вопрос(ТекстВопроса, КнопкиВопроса,, КнопкиВопроса[0].Значение);
			
			Если Ответ = КнопкиВопроса[0].Значение Тогда
				Возврат;
			Иначе
				Отчет.Показать(НСтр("ru = 'Пользователи, не включенные в группы'"));
				Возврат;
			КонецЕсли;
		Иначе
			Предупреждение(УведомлениеПользователя.Сообщение);
		КонецЕсли;
		
	КонецЕсли;
	
	Оповестить("РазмещениеПользователейВГруппах");
	Если ВнешниеПользователи Тогда
		Оповестить("Запись_ГруппыВнешнихПользователей");
	Иначе
		Оповестить("Запись_ГруппыПользователей");
	КонецЕсли;
	
	Если Не РежимОткрытияИзКарточкиПользователя Тогда
		Закрыть();
	Иначе
		ЗаполнитьДеревоГрупп();
		РазвернутьДеревоЗначений();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	ЗаполнитьДеревоГрупп(Истина);
	РазвернутьДеревоЗначений();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьДеревоГрупп(ТолькоСнятьФлажки = Ложь)
	
	ДеревоГруппПриемник = РеквизитФормыВЗначение("ДеревоГрупп");
	Если Не ТолькоСнятьФлажки Тогда
		ДеревоГруппПриемник.Строки.Очистить();
	КонецЕсли;
	
	Если ТолькоСнятьФлажки Тогда
		
		Найденные = ДеревоГруппПриемник.Строки.НайтиСтроки(Новый Структура("Пометка", 1), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если Не СтрокаДерева.ГруппаНеИзменяется Тогда
				СтрокаДерева.Пометка = 0;
			КонецЕсли;
		КонецЦикла;
		
		Найденные = ДеревоГруппПриемник.Строки.НайтиСтроки(Новый Структура("Пометка", 2), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			СтрокаДерева.Пометка = 0;
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(ДеревоГруппПриемник, "ДеревоГрупп");
		Возврат;
	КонецЕсли;
	
	ГруппыПользователей = Неопределено;
	ПодчиненныеГруппы = Новый Массив;
	МассивРодителей = Новый Массив;
	
	Если ВнешниеПользователи Тогда
		ПустаяГруппа = Справочники.ГруппыВнешнихПользователей.ПустаяСсылка();
		ПолучитьГруппыВнешнихПользователей(ГруппыПользователей);
	Иначе
		ПустаяГруппа = Справочники.ГруппыПользователей.ПустаяСсылка();
		ПолучитьГруппыПользователей(ГруппыПользователей);
	КонецЕсли;
	
	Если ГруппыПользователей.Количество() <= 1 Тогда
		Элементы.ГруппыИлиПредупреждение.ТекущаяСтраница = Элементы.Предупреждение;
		Возврат;
	КонецЕсли;
	
	ПолучитьПодчиненныеГруппы(ГруппыПользователей, ПодчиненныеГруппы, ПустаяГруппа);
	
	Если ТипЗнч(СписокПользователей.МассивПользователей[0]) = Тип("СправочникСсылка.Пользователи") Тогда
		ТипПользователя = "Пользователь";
	Иначе
		ТипПользователя = "ВнешнийПользователь";
	КонецЕсли;
	
	Пока ПодчиненныеГруппы.Количество() > 0 Цикл
		МассивРодителей.Очистить();
		
		Для Каждого Группа Из ПодчиненныеГруппы Цикл
			
			Если Группа.Родитель = ПустаяГруппа Тогда
				НоваяСтрокаГрупп = ДеревоГруппПриемник.Строки.Добавить();
				НоваяСтрокаГрупп.Группа = Группа.Ссылка;
				НоваяСтрокаГрупп.Картинка = ?(ТипПользователя = "Пользователь", 3, 9);
				
				Если СписокПользователей.КоличествоПользователей = 1 Тогда
					ПользовательКосвенноВключенВГруппу = Ложь;
					ПользовательСсылка = СписокПользователей.МассивПользователей[0];
					
					Если ТипПользователя = "ВнешнийПользователь" Тогда
						ПользовательКосвенноВключенВГруппу = (Группа.ВсеОбъектыАвторизации И 
							(ТипЗнч(ПользовательСсылка.ОбъектАвторизации) = ТипЗнч(Группа.ТипОбъектовАвторизации)));
						НоваяСтрокаГрупп.ГруппаНеИзменяется = ПользовательКосвенноВключенВГруппу;
					КонецЕсли;
					
					НайденныйПользователь = Группа.Ссылка.Состав.Найти(ПользовательСсылка, ТипПользователя);
					НоваяСтрокаГрупп.Пометка = ?(НайденныйПользователь <> Неопределено Или ПользовательКосвенноВключенВГруппу, 1, 0);
				Иначе
					НоваяСтрокаГрупп.Пометка = 2;
				КонецЕсли;
				
			Иначе
				ГруппаРодитель = 
					ДеревоГруппПриемник.Строки.НайтиСтроки(Новый Структура("Группа", Группа.Родитель), Истина);
				НоваяСтрокаПодчиненныхГрупп = ГруппаРодитель[0].Строки.Добавить();
				НоваяСтрокаПодчиненныхГрупп.Группа = Группа.Ссылка;
				НоваяСтрокаПодчиненныхГрупп.Картинка = ?(ТипПользователя = "Пользователь", 3, 9);
				
				Если СписокПользователей.КоличествоПользователей = 1 Тогда
					НоваяСтрокаПодчиненныхГрупп.Пометка = ?(Группа.Ссылка.Состав.Найти(
						СписокПользователей.МассивПользователей[0], ТипПользователя) = Неопределено, 0, 1);
				Иначе
					НоваяСтрокаПодчиненныхГрупп.Пометка = 2;
				КонецЕсли;
				
			КонецЕсли;
			
			МассивРодителей.Добавить(Группа.Ссылка);
		КонецЦикла;
		ПодчиненныеГруппы.Очистить();
		
		Для Каждого Элемент Из МассивРодителей Цикл
			ПолучитьПодчиненныеГруппы(ГруппыПользователей, ПодчиненныеГруппы, Элемент);
		КонецЦикла;
		
	КонецЦикла;
	
	ДеревоГруппПриемник.Строки.Сортировать("Группа Возр", Истина);
	ЗначениеВРеквизитФормы(ДеревоГруппПриемник, "ДеревоГрупп");
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьГруппыПользователей(ГруппыПользователей)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ГруппыПользователей.Ссылка,
	|	ГруппыПользователей.Родитель
	|ИЗ
	|	Справочник.ГруппыПользователей КАК ГруппыПользователей
	|ГДЕ
	|	ГруппыПользователей.ПометкаУдаления <> ИСТИНА";
	
	ГруппыПользователей = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьГруппыВнешнихПользователей(ГруппыПользователей)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ГруппыВнешнихПользователей.Ссылка,
	|	ГруппыВнешнихПользователей.Родитель,
	|	ГруппыВнешнихПользователей.ТипОбъектовАвторизации,
	|	ГруппыВнешнихПользователей.ВсеОбъектыАвторизации
	|ИЗ
	|	Справочник.ГруппыВнешнихПользователей КАК ГруппыВнешнихПользователей
	|ГДЕ
	|	ГруппыВнешнихПользователей.ПометкаУдаления <> ИСТИНА";
	
	ГруппыПользователей = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьПодчиненныеГруппы(ГруппыПользователей, ПодчиненныеГруппы, ГруппаРодитель)
	
	ПараметрыОтбора = Новый Структура("Родитель", ГруппаРодитель);
	ОтобранныеСтроки = ГруппыПользователей.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого Элемент Из ОтобранныеСтроки Цикл
		
		Если Элемент.Ссылка = Справочники.ГруппыПользователей.ВсеПользователи
			Или Элемент.Ссылка = Справочники.ГруппыВнешнихПользователей.ВсеВнешниеПользователи Тогда
			Продолжить;
		КонецЕсли;
		
		ПодчиненныеГруппы.Добавить(Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменения(УведомлениеПользователя)
	
	МассивПользователей = Неопределено;
	НеПеремещенныеПользователи = Новый Соответствие;
	ДеревоГруппИсточник = ДеревоГрупп.ПолучитьЭлементы();
	ПерезаполнитьСоставГрупп(ДеревоГруппИсточник, МассивПользователей, НеПеремещенныеПользователи);
	СформироватьТекстСообщения(МассивПользователей, УведомлениеПользователя, НеПеремещенныеПользователи)
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьСоставГрупп(ДеревоГруппИсточник, МассивПеремещенныхПользователей, НеПеремещенныеПользователи)
	
	МассивПользователей = СписокПользователей.МассивПользователей;
	Если МассивПеремещенныхПользователей = Неопределено Тогда
		МассивПеремещенныхПользователей = Новый Массив;
	КонецЕсли;
	
	Для Каждого СтрокаДерева Из ДеревоГруппИсточник Цикл
		
		Если СтрокаДерева.Пометка = 1
			И Не СтрокаДерева.ГруппаНеИзменяется Тогда
			
			Для Каждого ПользовательСсылка Из МассивПользователей Цикл
				
				Если ТипЗнч(ПользовательСсылка) = Тип("СправочникСсылка.Пользователи") Тогда
					ТипПользователя = "Пользователь";
				Иначе
					ТипПользователя = "ВнешнийПользователь";
					ПеремещениеВозможно = ПользователиСлужебный.ВозможноПеремещениеПользователя(СтрокаДерева.Группа, ПользовательСсылка);
					
					Если Не ПеремещениеВозможно Тогда
						
						Если НеПеремещенныеПользователи.Получить(ПользовательСсылка) = Неопределено Тогда
							НеПеремещенныеПользователи.Вставить(ПользовательСсылка, Новый Массив);
							НеПеремещенныеПользователи[ПользовательСсылка].Добавить(СтрокаДерева.Группа);
						Иначе
							НеПеремещенныеПользователи[ПользовательСсылка].Добавить(СтрокаДерева.Группа);
						КонецЕсли;
						
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
				Добавлять = ?(СтрокаДерева.Группа.Состав.Найти(
					ПользовательСсылка, ТипПользователя) = Неопределено, Истина, Ложь);
				Если Добавлять Тогда
					ПользователиСлужебный.ДобавитьПользователяВГруппу(СтрокаДерева.Группа, ПользовательСсылка, ТипПользователя);
					
					Если МассивПеремещенныхПользователей.Найти(ПользовательСсылка) = Неопределено Тогда
						МассивПеремещенныхПользователей.Добавить(ПользовательСсылка);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		ИначеЕсли СтрокаДерева.Пометка = 0
			И Не СтрокаДерева.ГруппаНеИзменяется Тогда
			
			Для Каждого ПользовательСсылка Из МассивПользователей Цикл
				
				Если ТипЗнч(ПользовательСсылка) = Тип("СправочникСсылка.Пользователи") Тогда
					ТипПользователя = "Пользователь";
				Иначе
					ТипПользователя = "ВнешнийПользователь";
				КонецЕсли;
				
				Удалять = ?(СтрокаДерева.Группа.Состав.Найти(
					ПользовательСсылка, ТипПользователя) <> Неопределено, Истина, Ложь);
				Если Удалять Тогда
					ПользователиСлужебный.УдалитьПользователяИзГруппы(СтрокаДерева.Группа, ПользовательСсылка, ТипПользователя);
					
					Если МассивПеремещенныхПользователей.Найти(ПользовательСсылка) = Неопределено Тогда
						МассивПеремещенныхПользователей.Добавить(ПользовательСсылка);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЭлементыСтрокиДерева = СтрокаДерева.ПолучитьЭлементы();
		// Рекурсия
		ПерезаполнитьСоставГрупп(ЭлементыСтрокиДерева, МассивПеремещенныхПользователей, НеПеремещенныеПользователи);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьТекстСообщения(МассивПеремещенныхПользователей, УведомлениеПользователя, НеПеремещенныеПользователи)
	
	КоличествоПользователей = МассивПеремещенныхПользователей.Количество();
	КоличествоНеПеремещенныхПользователей = НеПеремещенныеПользователи.Количество();
	СтрокаПользователей = "";
	
	Если КоличествоНеПеремещенныхПользователей > 0 Тогда
		
		Если КоличествоНеПеремещенныхПользователей = 1 Тогда
			Для Каждого НеПеремещенныйПользователь Из НеПеремещенныеПользователи Цикл
				Предмет = Строка(НеПеремещенныйПользователь.Ключ);
			КонецЦикла;
			СообщениеПользователю = НСтр("ru = 'Пользователя ""%1"" не удалось включить в выбранные группы,
									|т.к. у них различается тип или у групп установлен признак ""Все пользователи заданного типа"".'");
		Иначе
			ПараметрыПредметаИсчисления = НСтр("ru = 'пользователю,пользователям,пользователям,,,,,,0'");
			Предмет = ПользователиСлужебный.ФормированиеОкончанияСлова(КоличествоНеПеремещенныхПользователей, ПараметрыПредметаИсчисления);
			СообщениеПользователю = НСтр("ru = 'Не всех пользователей удалось включить в выбранные группы,
									|т.к. у них различается тип или у групп установлен признак ""Все пользователи заданного типа"".'");
			Для Каждого НеПеремещенныйПользователь Из НеПеремещенныеПользователи Цикл
			СтрокаПользователей = СтрокаПользователей + Строка(НеПеремещенныйПользователь.Ключ) + " : " + 
				СтроковыеФункцииКлиентСервер.ПолучитьСтрокуИзМассиваПодстрок(НеПеремещенныйПользователь.Значение) + Символы.ПС;
			КонецЦикла;
			УведомлениеПользователя.ПолныйТекстСообщения = НСтр("ru = 'Следующие пользователи не были включены в группы:'") +
				Символы.ПС + Символы.ПС + СтрокаПользователей;
		КонецЕсли;
		УведомлениеПользователя.Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СообщениеПользователю, Предмет);
		УведомлениеПользователя.ЕстьОшибки = Истина;
		
		Возврат;
	ИначеЕсли КоличествоПользователей = 1 Тогда
		НаименованиеПользователя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МассивПеремещенныхПользователей[0], "Наименование");
		СообщениеПользователю = НСтр("ru = 'Изменен состав групп у пользователя ""%1""'");
		УведомлениеПользователя.Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СообщениеПользователю, НаименованиеПользователя);
	ИначеЕсли КоличествоПользователей > 1 Тогда
		
		СообщениеПользователю = НСтр("ru = 'Изменен состав групп у %1'");
		СтрокаОбъект = ПользователиСлужебный.ФормированиеОкончанияСлова(
			КоличествоПользователей, НСтр("ru = 'пользователя,пользователей,пользователей,,,,,,0'"));
		УведомлениеПользователя.Сообщение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			СообщениеПользователю, СтрокаОбъект);
		
	КонецЕсли;
	УведомлениеПользователя.ЕстьОшибки = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоЗначений()
	
	Строки = ДеревоГрупп.ПолучитьЭлементы();
	Для Каждого Строка Из Строки Цикл
		Элементы.ДеревоГрупп.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

