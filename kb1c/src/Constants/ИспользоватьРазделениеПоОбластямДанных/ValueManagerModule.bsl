#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Процедура ПередЗаписью(Отказ)
	
	ДополнительныеСвойства.Вставить("ТекущееЗначение", Константы.ИспользоватьРазделениеПоОбластямДанных.Получить());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	// Следующие константы взаимоисключающие, используются в отдельных функциональных опциях.
	//
	// Константа.ЭтоАвтономноеРабочееМесто                -> ФО.РаботаВАвтономномРежиме
	// Константа.НеИспользоватьРазделениеПоОбластямДанных -> ФО.РаботаВЛокальномРежиме
	// Константа.ИспользоватьРазделениеПоОбластямДанных   -> ФО.РаботаВМоделиСервиса
	//
	// Имена констант сохранены для обратной совместимости.
	
	Если Значение Тогда
		
		Константы.НеИспользоватьРазделениеПоОбластямДанных.Установить(Ложь);
		Константы.ЭтоАвтономноеРабочееМесто.Установить(Ложь);
		
	ИначеЕсли Константы.ЭтоАвтономноеРабочееМесто.Получить() Тогда
		
		Константы.НеИспользоватьРазделениеПоОбластямДанных.Установить(Ложь);
		
	Иначе
		
		Константы.НеИспользоватьРазделениеПоОбластямДанных.Установить(Истина);
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТекущееЗначение <> Значение Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		Если Значение Тогда
			ОбработчикиСобытия = ОбщегоНазначения.ОбработчикиСлужебногоСобытия(
				"СтандартныеПодсистемы.БазоваяФункциональность\ПриВключенииРазделенияПоОбластямДанных");
			
			Для каждого Обработчик Из ОбработчикиСобытия Цикл
				Обработчик.Модуль.ПриВключенииРазделенияПоОбластямДанных();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли