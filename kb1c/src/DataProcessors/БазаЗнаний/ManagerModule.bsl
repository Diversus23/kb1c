Функция РаспаковатьПакетСКартинками()
	
	ВремФайл	= ПолучитьИмяВременногоФайла("zip");
	ВремКаталог	= КаталогВременныхФайлов();
	ВремКаталог = ВремКаталог + "kdb_pictures\";
	
	ДвоичныеДанные = Обработки.БазаЗнаний.ПолучитьМакет("pictures_v1");
	ДвоичныеДанные.Записать(ВремФайл);
	
	Чтение = Новый ЧтениеZipФайла(ВремФайл);
	Чтение.ИзвлечьВсе(ВремКаталог, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	Чтение.Закрыть();
	Чтение = Неопределено;
	
	УдалитьФайлы(ВремФайл);
	
	Возврат ВремКаталог;
	
КонецФункции

Функция ПоместитьКартинкиБазыЗнанийВХранилище(МассивФайлов) Экспорт
	
	// Распакуем архив с картинками на сервере
	АдресКаталога = РаспаковатьПакетСКартинками();
	
	// Создадим массив передаваемых файлов на клиент
	МассивОписаний = Новый Массив;
	Для Каждого СтруктураКартинки Из МассивФайлов Цикл
		ДвоичныеДанные	= Новый ДвоичныеДанные(АдресКаталога + СтруктураКартинки.Имя);
		АдресХранилища	= ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		Если АдресХранилища = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеФайла	= Новый ОписаниеПередаваемогоФайла(СтруктураКартинки.Путь, АдресХранилища);
		МассивОписаний.Добавить(ОписаниеФайла);
	КонецЦикла;
	
	// Удалим файлы
	УдалитьФайлы(АдресКаталога);
	
	Возврат МассивОписаний;
	
КонецФункции

Функция ПоместитьСкриптыБазыЗнанийВХранилище(МассивСкриптов) Экспорт
	
	ИмяВремФайла		= ПолучитьИмяВременногоФайла("js");
	ОбработкаМенеджер	= Обработки.БазаЗнаний;
	
	МассивОписаний = Новый Массив;
	Для Каждого СтруктураСкрипта Из МассивСкриптов Цикл
		ТекстовыйДокумент = ОбработкаМенеджер.ПолучитьМакет(СтруктураСкрипта.Имя);
		ТекстовыйДокумент.Записать(ИмяВремФайла);
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВремФайла);
		АдресХранилища	= ПоместитьВоВременноеХранилище(ДвоичныеДанные);
		Если АдресХранилища = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеФайла	= Новый ОписаниеПередаваемогоФайла(СтруктураСкрипта.Путь, АдресХранилища);
		МассивОписаний.Добавить(ОписаниеФайла);
	КонецЦикла;
	
	УдалитьФайлы(ИмяВремФайла);
	
	Возврат МассивОписаний;
	
КонецФункции

Функция ПолучитьТекстыСкриптов(МассивСкриптов) Экспорт
	
	ОбработкаМенеджер = Обработки.БазаЗнаний;
	
	МассивОписаний = Новый Массив;
	Для Каждого СтруктураСкрипта Из МассивСкриптов Цикл
		ТекстовыйДокумент = ОбработкаМенеджер.ПолучитьМакет(СтруктураСкрипта.Имя);
		Если ТипЗнч(ТекстовыйДокумент) <> Тип("ТекстовыйДокумент") Тогда
			Продолжить;
		КонецЕсли;
		
		ТелоСкрипта = ТекстовыйДокумент.ПолучитьТекст();
		МассивОписаний.Добавить(ТелоСкрипта);
	КонецЦикла;
	
	Возврат МассивОписаний;
	
КонецФункции
