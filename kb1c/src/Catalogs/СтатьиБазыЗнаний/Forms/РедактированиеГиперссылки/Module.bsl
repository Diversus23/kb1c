////////////////////////////////////////////////////////////////////////////////
// УПРАВЛЕНИЕ ФОРМОЙ

&НаКлиенте
Процедура НастроитьФорму()
	
	Элементы.АдресСсылки.Видимость			= (ЭтотОбъект.Тип = 1 ИЛИ ЭтотОбъект.Тип = 2);
	Элементы.СтатьяБазыЗнаний.Видимость		= (ЭтотОбъект.Тип = 0);
	Элементы.Область.Видимость				= (ЭтотОбъект.Тип = 0);
	Элементы.КатегорияБазыЗнаний.Видимость	= (ЭтотОбъект.Тип = 3);
	Элементы.СтрокаПоиска.Видимость			= (ЭтотОбъект.Тип = 4 ИЛИ ЭтотОбъект.Тип = 5);
	
	Если ЭтотОбъект.Тип = 2 Тогда
		Элементы.АдресСсылки.Заголовок = "E-mail";
	Иначе 
		Элементы.АдресСсылки.Заголовок = "URL";
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	НастроитьФорму();
	
	Если НЕ Элементы.АдресСсылки.Видимость Тогда
		ЭтотОбъект.АдресСсылки = "";
	КонецЕсли;
	Если НЕ Элементы.СтатьяБазыЗнаний.Видимость Тогда
		ЭтотОбъект.СтатьяБазыЗнаний = Неопределено;
	КонецЕсли;
	Если НЕ Элементы.Область.Видимость Тогда
		ЭтотОбъект.Область = "";
	КонецЕсли;
	Если НЕ Элементы.КатегорияБазыЗнаний.Видимость Тогда
		ЭтотОбъект.КатегорияБазыЗнаний = Неопределено;
	КонецЕсли;
	Если НЕ Элементы.СтрокаПоиска.Видимость Тогда
		ЭтотОбъект.СтрокаПоиска = "";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтатьяБазыЗнанийПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ЭтотОбъект.СтатьяБазыЗнаний) Тогда
		ЗаполнитьПодразделыНаСервере();
	Иначе 
		Элементы.Область.СписокВыбора.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияБазыЗнанийПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Гиперссылка = Неопределено Тогда
		ЭтотОбъект.Тип = 0;
	Иначе
		ОписаниеСсылки = ?(Параметры.Свойство("ОписаниеСсылки"), Параметры.ОписаниеСсылки, Неопределено);
		
		Если ОписаниеСсылки = Неопределено Тогда
			ЭтотОбъект.Тип = 0;
		Иначе
			ЭтотОбъект.Идентификатор	= ?(ОписаниеСсылки.Свойство("Идентификатор"), ОписаниеСсылки.Идентификатор, "");
			ЭтотОбъект.Тип				= ?(ОписаниеСсылки.Свойство("Тип"), ОписаниеСсылки.Тип, 0);
			ЭтотОбъект.Адрес			= ?(ОписаниеСсылки.Свойство("Адрес"), ОписаниеСсылки.Адрес, "");
			ЭтотОбъект.Открывать		= ?(ОписаниеСсылки.Свойство("Открывать"), ОписаниеСсылки.Открывать, 0);
			ЭтотОбъект.Подсказка		= ?(ОписаниеСсылки.Свойство("Подсказка"), ОписаниеСсылки.Подсказка, "");
			
			ИнициализацияДанныхСсылкиНаСервере();
		КонецЕсли;
	КонецЕсли;
	
	ЭтотОбъект.Представление = Параметры.Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастроитьФорму();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ЭтотОбъект.Тип = 0 Тогда
		ИдентификаторСтатьи = ПолучитьИдентификаторНаСервере(ЭтотОбъект.СтатьяБазыЗнаний);
		ЭтотОбъект.Адрес = ИдентификаторСтатьи + ?(ЗначениеЗаполнено(ЭтотОбъект.Область), "#", "") + ЭтотОбъект.Область;
	ИначеЕсли ЭтотОбъект.Тип = 1 Тогда
		ЭтотОбъект.Адрес = ЭтотОбъект.АдресСсылки;
	ИначеЕсли ЭтотОбъект.Тип = 2 Тогда
		ЭтотОбъект.Адрес = "mailto:" + ЭтотОбъект.АдресСсылки;
	ИначеЕсли ЭтотОбъект.Тип = 3 Тогда
		ЭтотОбъект.Адрес = ПолучитьИдентификаторНаСервере(ЭтотОбъект.КатегорияБазыЗнаний);
	ИначеЕсли ЭтотОбъект.Тип = 4 Тогда
		ЭтотОбъект.Адрес = ЭтотОбъект.СтрокаПоиска;
	ИначеЕсли ЭтотОбъект.Тип = 5 Тогда
		ЭтотОбъект.Адрес = ЭтотОбъект.СтрокаПоиска;
	КонецЕсли;
	
	ОписаниеСсылки = Новый Структура;
	ОписаниеСсылки.Вставить("Идентификатор"			, ЭтотОбъект.Идентификатор);
	ОписаниеСсылки.Вставить("Тип"					, ЭтотОбъект.Тип);
	ОписаниеСсылки.Вставить("Адрес"					, ЭтотОбъект.Адрес);
	ОписаниеСсылки.Вставить("Открывать"				, ЭтотОбъект.Открывать);
	ОписаниеСсылки.Вставить("Подсказка"				, ЭтотОбъект.Подсказка);
	ОписаниеСсылки.Вставить("Представление"			, ЭтотОбъект.Представление);
	ОписаниеСсылки.Вставить("Удалить"				, Ложь);
	ОписаниеСсылки.Вставить("Модифицированность"	, ЭтотОбъект.Модифицированность);	
	
	ЭтотОбъект.Закрыть(ОписаниеСсылки);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьГиперссылку(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьОчиститьГиперссылку", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, 
		НСтр("ru='Вы уверены что хотите очистить гиперссылку?'"),
		РежимДиалогаВопрос.ДаНет, ,
		КодВозвратаДиалога.Нет, ,
		КодВозвратаДиалога.Нет);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ МЕТОДЫ

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьСвойствоСтруктуры(Структура, Ключ, Значение)
	
	Если Структура.Свойство(Ключ) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Структура.Вставить(Ключ, Значение);
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ИнициализацияДанныхСсылкиНаСервере()
	
	Если ЭтотОбъект.Тип = 0 Тогда // статья
		Разделитель = Найти(ЭтотОбъект.Адрес, "#");
		Если Разделитель = 0 Тогда
			НайтиСтатьюПоИдентификаторуНаСервере(ЭтотОбъект.Адрес);
		Иначе
			ИдентификаторСтатьи = Лев(ЭтотОбъект.Адрес, Разделитель - 1);
			НайтиСтатьюПоИдентификаторуНаСервере(ИдентификаторСтатьи);
			Область = Сред(ЭтотОбъект.Адрес, Разделитель + 1);
		КонецЕсли;
	ИначеЕсли ЭтотОбъект.Тип = 1 Тогда // внешняя ссылка
		ЭтотОбъект.АдресСсылки = ЭтотОбъект.Адрес;
	ИначеЕсли ЭтотОбъект.Тип = 2 Тогда // e-mail
		ЭтотОбъект.АдресСсылки = СокрЛП(ЭтотОбъект.Адрес);
	ИначеЕсли ЭтотОбъект.Тип = 3 Тогда // категория
		НайтиКатегориюПоИдентификаторуНаСервере(ЭтотОбъект.Адрес);
	ИначеЕсли ЭтотОбъект.Тип = 4 Тогда // поиск по базе знаний
		ЭтотОбъект.СтрокаПоиска = ЭтотОбъект.Адрес;
	ИначеЕсли ЭтотОбъект.Тип = 5 Тогда
		ЭтотОбъект.СтрокаПоиска = ЭтотОбъект.Адрес;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Идентификатор) Тогда
		ЭтотОбъект.Идентификатор = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОчиститьГиперссылку(Результат, ДопПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСсылки = Новый Структура;
	ОписаниеСсылки.Вставить("Идентификатор"			, ЭтотОбъект.Идентификатор);
	ОписаниеСсылки.Вставить("Тип"					, 0);
	ОписаниеСсылки.Вставить("Адрес"					, "");
	ОписаниеСсылки.Вставить("Открывать"				, 0);
	ОписаниеСсылки.Вставить("Подсказка"				, "");
	ОписаниеСсылки.Вставить("Представление"			, "");
	ОписаниеСсылки.Вставить("Удалить"				, Истина);
	ОписаниеСсылки.Вставить("Модифицированность"	, Истина);	
	
	ЭтотОбъект.Закрыть(ОписаниеСсылки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИдентификаторНаСервере(ЗначениеСсылка)
	
	Возврат Строка(ЗначениеСсылка.УникальныйИдентификатор());
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПодразделыНаСервере()
	
	МассивЭлементов = Справочники.СтатьиБазыЗнаний.ПолучитьИерархиюОбластейСтатьи(ЭтотОбъект.СтатьяБазыЗнаний);
	
	Элементы.Область.СписокВыбора.Очистить();
	Для Каждого СтруктураДанных Из МассивЭлементов Цикл
		Элементы.Область.СписокВыбора.Добавить(СтруктураДанных.Область, СтруктураДанных.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НайтиСтатьюПоИдентификаторуНаСервере(ИдентификаторПоиска)
	
	Если ЭтотОбъект.Тип <> 0 ИЛИ НЕ ЗначениеЗаполнено(ИдентификаторПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСсылки = Новый УникальныйИдентификатор(ИдентификаторПоиска);
	ЭтотОбъект.СтатьяБазыЗнаний = Справочники.СтатьиБазыЗнаний.ПолучитьСсылку(ИдентификаторСсылки);
	
	Если ЗначениеЗаполнено(ЭтотОбъект.СтатьяБазыЗнаний) Тогда
		ЗаполнитьПодразделыНаСервере();
	КонецЕсли;
	
КонецПроцедуры
	
&НаСервере
Процедура НайтиКатегориюПоИдентификаторуНаСервере(ИдентификаторПоиска)
	
	Если ЭтотОбъект.Тип <> 3 ИЛИ НЕ ЗначениеЗаполнено(ИдентификаторПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСсылки = Новый УникальныйИдентификатор(ИдентификаторПоиска);
	ЭтотОбъект.КатегорияБазыЗнаний = Справочники.КатегорииБазыЗнаний.ПолучитьСсылку(ИдентификаторСсылки);
	
КонецПроцедуры