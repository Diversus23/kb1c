////////////////////////////////////////////////////////////////////////////////
// УПРАВЛЕНИЕ ФОРМОЙ

&НаКлиенте
Процедура УстановитьПометкуКнопокВыравнивание()
	
	Элементы.КартинкаСлеваТекстВокруг.Пометка			= (ЭтотОбъект.Выравнивание = 0);
	Элементы.КартинкаПоЦентруТекстСверхуСнизу.Пометка	= (ЭтотОбъект.Выравнивание = 2);
	Элементы.КартинкаСправаТекстВокруг.Пометка			= (ЭтотОбъект.Выравнивание = 1);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("АдресХранилища") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.АдресХранилища	= Параметры.АдресХранилища;
	ЭтотОбъект.Выравнивание 	= Параметры.Выравнивание;
	ЭтотОбъект.ВысотаКартинки 	= Параметры.Высота;
	ЭтотОбъект.Граница			= Параметры.Граница;
	ЭтотОбъект.Имя				= Параметры.Имя;
	ЭтотОбъект.Подпись			= Параметры.Подпись;
	ЭтотОбъект.Подсказка		= Параметры.Подсказка;
	
	ЭтотОбъект.ИдентификаторВладельца	= Параметры.Идентификатор;
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресХранилища) Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, ЭтотОбъект.ИдентификаторВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьПометкуКнопокВыравнивание();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Добавить("Имя");
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыбратьФайл(Команда)
	
	ОбъектКартинка = Новый Картинка;
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.Фильтр				= ОбъектКартинка.ФильтрИменФайлов();
	ДиалогВыбораФайла.МножественныйВыбор	= Ложь;
	ДиалогВыбораФайла.Заголовок				= "Выбор картинки";
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Истина;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		// Отображение картинки
		ДвоичныеДанные	= Новый ДвоичныеДанные(ДиалогВыбораФайла.ПолноеИмяФайла);
		АдресХранилища	= ПоместитьВоВременноеХранилище(ДвоичныеДанные, ЭтотОбъект.ИдентификаторВладельца);
		
		// Параметры
		Файл = Новый Файл(ДиалогВыбораФайла.ПолноеИмяФайла);
		Имя = Файл.ИмяБезРасширения;
		
		ЭтотОбъект.Модифицированность = Истина;
		ЭтотОбъект.ОбновитьОтображениеДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Просмотр(Команда)
	
	ИмяФайла = КаталогВременныхФайлов() + "pict_" + ЭтотОбъект.Имя;
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
	ДвоичныеДанные.Записать(ИмяФайла);
	
	ТекстКартинки = "<html>
	|" + БазаЗнанийCSSКлиентСервер.ПолучитьТаблицуСтилей() + "
	|<body>
	|	<div class='maintext'>
	|		<div class='pictureblock' %1>
	|			<a href='%2'><img src='%2' %3></a>
	|			" + ?(ПустаяСтрока(Подпись), "", "<br>" + Подпись) + "
	|		</div>
	|Здесь будет размещен текст статьи. Его положение относительно картинки зависит от параметров выравнивания.</div>
	|</body>
	|</html>";
	
	Строка_1 = БазаЗнанийКлиентСерверПовтИсп.ТекстHTML_Выравнивание(Выравнивание);
	Если Граница > 0 Тогда
		Строка_1 = Строка_1 + "; border:" + Формат(Граница, "ЧН=0; ЧГ=") + "px solid #666;";
	КонецЕсли;
	Строка_1 = "style='" + Строка_1 + "'"; 
	Строка_2 = ИмяФайла ;
	Строка_3 = "";
	Если ВысотаКартинки > 0 Тогда
		Строка_3 = Строка_3 + " height='" + Формат(ВысотаКартинки, "ЧГ=") + "px'";
	КонецЕсли;
	Если НЕ ПустаяСтрока(Подсказка) Тогда
		Строка_3 = Строка_3 + " title='" + Подсказка + "'";
	КонецЕсли;
	
	ТекстКартинки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстКартинки,
		Строка_1,
		Строка_2,
		Строка_3);
	
	ПараметрыФормы = Новый Структура("СтрокаHTML", ТекстКартинки);
	
	ОткрытьФорму("Справочник.СтатьиБазыЗнаний.Форма.ПросмотрHTML", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Результат = ЭтотОбъект.ПроверитьЗаполнение();
	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	Результат = БазаЗнанийКлиентСервер.ПроверитьПравильностьЗаполненияИмени(ЭтотОбъект.Имя);
	Если НЕ Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Имя может содержать только буквы и цифры, без пробелов и начинаться с буквы.");
		Возврат;
	КонецЕсли;
		
	Если НЕ Параметры.Имена.Найти(Имя) = Неопределено Тогда
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Картинка с именем ""%1"" уже существует. Задайте другое имя.'"),
			Имя);
			
		ПоказатьПредупреждение(, ТекстПредупреждения);
		
		Возврат;
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("АдресХранилища"		, ЭтотОбъект.АдресХранилища);
	ПараметрыЗакрытия.Вставить("Выравнивание"		, ЭтотОбъект.Выравнивание);
	ПараметрыЗакрытия.Вставить("Высота"				, ЭтотОбъект.ВысотаКартинки);
	ПараметрыЗакрытия.Вставить("Граница"			, ЭтотОбъект.Граница);
	ПараметрыЗакрытия.Вставить("Имя"				, ЭтотОбъект.Имя);
	ПараметрыЗакрытия.Вставить("Подпись"			, ЭтотОбъект.Подпись);
	ПараметрыЗакрытия.Вставить("Подсказка"			, ЭтотОбъект.Подсказка);
	ПараметрыЗакрытия.Вставить("Модифицированность"	, ЭтотОбъект.Модифицированность);
	
	ЭтаФорма.Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзменениеВыравнивание(Команда)
	
	Если Команда.Имя = "КартинкаСлеваТекстВокруг" Тогда
		ЭтотОбъект.Выравнивание		= 0;
	ИначеЕсли Команда.Имя = "КартинкаПоЦентруТекстСверхуСнизу" Тогда
		ЭтотОбъект.Выравнивание		= 2;
	ИначеЕсли Команда.Имя = "КартинкаСправаТекстВокруг" Тогда
		ЭтотОбъект.Выравнивание		= 1;
	КонецЕсли;
	
	УстановитьПометкуКнопокВыравнивание();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ МЕТОДЫ
