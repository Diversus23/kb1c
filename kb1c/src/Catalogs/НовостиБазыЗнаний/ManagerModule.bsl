Процедура ДополнитьВышестоящимиКатегориями(МассивКатегорий, КатегорияСсылка)
	РодительСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КатегорияСсылка, "Родитель");
	Если ЗначениеЗаполнено(РодительСсылка) Тогда
		Если МассивКатегорий.Найти(РодительСсылка) = Неопределено Тогда
			МассивКатегорий.Добавить(РодительСсылка);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьМассивКатегорийПоСтатье(СтатьяСсылка)
	
	МассивКатегорий = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СтатьяСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиБазыЗнанийКатегории.Категория,
	|	СтатьиБазыЗнанийКатегории.Категория.Родитель КАК Родитель
	|ИЗ
	|	Справочник.СтатьиБазыЗнаний.Категории КАК СтатьиБазыЗнанийКатегории
	|ГДЕ
	|	СтатьиБазыЗнанийКатегории.Ссылка = &Ссылка";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат МассивКатегорий;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если МассивКатегорий.Найти(Выборка.Категория) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		МассивКатегорий.Добавить(Выборка.Категория);
		
		Если ЗначениеЗаполнено(Выборка.Родитель) И МассивКатегорий.Найти(Выборка.Родитель) = Неопределено Тогда
			МассивКатегорий.Добавить(Выборка.Родитель);
			ДополнитьВышестоящимиКатегориями(МассивКатегорий, Выборка.Родитель);
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивКатегорий;
	
КонецФункции

Процедура ДополнитьПолучателейНовостиПоВидуИОбъекту(Получатели, ВидНовости, ОбъектБазыЗнаний)
	
	Если ТипЗнч(ОбъектБазыЗнаний) = Тип("СправочникСсылка.СтатьиБазыЗнаний") Тогда
		МассивКатегорий = ПолучитьМассивКатегорийПоСтатье(ОбъектБазыЗнаний);
	ИначеЕсли ТипЗнч(ОбъектБазыЗнаний) = Тип("СправочникСсылка.КомментарииБазыЗнаний") Тогда
		СтатьяВладелец	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектБазыЗнаний, "Владелец");
		МассивКатегорий	= ПолучитьМассивКатегорийПоСтатье(СтатьяВладелец);
	ИначеЕсли ТипЗнч(ОбъектБазыЗнаний) = Тип("СправочникСсылка.КатегорииБазыЗнаний") Тогда
		МассивКатегорий = Новый Массив;
		МассивКатегорий.Добавить(ОбъектБазыЗнаний);
		ДополнитьВышестоящимиКатегориями(МассивКатегорий, ОбъектБазыЗнаний);
		
		// Если добавили категорию на самый первый уровень тогда оповестим всех
		Если МассивКатегорий.Количество() = 1 И ВидНовости = Перечисления.ВидыНовостейБазыЗнаний.Создание Тогда
			Получатели.Добавить(Справочники.Пользователи.ПустаяСсылка());
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПеречислениеМенеджер = Перечисления.ВидыНовостейБазыЗнаний;
	
	СоответствиеТипов = Новый Соответствие;
	СоответствиеТипов.Вставить(ПеречислениеМенеджер.Создание	, "Создание");
	СоответствиеТипов.Вставить(ПеречислениеМенеджер.Изменение	, "Изменение");
	СоответствиеТипов.Вставить(ПеречислениеМенеджер.Удаление	, "Удаление");
	СоответствиеТипов.Вставить(ПеречислениеМенеджер.Оповещение	, "Оповещение");
	СоответствиеТипов.Вставить(ПеречислениеМенеджер.Прочее		, "Прочее");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	Запрос.УстановитьПараметр("Категории", МассивКатегорий);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИзбранноеБазыЗнаний.Пользователь
	|ИЗ
	|	РегистрСведений.ИзбранноеБазыЗнаний КАК ИзбранноеБазыЗнаний
	|ГДЕ
	|	ИзбранноеБазыЗнаний.КатегорияБазыЗнаний В(&Категории)
	|	И ИзбранноеБазыЗнаний.Пользователь <> &ТекущийПользователь
	|	И ИСТИНА";
	РеквизитыУсловия = СоответствиеТипов.Получить(ВидНовости);
	Если НЕ РеквизитыУсловия = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ИСТИНА", "И " + РеквизитыУсловия);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Получатели.Найти(Выборка.Пользователь) = Неопределено Тогда
			Получатели.Добавить(Выборка.Пользователь);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьОбъектНовостьБазыЗнаний(Автор, Заголовок, ОбъектБазыЗнаний)
	
	ДатаНовости = ТекущаяДатаСеанса();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заголовок"			, Заголовок);
	Запрос.УстановитьПараметр("НачалоДня"			, НачалоДня(ДатаНовости));
	Запрос.УстановитьПараметр("КонецДня"			, КонецДня(ДатаНовости));
	Запрос.УстановитьПараметр("ОбъектБазыЗнаний"	, ОбъектБазыЗнаний);
	Запрос.УстановитьПараметр("Автор"				, Автор);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НовостиБазыЗнаний.Ссылка
	|ИЗ
	|	Справочник.НовостиБазыЗнаний КАК НовостиБазыЗнаний
	|ГДЕ
	|	НовостиБазыЗнаний.Наименование = &Заголовок
	|	И НовостиБазыЗнаний.Дата МЕЖДУ &НачалоДня И &КонецДня
	|	И НовостиБазыЗнаний.ОбъектБазыЗнаний = &ОбъектБазыЗнаний
	|	И НовостиБазыЗнаний.Автор = &Автор";
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
	    Возврат Справочники.НовостиБазыЗнаний.СоздатьЭлемент();
	Иначе 
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.Ссылка.ПолучитьОбъект();
	КонецЕсли;
	
КонецФункции

// Автор - СправочникСсылка.Пользователи
// Заголовок - Строка
// ТекстHTML - Строка
// ОбъектБазыЗнаний - СправочникСсылка.КатегорииБазыЗнаний, СтатьиБазыЗнаний, КомментарииБазыЗнаний
// ВидНовости - ПеречислениеСсылка.ВидыНовостейБазыЗнаний
Функция ДобавитьНовость(знач Автор, знач Заголовок, знач ТекстHTML, знач ОбъектБазыЗнаний = Неопределено, знач ВидНовости = Неопределено, Получатели = Неопределено, знач Важность = 2, знач Текст = "") Экспорт 
	
	Если ПустаяСтрока(Текст) Тогда
		Текст = СтроковыеФункцииКлиентСервер.ИзвлечьТекстИзHTML(ТекстHTML);
	КонецЕсли;
	
	Если НЕ ТипЗнч(Получатели) = Тип("Массив") Тогда
		Получатели = Новый Массив;
	КонецЕсли;
	
	Если ВидНовости = Неопределено Тогда
		ВидНовости = Перечисления.ВидыНовостейБазыЗнаний.Прочее; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОбъектБазыЗнаний) Тогда
		ДополнитьПолучателейНовостиПоВидуИОбъекту(Получатели, ВидНовости, ОбъектБазыЗнаний);
	КонецЕсли;
	
	СоздаватьОбщуюЗапись = (ВидНовости = Перечисления.ВидыНовостейБазыЗнаний.Оповещение
		ИЛИ ВидНовости = Перечисления.ВидыНовостейБазыЗнаний.Прочее);

	Если Получатели.Количество() = 0 И СоздаватьОбщуюЗапись Тогда
		Получатели.Добавить(Справочники.Пользователи.ПустаяСсылка());
	КонецЕсли;
	
	Если Получатели.Количество() = 0 Тогда
		Возврат Справочники.НовостиБазыЗнаний.ПустаяСсылка();
	КонецЕсли;
	
	ЭлементОбъект = ПолучитьОбъектНовостьБазыЗнаний(Автор, Заголовок, ОбъектБазыЗнаний);
	ЭлементОбъект.Автор				= Автор;
	ЭлементОбъект.Наименование		= Заголовок;
	ЭлементОбъект.ТекстHTML			= ТекстHTML;
	ЭлементОбъект.Текст				= Текст;
	ЭлементОбъект.Дата				= ТекущаяДатаСеанса();
	ЭлементОбъект.ОбъектБазыЗнаний	= ОбъектБазыЗнаний;
	ЭлементОбъект.Важность			= Важность;	
	ЭлементОбъект.Вид				= ВидНовости;
	ЭлементОбъект.Записать();
	
	Для Каждого ПолучательСсылка Из Получатели Цикл
		
		НаборЗаписей = РегистрыСведений.ПолучателиНовостейБазыЗнаний.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Новость.Установить(ЭлементОбъект.Ссылка);
		НаборЗаписей.Отбор.Пользователь.Установить(ПолучательСсылка);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			ЗаписьРегистра = НаборЗаписей.Добавить();
			ЗаписьРегистра.Новость		= ЭлементОбъект.Ссылка;
			ЗаписьРегистра.Пользователь	= ПолучательСсылка;
		Иначе 
			ЗаписьРегистра = НаборЗаписей[0];
		КонецЕсли;
		
		ЗаписьРегистра.Просмотрено = Ложь;
		
		НаборЗаписей.Записать(Истина);
		
	КонецЦикла;
	
	Возврат ЭлементОбъект.Ссылка;
	
КонецФункции
