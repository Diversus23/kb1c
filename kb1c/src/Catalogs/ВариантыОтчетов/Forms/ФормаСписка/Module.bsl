////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВключаяПодчиненные = Истина;
	
	//КорневаяПодсистема = Справочники.ИдентификаторыОбъектовМетаданных.Подсистемы;
	ДеревоЗначений = ВариантыОтчетовПовтИсп.ПодсистемыТекущегоПользователя();
	ЗначениеВРеквизитФормы(ДеревоЗначений, "ДеревоПодсистем");
	
	ДеревоПодсистемТекущаяСтрока = -1;
	Элементы.ДеревоПодсистем.ТекущаяСтрока = 0;
	Если Параметры.РежимВыбора = Истина Тогда
		РежимРаботыФормы = "Выбор";
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ИначеЕсли Параметры.Свойство("РазделСсылка") Тогда
		РежимРаботыФормы = "ВсеОтчетыРаздела";
		МассивОбхода = Новый Массив;
		МассивОбхода.Добавить(ДеревоПодсистем.ПолучитьЭлементы()[0]);
		Пока МассивОбхода.Количество() > 0 Цикл
			СтрокиРодителя = МассивОбхода[0].ПолучитьЭлементы();
			МассивОбхода.Удалить(0);
			Для Каждого СтрокаДерева Из СтрокиРодителя Цикл
				Если СтрокаДерева.Ссылка = Параметры.РазделСсылка Тогда
					Элементы.ДеревоПодсистем.ТекущаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
					МассивОбхода.Очистить();
					Прервать;
				Иначе
					МассивОбхода.Добавить(СтрокаДерева);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Иначе
		РежимРаботыФормы = "Список";
		Элементы.Изменить.Отображение = ОтображениеКнопки.КартинкаИТекст;
		Элементы.РазместитьВРазделах.ТолькоВоВсехДействиях = Ложь;
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = РежимРаботыФормы;
	КлючНазначенияИспользования = РежимРаботыФормы;
	
	УстановитьСвойствоСпискаПоПараметруФормы("РежимВыбора");
	УстановитьСвойствоСпискаПоПараметруФормы("ВыборГруппИЭлементов");
	УстановитьСвойствоСпискаПоПараметруФормы("МножественныйВыбор");
	УстановитьСвойствоСпискаПоПараметруФормы("ТекущаяСтрока");
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Выбрать.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.Выбрать.Видимость = Ложь;
	КонецЕсли;
	
	ПолныеПраваНаВарианты = ВариантыОтчетов.ПолныеПраваНаВарианты();
	Если НЕ ПолныеПраваНаВарианты Тогда
		Элементы.ОтборТипОтчета.Видимость = Ложь;
	КонецЕсли;
	
	СписокВыбора = Элементы.ОтборТипОтчета.СписокВыбора;
	СписокВыбора.Добавить(1, НСтр("ru = 'Внутренние и Дополнительные'"));
	СписокВыбора.Добавить(Перечисления.ТипыОтчетов.Внутренний, НСтр("ru = 'Внутренние'"));
	СписокВыбора.Добавить(Перечисления.ТипыОтчетов.Дополнительный, НСтр("ru = 'Дополнительные'"));
	СписокВыбора.Добавить(Перечисления.ТипыОтчетов.Внешний, НСтр("ru = 'Внешние'"));
	
	Если Параметры.Отбор.Свойство("ТипОтчета", ОтборТипОтчета) Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ОтборТипОтчета", ОтборТипОтчета);
		Параметры.Отбор.Удалить("ТипОтчета");
	КонецЕсли;
	
	ОтчетыПользователя = Новый ФиксированныйМассив(ВариантыОтчетов.ОтчетыТекущегоПользователя());
	ОтключенныеВариантыПрограммы = Новый ФиксированныйМассив(ВариантыОтчетовПовтИсп.ОтключенныеВариантыПрограммы());
	
	ДеревоПодсистемУстановитьПараметрыСписка();
	
	Список.Параметры.УстановитьЗначениеПараметра("ТипВнутренний",       Перечисления.ТипыОтчетов.Внутренний);
	Список.Параметры.УстановитьЗначениеПараметра("ТипДополнительный",   Перечисления.ТипыОтчетов.Дополнительный);
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтборНаименование",  ОтборНаименование);
	Список.Параметры.УстановитьЗначениеПараметра("ОтборАвтор",         ОтборАвтор);
	Список.Параметры.УстановитьЗначениеПараметра("ОтборТипОтчета",     ОтборТипОтчета);
	
	ТекущийЭлемент = Элементы.Список;
	
	// Установка отбора динамического списка.
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ПометкаУдаления", Ложь, ВидСравненияКомпоновкиДанных.Равно, , ,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если РежимРаботыФормы = "ВсеОтчетыРаздела" ИЛИ РежимРаботыФормы = "Выбор" Тогда
		Элементы.ДеревоПодсистем.Развернуть(ДеревоПодсистемТекущаяСтрока, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта() Тогда
		ДеревоПодсистемТекущаяСтрока = -1;
		ПодключитьОбработчикОжидания("ДеревоПодсистемОбработчикАктивизацииСтроки", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	// Вызывается после загрузки данных из настроек в реквизиты формы
	
	ОтборНаименованиеСписокВыбора = Настройки.Получить("ОтборНаименованиеСписокВыбора");
	Если ТипЗнч(ОтборНаименованиеСписокВыбора) = Тип("Массив") Тогда
		Элементы.ОтборНаименование.СписокВыбора.ЗагрузитьЗначения(ОтборНаименованиеСписокВыбора);
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтборНаименование",  ОтборНаименование);
	Список.Параметры.УстановитьЗначениеПараметра("ОтборАвтор",         ОтборАвтор);
	Список.Параметры.УстановитьЗначениеПараметра("ОтборТипОтчета",     ОтборТипОтчета);
	//Список.Параметры.УстановитьЗначениеПараметра("ВключаяПодчиненные", ВключаяПодчиненные);
КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	Настройки.Вставить("ОтборНаименованиеСписокВыбора", Элементы.ОтборНаименование.СписокВыбора.ВыгрузитьЗначения());
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОтборТипОтчетаПриИзменении(Элемент)
	Список.Параметры.УстановитьЗначениеПараметра("ОтборТипОтчета", ОтборТипОтчета);
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипОтчетаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОтборТипОтчета = Неопределено;
	Список.Параметры.УстановитьЗначениеПараметра("ОтборТипОтчета", ОтборТипОтчета);
КонецПроцедуры

&НаКлиенте
Процедура ОтборНаименованиеПриИзменении(Элемент)
	Если ЗначениеЗаполнено(ОтборНаименование) Тогда
		СписокВыбора = Элементы.ОтборНаименование.СписокВыбора;
		ЭлементСписка = СписокВыбора.НайтиПоЗначению(ОтборНаименование);
		Если ЭлементСписка = Неопределено Тогда
			СписокВыбора.Вставить(0, ОтборНаименование);
			Если СписокВыбора.Количество() > 10 Тогда
				СписокВыбора.Удалить(10);
			КонецЕсли;
		Иначе
			Индекс = СписокВыбора.Индекс(ЭлементСписка);
			Если Индекс <> 0 Тогда
				СписокВыбора.Сдвинуть(Индекс, -Индекс);
			КонецЕсли;
		КонецЕсли;
		ТекущийЭлемент = Элементы.ОтборНаименование;
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ОтборНаименование", ОтборНаименование);
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторПриИзменении(Элемент)
	Список.Параметры.УстановитьЗначениеПараметра("ОтборАвтор", ОтборАвтор);
КонецПроцедуры

&НаКлиенте
Процедура ВключаяПодчиненныеПриИзменении(Элемент)
	//Список.Параметры.УстановитьЗначениеПараметра("ВключаяПодчиненные", ВключаяПодчиненные);
	ДеревоПодсистемТекущаяСтрока = -1;
	ПодключитьОбработчикОжидания("ДеревоПодсистемОбработчикАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ДеревоПодсистем

&НаКлиенте
Процедура ДеревоПодсистемПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодсистемПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодсистемПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодсистемПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ДеревоПодсистемОбработчикАктивизацииСтроки", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодсистемПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Размещение = Новый Структура("Варианты, Действие, Приемник, Источник"); //МассивВариантов, Всего, Представление
	Размещение.Варианты = Новый Структура("Массив, Всего, Представление");
	Размещение.Варианты.Массив = ПараметрыПеретаскивания.Значение;
	Размещение.Варианты.Всего  = ПараметрыПеретаскивания.Значение.Количество();
	
	Если Размещение.Варианты.Всего = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПриемник = ДеревоПодсистем.НайтиПоИдентификатору(Строка);
	Если СтрокаПриемник = Неопределено ИЛИ СтрокаПриемник.Приоритет = "" Тогда
		Возврат;
	КонецЕсли;
	
	Размещение.Приемник = Новый Структура("Ссылка, ПолноеПредставление");
	ЗаполнитьЗначенияСвойств(Размещение.Приемник, СтрокаПриемник);
	
	СтрокаИсточник = Элементы.ДеревоПодсистем.ТекущиеДанные;
	Размещение.Источник = Новый Структура("Ссылка, ПолноеПредставление");
	Если СтрокаИсточник = Неопределено ИЛИ СтрокаИсточник.Приоритет = "" Тогда
		Размещение.Действие = "Копирование";
	Иначе
		ЗаполнитьЗначенияСвойств(Размещение.Источник, СтрокаИсточник);
		Если ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Копирование Тогда
			Размещение.Действие = "Копирование";
		Иначе
			Размещение.Действие = "Перемещение";
		КонецЕсли;
	КонецЕсли;
	
	Если Размещение.Источник.Ссылка = Размещение.Приемник.Ссылка Тогда
		Предупреждение(НСтр("ru = 'Выбранные варианты отчетов уже в данном разделе.'"));
		Возврат;
	КонецЕсли;
	
	Если Размещение.Варианты.Всего = 1 Тогда
		Если Размещение.Действие = "Копирование" Тогда
			ШаблонВопроса = НСтр("ru = 'Разместить ""%1"" в ""%4""?'");
		Иначе
			ШаблонВопроса = НСтр("ru = 'Переместить ""%1"" из ""%3"" в ""%4""?'");
		КонецЕсли;
		Размещение.Варианты.Представление = Строка(Размещение.Варианты.Массив[0]);
	Иначе
		Размещение.Варианты.Представление = "";
		Для Каждого ВариантСсылка Из Размещение.Варианты.Массив Цикл
			Размещение.Варианты.Представление = Размещение.Варианты.Представление
			+ ?(Размещение.Варианты.Представление = "", "", ", ")
			+ Строка(ВариантСсылка);
			Если СтрДлина(Размещение.Варианты.Представление) > 23 Тогда
				Размещение.Варианты.Представление = Лев(Размещение.Варианты.Представление, 20) + "...";
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Размещение.Действие = "Копирование" Тогда
			ШаблонВопроса = НСтр("ru = 'Разместить варианты отчетов ""%1"" (%2 шт.) в ""%4""?'");
		Иначе
			ШаблонВопроса = НСтр("ru = 'Переместить варианты отчетов ""%1"" (%2 шт.) из ""%3"" в ""%4""?'");
		КонецЕсли;
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонВопроса,
		Размещение.Варианты.Представление,
		Формат(Размещение.Варианты.Всего, "ЧГ=0"),
		Размещение.Источник.ПолноеПредставление,
		Размещение.Приемник.ПолноеПредставление);
	Ответ = Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыполнения = РазместитьВариантыВПодсистеме(Размещение);
	
	ВариантыОтчетовКлиент.ОбновитьОткрытыеФормы();
	
	СтандартныеПодсистемыКлиент.ПоказатьРезультатВыполнения(ЭтаФорма, РезультатВыполнения);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Список

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если РежимРаботыФормы = "ВсеОтчетыРаздела" Тогда
		СтандартнаяОбработка = Ложь;
		ВариантыОтчетовКлиент.ОткрытьВариантОтчета(ЭтаФорма);
	ИначеЕсли РежимРаботыФормы = "Выбор" Тогда
		// Действие не требуется.
	Иначе
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ВариантыОтчетов.ФормаОбъекта", Новый Структура("Ключ", ВыбраннаяСтрока), ЭтаФорма, , ВариантОткрытияОкна.ОтдельноеОкно);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьСвойствоСпискаПоПараметруФормы(Ключ)
	
	Если Параметры.Свойство(Ключ) И ЗначениеЗаполнено(Параметры[Ключ]) Тогда
		Элементы.Список[Ключ] = Параметры[Ключ];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодсистемОбработчикАктивизацииСтроки()
	Если ДеревоПодсистемТекущаяСтрока <> Элементы.ДеревоПодсистем.ТекущаяСтрока Тогда
		ДеревоПодсистемУстановитьПараметрыСписка();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДеревоПодсистемУстановитьПараметрыСписка()
	ДеревоПодсистемТекущаяСтрока = Элементы.ДеревоПодсистем.ТекущаяСтрока;
	
	СтрокаДерева = ДеревоПодсистем.НайтиПоИдентификатору(ДеревоПодсистемТекущаяСтрока);
	Если СтрокаДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВсеПодсистемы = НЕ ЗначениеЗаполнено(СтрокаДерева.ПолноеИмя);
	
	Фрагмент1 = "Варианты.Ссылка.Отчет В(&ОтчетыПользователя)
	|	И НЕ Варианты.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)";
	Фрагмент2 = "Варианты.Ссылка В(&ВариантыПользователя)";
	
	Элементы.ВключаяПодчиненные.Доступность = НЕ ВсеПодсистемы;
	
	Если ВсеПодсистемы Тогда
		
		// Курсор установлен на строке дерева "Все разделы".
		// В качестве основных фильтров к ДС используется 2 массива:
		//   - включенные отчеты
		//   - отключенные варианты отчетов
		
		Если Найти(Список.ТекстЗапроса, Фрагмент2) <> 0 Тогда
			Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, Фрагмент2, Фрагмент1);
		КонецЕсли;
		
		Список.Параметры.УстановитьЗначениеПараметра("ОтчетыПользователя", ОтчетыПользователя);
		Список.Параметры.УстановитьЗначениеПараметра("ОтключенныеВариантыПрограммы", ОтключенныеВариантыПрограммы);
		
	Иначе
		
		// Курсор установлен на строке дерева, относящейся к конкретной подсистеме.
		// В качестве основного фильтра к ДС используется 1 массив:
		//   - варианты отчетов, доступные в этой подсистеме текущему пользователю.
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВариантыОтчетов.Ссылка,
		|	ПредопределенныеРазмещение.Подсистема
		|ПОМЕСТИТЬ втПредопределенные
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Размещение КАК ПредопределенныеРазмещение
		|		ПО (ПредопределенныеРазмещение.Подсистема В (&МассивПодсистем))
		|			И ВариантыОтчетов.ПредопределенныйВариант = ПредопределенныеРазмещение.Ссылка
		|			И (ВариантыОтчетов.Отчет В (&ОтчетыПользователя))
		|			И (НЕ ПредопределенныеРазмещение.Ссылка В (&ОтключенныеВариантыПрограммы))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВариантыРазмещение.Ссылка,
		|	ВариантыРазмещение.РазделИлиГруппа,
		|	ВариантыРазмещение.Использование
		|ПОМЕСТИТЬ втВарианты
		|ИЗ
		|	Справочник.ВариантыОтчетов.Размещение КАК ВариантыРазмещение
		|ГДЕ
		|	ВариантыРазмещение.РазделИлиГруппа В(&МассивПодсистем)
		|	И ВариантыРазмещение.Ссылка.Отчет В(&ОтчетыПользователя)
		|	И НЕ ВариантыРазмещение.Ссылка.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(втПредопределенные.Ссылка, втВарианты.Ссылка) КАК Ссылка
		|ИЗ
		|	втПредопределенные КАК втПредопределенные
		|		ПОЛНОЕ СОЕДИНЕНИЕ втВарианты КАК втВарианты
		|		ПО втПредопределенные.Ссылка = втВарианты.Ссылка
		|			И втПредопределенные.Подсистема = втВарианты.РазделИлиГруппа
		|ГДЕ
		|	(втВарианты.Использование = ИСТИНА
		|			ИЛИ втВарианты.Использование ЕСТЬ NULL )";
		
		Запрос = Новый Запрос;
		Если ВключаяПодчиненные Тогда
			МассивПодсистем = Новый Массив;
			МассивПодсистем.Добавить(СтрокаДерева.Ссылка);
			ДобавитьРекурсивно(МассивПодсистем, СтрокаДерева.ПолучитьЭлементы());
			Запрос.УстановитьПараметр("МассивПодсистем", МассивПодсистем);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "В (&МассивПодсистем)", "= &Подсистема");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "В(&МассивПодсистем)", "= &Подсистема");
			Запрос.УстановитьПараметр("Подсистема", СтрокаДерева.Ссылка);
		КонецЕсли;
		Запрос.УстановитьПараметр("ОтчетыПользователя", ОтчетыПользователя);
		Запрос.УстановитьПараметр("ОтключенныеВариантыПрограммы", ОтключенныеВариантыПрограммы);
		Запрос.Текст = ТекстЗапроса;
		
		ВариантыПользователя = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		Если Найти(Список.ТекстЗапроса, Фрагмент1) <> 0 Тогда
			Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, Фрагмент1, Фрагмент2);
		КонецЕсли;
		
		Список.Параметры.УстановитьЗначениеПараметра("ВариантыПользователя", ВариантыПользователя);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Список.ОсновнаяТаблица) Тогда
		Список.ОсновнаяТаблица = "Справочник.ВариантыОтчетов";
		Список.ДинамическоеСчитываниеДанных = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРекурсивно(МассивПодсистем, КоллекцияСтрокДерева)
	Для Каждого СтрокаДерева Из КоллекцияСтрокДерева Цикл
		МассивПодсистем.Добавить(СтрокаДерева.Ссылка);
		ДобавитьРекурсивно(МассивПодсистем, СтрокаДерева.ПолучитьЭлементы());
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция РазместитьВариантыВПодсистеме(Размещение)
	Размещено = 0;
	ТекстОшибок = "";
	НачатьТранзакцию();
	Для Каждого ВариантСсылка Из Размещение.Варианты.Массив Цикл
		Если ВариантСсылка.ТипОтчета = Перечисления.ТипыОтчетов.Внешний Тогда
			ТекстОшибок = ТекстОшибок + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '""%1"" внешний.'"),
				Строка(ВариантСсылка)
			) + Символы.ПС;
			Продолжить;
		ИначеЕсли ВариантСсылка.ПометкаУдаления Тогда
			ТекстОшибок = ТекстОшибок + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '""%1"" помечен на удаление.'"),
				Строка(ВариантСсылка)
			) + Символы.ПС;
			Продолжить;
		КонецЕсли;
		
		ВариантОбъект = ВариантСсылка.ПолучитьОбъект();
		
		СтрокаПриемник = ВариантОбъект.Размещение.Найти(Размещение.Приемник.Ссылка, "РазделИлиГруппа");
		
		Если Размещение.Действие = "Перемещение" Тогда
			СтрокаИсточник = ВариантОбъект.Размещение.Найти(Размещение.Источник.Ссылка, "РазделИлиГруппа");
			Если СтрокаИсточник = Неопределено Тогда
				// Действие не требуется
			ИначеЕсли СтрокаПриемник = Неопределено Тогда
				// Замена подсистемы
				СтрокаИсточник.РазделИлиГруппа = Размещение.Приемник.Ссылка;
				СтрокаПриемник = СтрокаИсточник;
			Иначе
				// Удаление строки
				ВариантОбъект.Размещение.Удалить(СтрокаИсточник);
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаПриемник = Неопределено Тогда
			СтрокаПриемник = ВариантОбъект.Размещение.Добавить();
			СтрокаПриемник.РазделИлиГруппа = Размещение.Приемник.Ссылка;
		КонецЕсли;
		
		СтрокаПриемник.Использование = Истина;
		
		Размещено = Размещено + 1;
		ВариантОбъект.Записать();
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
	
	РезультатВыполнения = Новый Структура;
	Если Размещение.Варианты.Всего = Размещено Тогда
		РезультатВыполнения.Вставить("ВыводОповещения", Новый Структура("Использование, Заголовок, Текст, Ссылка, Картинка"));
		ВыводОповещения = РезультатВыполнения.ВыводОповещения;
		ВыводОповещения.Использование = Истина;
		Если Размещение.Варианты.Всего = 1 Тогда
			ВыводОповещения.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Вариант отчета ""%1"" размещен в ""%2"".'"),
				Размещение.Варианты.Представление,
				Размещение.Приемник.ПолноеПредставление);
			ВыводОповещения.Текст = Размещение.Варианты.Представление;
			ВыводОповещения.Ссылка = ПолучитьНавигационнуюСсылку(Размещение.Варианты.Массив[0]);
		Иначе
			ВыводОповещения.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Варианты отчетов ""%1"" (%2 шт.) размещены в ""%3"".'"),
				Размещение.Варианты.Представление,
				Формат(Размещение.Варианты.Всего, "ЧН=0; ЧГ=0"),
				Размещение.Приемник.ПолноеПредставление);
		КонецЕсли;
	Иначе
		РезультатВыполнения.Вставить("ВыводПредупреждения", Новый Структура("Использование, Текст, ТекстОшибок"));
		ВыводПредупреждения = РезультатВыполнения.ВыводПредупреждения;
		ВыводПредупреждения.Использование = Ложь;
		ВыводПредупреждения.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Размещено вариантов отчетов: %1 из %2'"), 
			Формат(Размещено, "ЧН=0; ЧГ=0"),
			Формат(Размещение.Варианты.Всего, "ЧН=0; ЧГ=0"));
		ВыводПредупреждения.ТекстОшибок = НСтр("ru = 'Варианты отчетов, которые не могут размещаться в командном интерфейсе:'")
		+ Символы.ПС
		+ ТекстОшибок;
	КонецЕсли;
	
	Если Размещение.Действие = "Перемещение" И Размещено > 0 Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
КонецФункции

