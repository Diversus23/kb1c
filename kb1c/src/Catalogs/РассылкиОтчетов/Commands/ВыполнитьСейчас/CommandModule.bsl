////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаКлиенте
Процедура ОбработкаКоманды(МассивРассылок, Параметры)
	Если ТипЗнч(МассивРассылок) <> Тип("Массив") ИЛИ МассивРассылок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЭтаФорма = Параметры.Источник;
	
	ПредварительныеНастройки = Неопределено;
	Если ЭтаФорма.ИмяФормы = "Справочник.РассылкиОтчетов.Форма.ФормаЭлемента" Тогда
		ТекстСообщения = НСтр("ru = 'Выполняется рассылка отчетов'");
		
		Объект = ЭтаФорма.Объект;
		Если НЕ Объект.Подготовлена Тогда
			Предупреждение(НСтр("ru = 'Рассылка не подготовлена'"));
			Возврат;
		КонецЕсли;
		
		Если Объект.ИспользоватьЭлектроннуюПочту Тогда
			Получатели = РассылкаОтчетовКлиент.ВыбратьПолучателя(Объект, Истина, Истина);
			Если Получатели = Неопределено Тогда
				Возврат;
			КонецЕсли;
			ПредварительныеНастройки = Новый Структура("Получатели", Получатели);
		КонецЕсли;
	Иначе
		ТекстСообщения = НСтр("ru = 'Выполняются рассылки отчетов'");
	КонецЕсли;
	
	Состояние(ТекстСообщения, , , );
	
	ПараметрыВызоваСервера = Новый Структура("МассивРассылок, ПредварительныеНастройки",
		МассивРассылок,
		ПредварительныеНастройки);
	
	Результат = ВыполнитьРассылкиВФоновомЗадании(ПараметрыВызоваСервера, ЭтаФорма.УникальныйИдентификатор);
	
	Если Результат.Статус = "ВыполненоУспешно" Тогда
		РассылкаОтчетовКлиент.ПоказатьРезультат(Результат.Детали);
	ИначеЕсли Результат.Статус = "Исключение" Тогда
		Предупреждение(
			НСтр("ru = 'Рассылки не выполнены из-за ошибки,
			|Подробности см. в журнале регистрации.'"));
	ИначеЕсли Результат.Статус = "Выполняется" Тогда
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, Результат.Детали.ИдентификаторЗадания);
		
		ПараметрыОбработчика = Неопределено;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		
		ЭтаФорма.ПараметрыФоновогоЗадания.Очистить();
		ЭтаФорма.ПараметрыФоновогоЗадания.Добавить(Результат.Детали.ИдентификаторЗадания);
		ЭтаФорма.ПараметрыФоновогоЗадания.Добавить(Результат.Детали.АдресХранилища);
		ЭтаФорма.ПараметрыФоновогоЗадания.Добавить(ПараметрыОбработчика);
		ЭтаФорма.ПараметрыФоновогоЗадания.Добавить(ФормаДлительнойОперации);
		
		ЭтаФорма.ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеФоновогоЗадания", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ВыполнитьРассылкиВФоновомЗадании(ПараметрыВызоваСервера, Знач УникальныйИдентификатор)
	Результат = Новый Структура("Статус, Детали");
	
	Попытка
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			РезультатФоновогоЗадания = Новый Структура("ЗаданиеВыполнено, АдресХранилища");
			РезультатФоновогоЗадания.ЗаданиеВыполнено = Истина;
			РезультатФоновогоЗадания.АдресХранилища   = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
			
			РассылкаОтчетов.ВыполнитьРассылкиВФоновомЗадании(ПараметрыВызоваСервера, РезультатФоновогоЗадания.АдресХранилища);
		Иначе
			РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
				УникальныйИдентификатор,
				"РассылкаОтчетов.ВыполнитьРассылкиВФоновомЗадании", 
				ПараметрыВызоваСервера, 
				НСтр("ru = 'Рассылки отчетов: Выполнение рассылок в фоне'"));
		КонецЕсли;
		
		Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
			Результат.Статус = "ВыполненоУспешно"; // Не локализуется
			Результат.Детали = ПолучитьИзВременногоХранилища(РезультатФоновогоЗадания.АдресХранилища);
		Иначе
			Результат.Статус = "Выполняется"; // Не локализуется
			Результат.Детали = Новый Структура("ИдентификаторЗадания, АдресХранилища");
			ЗаполнитьЗначенияСвойств(Результат.Детали, РезультатФоновогоЗадания);
		КонецЕсли;
	Исключение
		Результат.Статус = "Исключение"; // Не локализуется
	КонецПопытки;
	
	Возврат Результат;
КонецФункции
